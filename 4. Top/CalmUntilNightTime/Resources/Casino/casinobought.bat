CALL :CALCULATEMONEY
CALL :COLLECT
CALL :MENU
GOTO :EOF

:MENU
CLS
ECHO.
ECHO %ACCOUNTANTNAME%: Would you like to invest in your casino? You currently have %PLAYERGIL% Gil
ECHO.
ECHO !SPACES%ACCOUNTANTSPACES%!  You currently generate %CASINOINCOMEPERCHUNK% gil every 15 mins
ECHO !SPACES%ACCOUNTANTSPACES%!  Your casino can currently hold a maximum of %CASINOVAULTCAPACITY% gil
ECHO.
ECHO. 1 - Buy Roulette tables      (Costs %ROULETTECOST% Gil, Generates +%ROULETTEINCOME% every 15 mins)
ECHO. 2 - Buy Blackjack tables     (Costs %BLACKJACKCOST% Gil, Generates +%BLACKJACKINCOME% every 15 mins)
ECHO. 3 - Buy Slot machines        (Costs %SLOTSCOST% Gil, Generates +%SLOTSINCOME% every 15 mins)
ECHO. 4 - Upgrade the casino vault (Costs %VAULTCOST% Gil, Vault capacity +%VAULTCAPACITYINCREASE% Gil)
ECHO. 5 - Can I just play them for fun?
ECHO. 6 - So how does this all this work?
ECHO. 0 - No thanks
ECHO.
SET /P CHOICE=
ECHO.
IF "%CHOICE%" EQU "1" (
	SET BUYVAR=ROULETTE
	CALL :BUY1
) ELSE IF "%CHOICE%" EQU "2" ( 
	SET BUYVAR=BLACKJACK
	CALL :BUY1
) ELSE IF "%CHOICE%" EQU "3" (
	SET BUYVAR=SLOTS
	CALL :BUY1
) ELSE IF "%CHOICE%" EQU "4" (
	CALL :BUYVAULT
) ELSE IF "%CHOICE%" EQU "5" (
	SET /a PLAYGIL = 1000
	CALL :PLAY
) ELSE IF "%CHOICE%" EQU "6" (
	CALL :EXPLANATION
	CALL :WAITFORTWO
	GOTO :MENU
) ELSE IF "%CHOICE%" EQU "0" (
	CALL "%REMOTERESOURCEPATH%\casino.bat"
	GOTO :EOF
)
GOTO :MENU

:EXPLANATION
CLS.
ECHO.
ECHO %ACCOUNTANTNAME%: The Casino will generate gil every 15 minutes, even when you're not playing^^!
ECHO.
ECHO !SPACES%ACCOUNTANTSPACES%!  The generated income will max-out after 24 hours without collection, 
ECHO !SPACES%ACCOUNTANTSPACES%!  or when you reach the maximum vault capacity.
ECHO.
ECHO !SPACES%ACCOUNTANTSPACES%!  You increase the maximum vault capacity, and the income rate by investing
ECHO !SPACES%ACCOUNTANTSPACES%!  gil in the casino.
ECHO.
pause
GOTO :EOF

:BUY1
CLS
ECHO.
IF %PLAYERGIL% LSS !%BUYVAR%COST! (
	ECHO %ACCOUNTANTNAME%: You don't have enough money for this
	ECHO.
	CALL :WAITFORTWO
	GOTO :EOF
)
SET /a PLAYERGIL = %PLAYERGIL% - !%BUYVAR%COST!
SET /a CASINOINCOMEPERCHUNK = %CASINOINCOMEPERCHUNK% + !%BUYVAR%INCOME!
ECHO %ACCOUNTANTNAME%: Sorted^^!
ECHO.
CALL :WAITFORTWO
GOTO :EOF

:BUYVAULT
CLS
ECHO.
IF %PLAYERGIL% LSS %VAULTCOST% (
	ECHO %ACCOUNTANTNAME%: You don't have enough money for this
	ECHO.
	CALL :WAITFORTWO
	GOTO :EOF
)
SET /a PLAYERGIL = %PLAYERGIL% - %VAULTCOST%
SET /a CASINOVAULTCAPACITY = %CASINOVAULTCAPACITY% + %VAULTCAPACITYINCREASE%
ECHO %ACCOUNTANTNAME%: Sorted^^!
ECHO.
CALL :WAITFORTWO
GOTO :EOF

:PLAY
SET GILVAR=PLAYGIL
CLS
ECHO. 
ECHO %ACCOUNTANTNAME%: What would you like to play?
ECHO. 
ECHO. 1 - I want to play Roulette
ECHO. 2 - I want to play Blackjack
ECHO. 3 - I want to play The Slots
ECHO. 0 - None thank you
ECHO. 
:CASINOCHOICE
SET /P CHOICE=
ECHO.
IF "%CHOICE%" EQU "1" (
	CALL "%CASINORESOURCEPATH%\roulette.bat"
) ELSE IF "%CHOICE%" EQU "2" (
	CALL "%CASINORESOURCEPATH%\blackjack.bat"
) ELSE IF "%CHOICE%" EQU "3" (
	CALL "%CASINORESOURCEPATH%\slots.bat"
) ELSE IF "%CHOICE%" EQU "0" (
	SET /a TOTALCASINOPROFIT = %ROULETTEPROFIT%+%BLACKJACKPROFIT%+%SLOTMACHINEPROFIT%
	GOTO :EOF
) ELSE (
	GOTO :CASINOCHOICE
)
GOTO :PLAY


:COLLECT
CLS
ECHO.
IF %PLAYERDOUBLECASINO% EQU 1 (
	ECHO Your special ability gives you more money^^!
	ECHO.
	CALL :WAITFORTWO
)
ECHO You had %PLAYERGIL% Gil
ECHO.
ECHO Your casino has generated %MONEY% Gil^^!
ECHO.
SET /a PLAYERGIL = %PLAYERGIL%+%MONEY%
ECHO You now have %PLAYERGIL% Gil^^!
ECHO.
SET /a MONEY = 0
SET /a CHUNKS = 0
SET PREFIX=CASINO
CALL "%MISCRESOURCEPATH%\calculatedatetime.bat"
pause
GOTO :EOF



:CALCULATEMONEY
SET PREFIX=NOW
CALL "%MISCRESOURCEPATH%\calculatedatetime.bat"
SET /a YEARDIFF = %NOWYEAR%-%CASINOYEAR%
SET /a MONTHDIFF = %NOWMONTH%-%CASINOMONTH%
SET /a DAYDIFF = %NOWDAY%-%CASINODAY%
SET /a HOURDIFF = %NOWHOUR%-%CASINOHOUR%
SET /a MINDIFF = %NOWMIN%-%CASINOMIN%
SET /a CHUNKS = 96
IF %YEARDIFF% EQU 0 (
	IF %MONTHDIFF% EQU 0 (
		IF %DAYDIFF% EQU 1 (
			CALL :DAYDIFF1
		) ELSE IF %DAYDIFF% EQU 0 (
			CALL :DAYDIFF0
		)
	)
)
SET /a MONEY = %CASINOINCOMEPERCHUNK%*%CHUNKS%
IF %PLAYERDOUBLECASINO% EQU 1 (
	CALL :MOREMONEY
)
IF NOT DEFINED MONEY (
	SET /a MONEY = 0
)
IF %MONEY% GTR %CASINOVAULTCAPACITY% (
	SET /a MONEY = %CASINOVAULTCAPACITY%
)
GOTO :EOF

:MOREMONEY
SET /a EXTRAMONEY = (%MONEY%*%PLAYERBASELCK%)/100
SET /a MONEY = %MONEY% + %EXTRAMONEY%
GOTO :EOF

:DAYDIFF1
IF %HOURDIFF% GTR 0 (
	GOTO :EOF
)
SET /a HOURCHUNK = (24+%HOURDIFF%)*4
SET /a CHUNKS = %HOURCHUNK%
CALL :MINDIFF1
GOTO :EOF


:MINDIFF1
SET /a MINCHUNK = (%MINDIFF%/15)
IF %MINDIFF% LSS 0 (
	SET /a MINCHUNK = %MINCHUNK%-1
)
SET /a CHUNKS = %CHUNKS%+%MINCHUNK%
GOTO :EOF

:DAYDIFF0
SET /a HOURCHUNK = %HOURDIFF%*4
SET /a CHUNKS = %HOURCHUNK%
CALL :MINDIFF0
GOTO :EOF

:MINDIFF0
SET /a MINCHUNK = %MINDIFF%/15
SET /a CHUNKS = %CHUNKS%+%MINCHUNK%
GOTO :EOF


:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF


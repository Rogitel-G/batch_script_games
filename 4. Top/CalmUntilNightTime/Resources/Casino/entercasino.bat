IF %REMOTEACCESS% EQU 0 (
	CALL :SETLOCATION
) ELSE (
	SET /a REMOTEACCESS = 0
)
IF %STORY14VERSIONNUM% EQU 0 (
	SET /a STORY14VERSIONNUM = 2
)
SET /a MINIGAMEGAMBLE = 0
IF %CASINOBOUGHT% EQU 1 (
	CALL "%CASINORESOURCEPATH%\casinobought.bat"
	GOTO :EOF
) ELSE IF %TOTALCASINOPROFIT% GTR %CASINOTHRESHOLD% (
	CALL "%CASINORESOURCEPATH%\casinobattle.bat"
	GOTO :EOF
)
IF %CASINOWELCOMESEEN% EQU 0 (
	CALL "%CASINORESOURCEPATH%\casinowelcome.bat"
)
:CASINOWELCOME
SET /a TOTALCASINOPROFIT = %ROULETTEPROFIT%+%BLACKJACKPROFIT%+%SLOTMACHINEPROFIT%
CALL :BOUNCERQUERY
IF %BOUNCERALIVE% EQU 1 (
	CALL :BOUNCERCONVERSATION
	GOTO :EOF
)
SET GILVAR=PLAYERGIL
CALL "%CASINORESOURCEPATH%\casinodisplay.bat"
ECHO.
ECHO %CASINOOWNERNAME%: Welcome to the Three Feathers Casino. May luck run through you like the spirit of the buffalo
ECHO. 
ECHO. 1 - I want to play Roulette
ECHO. 2 - I want to play Blackjack
ECHO. 3 - I want to play The Slots
ECHO. 0 - No thanks, I'm off
ECHO. 
:CASINOCHOICE
SET /P CASINOCHOICE=
ECHO.
IF "%CASINOCHOICE%" EQU "1" (
	CALL "%CASINORESOURCEPATH%\roulette.bat"
) ELSE IF "%CASINOCHOICE%" EQU "2" (
	CALL "%CASINORESOURCEPATH%\blackjack.bat"
) ELSE IF "%CASINOCHOICE%" EQU "3" (
	CALL "%CASINORESOURCEPATH%\slots.bat"
) ELSE IF "%CASINOCHOICE%" EQU "0" (
	ECHO %CASINOOWNERNAME%: Ok bye...
	ECHO.
	pause
	SET /a TOTALCASINOPROFIT = %ROULETTEPROFIT%+%BLACKJACKPROFIT%+%SLOTMACHINEPROFIT%
	GOTO :EOF
) ELSE (
	GOTO :CASINOCHOICE
)
GOTO :CASINOWELCOME


:SETLOCATION
SET /a PLAYERXCOORD = 11
SET /a PLAYERYCOORD = 6
GOTO :EOF


:BOUNCERQUERY
IF %BEATBOUNCER% EQU 1 (
	GOTO :EOF
) ELSE IF %BOUNCERALIVE% EQU 0 (
	IF %TOTALCASINOPROFIT% GTR %CASINOTHRESHOLD% (
		SET /a BOUNCERALIVE = 1
	)
)
GOTO :EOF

:BOUNCERCONVERSATION
CLS
ECHO.
ECHO %BOUNCERNAME%: I think you've won enough sunshine.
ECHO.
CALL :WAITFORTWO
ECHO %BOUNCERSPACES%  I'll just see you out. Don't let me see you around here again.
ECHO.
pause
SET /a STORY16VERSIONNUM = 2
GOTO :EOF


:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF

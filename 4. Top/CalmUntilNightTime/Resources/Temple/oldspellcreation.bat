SET /a MPREDUCTIONPOINTCOST = 50
CALL %TEMPLERESOURCEPATH%\availablespellcustomisations.bat"
REM Need to make MP red if they don't have enough MP
REM Same for Gil
REM Also limit the spell creation by max MP
CLS
CALL :INITIALISEVARIABLES
CALL :INITIALISEMPCOST
CALL :INITIALISEGILCOST
:SPELLSLOTSET
CALL :SPELLSLOT
IF %SPELLSLOTNUM% EQU 0 (
	SET SPELLNAME=0
	GOTO :EOF
)
:NAMESPELLSET
CALL :NAMESPELL
IF %SPELLNAME% EQU 0 (
	GOTO :EOF
)
:TARGETSET
CALL :TARGET
IF %TARGETBACK% EQU 1 (
	GOTO :NAMESPELLSET
)
:DAMAGESET
IF %TARGET% NEQ PLAYER (
	SET /a DAMAGEMPCOST = 0
	SET /a DAMAGEGILCOST = 0
	SET /a DAMAGETRUE = 0
	CALL :DAMAGE
)
IF %DAMAGEBACK% EQU 1 (
	GOTO :TARGETSET
)
:DAMAGEVALUESET
IF %TARGET% NEQ PLAYER (
	SET /a DAMAGEMPCOST = 0
	SET /a DAMAGEGILCOST = 0
	IF %DAMAGETRUE% EQU 1 (
		CALL :DAMAGEVALUE
	)
)
IF %DAMAGEVALUEBACK% EQU 1 (
	GOTO :DAMAGESET
)
:STATUSSET
IF %TARGET% NEQ PLAYER (
	SET /a STATUSMPCOST = 0
	SET /a STATUSGILCOST = 0
	SET /a STATUSTRUE = 0
	CALL :STATUS
)
IF %STATUSBACK% EQU 1 (
	IF %DAMAGETRUE% EQU 1 (
		GOTO :DAMAGEVALUESET
	) ELSE (
		GOTO :DAMAGESET
	)
)
IF %TARGET% NEQ PLAYER (
	IF %STATUSTRUE% EQU 1 (
		CALL :STATUSVALUE
	)
)
:DISPELSET
IF %TARGET% NEQ PLAYER (
	SET /a DISPELMPCOST = 0
	SET /a DISPELGILCOST = 0
	SET /a DISPEL = 0
	CALL :DISPEL
)
IF %DISPELBACK% EQU 1 (
	GOTO :STATUSSET
)
IF %TARGET% EQU ENEMY (
	GOTO :ACCURACYSET
)
:CURESET
IF %TARGET% NEQ ENEMY (
	SET /a CUREMPCOST = 0
	SET /a CUREGILCOST = 0
	SET /a CURETRUE = 0
	CALL :CURE
)
IF %CUREBACK% EQU 1 (
	IF %TARGET% EQU PLAYER (
		GOTO :TARGETSET
	) ELSE (
		GOTO :DISPELSET
	)
)
:CUREVALUESET
IF %TARGET% NEQ ENEMY (
	IF %CURETRUE% EQU 1 (
		SET /a CUREMPCOST = 0
		SET /a CUREGILCOST = 0
		CALL :CUREVALUE
	)
)
IF %CUREVALUEBACK% EQU 1 (
	GOTO :CURESET
)
:STATUSCURESET
IF %TARGET% NEQ ENEMY (
	SET /a STATUSCUREMPCOST = 0
	SET /a STATUSCUREGILCOST = 0
	SET /a STATUSCURETRUE = 0
	CALL :STATUSCURE
)
IF %STATUSCUREBACK% EQU 1 (
	IF %CURETRUE% EQU 1 (
		GOTO :CUREVALUESET
	) ELSE (
		GOTO :CURESET
	)
)
:SUPPORTSET
IF %TARGET% NEQ ENEMY (
	SET /a SUPPORTMPCOST = 0
	SET /a SUPPORTGILCOST = 0
	SET /a SUPPORTTRUE = 0
	CALL :SUPPORT
)
IF %SUPPORTBACK% EQU 1 (
	GOTO :STATUSCURESET
)
:SUPPORTVALUESET
IF %TARGET% NEQ ENEMY (
	IF %SUPPORTTRUE% EQU 1 (
		CALL :SUPPORTVALUE
	)
)
:ACCURACYSET
CALL :ACCURACY
IF %ACCURACYBACK% EQU 1 (
	IF %TARGET% EQU ENEMY (
		GOTO :DISPELSET
	) ELSE (
		GOTO :SUPPORTSET
	)
)

:MPREDUCTIONSET
CALL :MPREDUCTION
IF %MPREDUCTIONBACK% EQU 1 (
	GOTO :ACCURACYSET
)
:CONFIRMATIONSET
CALL :CONFIRMATION
IF %CONFIRMATIONBACK% EQU 1 (
	GOTO :MPREDUCTIONSET
)
GOTO :EOF


:SPELLSLOT
ECHO.
SET /a AVAILABLESPELLSLOTS = %PLAYERLVL%/5
IF %AVAILABLESPELLSLOTS% GTR 9 (
	SET /a AVAILABLESPELLSLOTS = 9
)
IF EXIST "%CUSTOMSPELLRESOURCEPATH%\Temp\customspelllist.bat" (
	CALL "%CUSTOMSPELLRESOURCEPATH%\Temp\customspelllist.bat"
)
IF %AVAILABLESPELLSLOTS% EQU 0 (
	ECHO %PRIESTNAME%: Sorry you're not a high enough level to create spells yet...
	ECHO.
	SET /a SPELLSLOTNUM = 0
	pause
	GOTO :EOF
) ELSE IF %AVAILABLESPELLSLOTS% EQU 1 (
	ECHO %PRIESTNAME%: You can maintain %AVAILABLESPELLSLOTS% custom spell
) ELSE IF %AVAILABLESPELLSLOTS% GEQ 2 (	
	ECHO %PRIESTNAME%: You can maintain %AVAILABLESPELLSLOTS% custom spells
)
ECHO.
ECHO           Select a spell slot
ECHO.
IF %AVAILABLESPELLSLOTS% GEQ 1 (
ECHO. 1 - %SPELLSLOT1NAME%
)
IF %AVAILABLESPELLSLOTS% GEQ 2 (
ECHO. 2 - %SPELLSLOT2NAME%
)
IF %AVAILABLESPELLSLOTS% GEQ 3 (
ECHO. 3 - %SPELLSLOT3NAME%
)
IF %AVAILABLESPELLSLOTS% GEQ 4 (
ECHO. 4 - %SPELLSLOT4NAME%
)
IF %AVAILABLESPELLSLOTS% GEQ 5 (
ECHO. 5 - %SPELLSLOT5NAME%
)
IF %AVAILABLESPELLSLOTS% GEQ 6 (
ECHO. 6 - %SPELLSLOT6NAME%
)
IF %AVAILABLESPELLSLOTS% GEQ 7 (
ECHO. 7 - %SPELLSLOT7NAME%
)
IF %AVAILABLESPELLSLOTS% GEQ 8 (
ECHO. 8 - %SPELLSLOT8NAME%
)
IF %AVAILABLESPELLSLOTS% GEQ 9 (
ECHO. 9 - %SPELLSLOT9NAME%
)
ECHO. 0 - Back
ECHO.
SET /P SPELLSLOTNUM=
IF "%SPELLSLOTNUM%" EQU "" (
	GOTO :SPELLSLOT
)
IF %SPELLSLOTNUM% GTR %AVAILABLESPELLSLOTS% (
	GOTO :SPELLSLOT
) ELSE IF %SPELLSLOTNUM% LSS 0 (
	GOTO :SPELLSLOT
) ELSE IF %SPELLSLOTNUM% EQU 0 (
	GOTO :EOF
)
ECHO.
GOTO :EOF

:NAMESPELL
CALL :INITIALISEBACKS
CLS
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: What would you like to call your spell? (1 word)
ECHO.
ECHO           0 to exit
ECHO.
SET /P SPELLNAME=
ECHO.
IF "%SPELLNAME%" EQU "" (
	GOTO :NAMESPELL
)
GOTO :EOF

:TARGET
CALL :INITIALISEBACKS
CLS
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: Who would you like the target of your spell to be?
ECHO.
ECHO. 1 - The Enemy
ECHO. 2 - Myself
ECHO. 3 - Both
ECHO. 0 - Back
ECHO.
SET /P TARGETID=
ECHO.
IF "%TARGETID%" EQU "1" (
	SET TARGET=ENEMY
) ELSE IF "%TARGETID%" EQU "2" (
	SET TARGET=PLAYER
) ELSE IF "%TARGETID%" EQU "3" (
	SET TARGET=BOTH
) ELSE IF "%TARGETID%" EQU "0" (
	SET /a TARGETBACK = 1
) ELSE (
	GOTO :TARGET
)
GOTO :EOF



:DAMAGE
CALL :INITIALISEBACKS
CLS
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: Would you like your spell to do damage?
ECHO.
ECHO. 1 - Yes
ECHO. 2 - No
ECHO. 0 - Back
ECHO.
ECHO.
SET /P DAMAGETRUE=
ECHO.
IF "%DAMAGETRUE%" EQU "1" (
	GOTO :EOF
) ELSE IF "%DAMAGETRUE%" EQU "2" (
	GOTO :EOF
) ELSE IF "%DAMAGETRUE%" EQU "0" (
	SET /a DAMAGEBACK = 1
) ELSE (
	GOTO :DAMAGE
)
GOTO :EOF

:DAMAGEVALUE
CALL :INITIALISEBACKS
CLS
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: What would you like the base damage of %SPELLNAME% to be?
ECHO           Must be between 1 and 200
ECHO           %DAMAGEPOINTMPCOST% MP for each point
ECHO           %DAMAGEPOINTGILCOST% Gil for each point
ECHO.
ECHO           0 to go back
ECHO.
SET /P BASEDAM=
ECHO.
IF "%BASEDAM%" EQU "" (
	GOTO :DAMAGEVALUE
)
IF %BASEDAM% LSS 0 (
	GOTO :DAMAGEVALUE
) ELSE IF %BASEDAM% GTR 200 (
	GOTO :DAMAGEVALUE
) ELSE IF %BASEDAM% EQU 0 (
	SET /a DAMAGEVALUEBACK = 1
)
SET /a DAMAGEMPCOST = %BASEDAM%*%DAMAGEPOINTMPCOST%
SET /a DAMAGEGILCOST = %BASEDAM%*%DAMAGEPOINTGILCOST%
GOTO :EOF

:STATUS
CALL :INITIALISEBACKS
CALL :INITIALISESTATUS
CLS
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: Ok, would you like status ailments?
ECHO.
ECHO. 1 - Yes
ECHO. 2 - No
ECHO. 0 - Back
ECHO.
SET /P STATUSTRUE=
ECHO.
IF "%STATUSTRUE%" EQU "1" (
	GOTO :EOF
) ELSE IF "%STATUSTRUE%" EQU "2" (
	SET /a STATUSMPCOST = 0
	SET /a STATUSGILCOST = 0
	GOTO :EOF
) ELSE IF "%STATUSTRUE%" EQU "0" (
	SET /a STATUSBACK = 1
) ELSE (
	GOTO :STATUS
)
GOTO :EOF

:STATUSVALUE
CLS
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: Which Status Ailments would you like
ECHO.
ECHO. 1 - Blind             (%STAT1MPCOST% MP) (%STAT1GILCOST% Gil)
ECHO. 2 - Silence           (%STAT2MPCOST% MP) (%STAT2GILCOST% Gil)
ECHO. 3 - Poison            (%STAT3MPCOST% MP) (%STAT3GILCOST% Gil)
ECHO. 4 - Slow              (%STAT4MPCOST% MP) (%STAT4GILCOST% Gil)
ECHO. 5 - Sleep             (%STAT5MPCOST% MP) (%STAT5GILCOST% Gil)
ECHO. 6 - Confusion         (%STAT6MPCOST% MP) (%STAT6GILCOST% Gil)
ECHO. 7 - Stop              (%STAT7MPCOST% MP) (%STAT7GILCOST% Gil)
ECHO. 0 - Done
ECHO.
ECHO. %SPELLNAME% currently casts:
ECHO.
IF %BLIND% EQU 1 (
	ECHO. Blind
)
IF %SILENCE% EQU 1 (
	ECHO. Silence
)
IF %POISON% EQU 1 (
	ECHO. Poison
)
IF %SLOW% EQU 1 (
	ECHO. Slow
)
IF %SLEEP% EQU 1 (
	ECHO. Sleep
)
IF %CONFUSION% EQU 1 (
	ECHO. Confusion
)
IF %STOP% EQU 1 (
	ECHO. Stop
)
ECHO.
SET /P AILMENTID=
ECHO.
IF "%AILMENTID%" EQU "1" (
	IF %PLAYERBLINDENABLED% EQU 1 (
		SET AILMENT=BLIND
		CALL :STATUSALREADYSET
		SET /a BLIND=1
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%AILMENTID%" EQU "2" ( 
	IF %PLAYERSILENCEENABLED% EQU 1 (
		SET AILMENT=SILENCE
		CALL :STATUSALREADYSET
		SET /a SILENCE=1
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%AILMENTID%" EQU "3" ( 
	IF %PLAYERPOISONENABLED% EQU 1 (
		SET AILMENT=POISON
		CALL :STATUSALREADYSET
		SET /a POISON=1
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%AILMENTID%" EQU "4" ( 
	IF %PLAYERSLOWENABLED% EQU 1 (
		SET AILMENT=SLOW
		CALL :STATUSALREADYSET
		SET /a SLOW=1
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%AILMENTID%" EQU "5" ( 
	IF %PLAYERSLEEPENABLED% EQU 1 (
		SET AILMENT=SLEEP
		CALL :STATUSALREADYSET
		SET /a SLEEP=1
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%AILMENTID%" EQU "6" ( 
	IF %PLAYERCONFUSIONENABLED% EQU 1 (
		SET AILMENT=CONFUSION
		CALL :STATUSALREADYSET
		SET /a CONFUSION=1
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%AILMENTID%" EQU "7" ( 
	IF %PLAYERSTOPENABLED% EQU 1 (
		SET AILMENT=STOP
		CALL :STATUSALREADYSET
		SET /a STOP=1
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%AILMENTID%" EQU "0" ( 
	GOTO :EOF
) ELSE (
	GOTO :STATUSVALUE
)
GOTO :STATUSVALUE

:STATUSALREADYSET
CLS
IF !%AILMENT%! EQU 1 (
	ECHO. You already have this
	ECHO.
	CALL :WAITFORONE
	GOTO :EOF
)
SET /a STATUSMPCOST = %STATUSMPCOST%+!STAT%AILMENTID%MPCOST!
SET /a STATUSGILCOST = %STATUSGILCOST%+!STAT%AILMENTID%GILCOST!
GOTO :EOF

:DISPEL
CLS
CALL :INITIALISEBACKS
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: Would you like %SPELLNAME% to dispel the enemy?
ECHO.
ECHO. 1 - Yes         (%SUPP5MPCOST% MP) (%SUPP5GILCOST% Gil)
ECHO. 2 - No
ECHO. 0 - Back
ECHO.
SET /P DISPEL=
ECHO.
IF "%DISPEL%" EQU "1" (
	IF %PLAYERDISPELENABLED% EQU 1 (
		SET /a DISPELMPCOST = %SUPP5MPCOST%
		SET /a DISPELGILCOST = %SUPP5GILCOST%
		GOTO :EOF
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%DISPEL%" EQU "2" ( 
	GOTO :EOF
) ELSE IF "%DISPEL%" EQU "0" ( 
	SET /a DISPELBACK = 1
) ELSE (
	GOTO :DISPEL
)
GOTO :EOF

:CURE
CLS
CALL :INITIALISEBACKS
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: Would you like your spell to cure you?
ECHO.
ECHO. 1 - Yes
ECHO. 2 - No
ECHO. 0 - Back
ECHO.
SET /P CURETRUE=
ECHO.
IF "%CURETRUE%" EQU "1" (
	GOTO :EOF
) ELSE IF "%CURETRUE%" EQU "2" (
	GOTO :EOF
) ELSE IF "%CURETRUE%" EQU "0" (
	SET /a CUREBACK = 1
) ELSE (
	GOTO :CURE
)
GOTO :EOF

:CUREVALUE
CLS
CALL :INITIALISEBACKS
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: What would you like the base healing power of %SPELLNAME% to be?
ECHO           Must be between 1 and 100
ECHO           %HEALPOINTMPCOST% MP for each point
ECHO           %HEALPOINTGILCOST% Gil for each point
ECHO.
ECHO           0 to go back
ECHO.
SET /P BASEHEAL=
ECHO.
IF "%BASEHEAL%" EQU "" (
	GOTO :CUREVALUE
)
IF %BASEHEAL% LSS 0 (
	GOTO :CUREVALUE
) ELSE IF %BASEHEAL% GTR 100 (
	GOTO :CUREVALUE
) ELSE IF %BASEHEAL% EQU 0 (
	SET /a CUREVALUEBACK = 1
)
SET /a CUREMPCOST = %BASEHEAL%*%HEALPOINTMPCOST%
SET /a CUREGILCOST = %BASEHEAL%*%HEALPOINTGILCOST%
GOTO :EOF

:STATUSCURE
CLS
CALL :INITIALISEBACKS
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: Would you like %SPELLNAME% to cure status ailments?
ECHO           This will cure Blind, Poison and Slow
ECHO.
ECHO. 1 - Yes             (%REST5MPCOST% MP) (%REST5GILCOST% Gil)
ECHO. 2 - No
ECHO. 0 - Back
ECHO.
SET /P STATUSCURETRUE=
ECHO.
IF "%STATUSCURETRUE%" EQU "1" (
	IF %PLAYERESUNAENABLED% EQU 1 (
		SET /a STATUSCUREMPCOST = %REST5MPCOST%
		SET /a STATUSCUREGILCOST = %REST5GILCOST%
		GOTO :EOF
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%STATUSCURETRUE%" EQU "2" ( 
	GOTO :EOF
) ELSE IF "%STATUSCURETRUE%" EQU "0" ( 
	SET /a STATUSCUREBACK = 1
) ELSE (
	GOTO :STATUSCURE
)
GOTO :EOF

:SUPPORT
CLS
CALL :INITIALISEBACKS
CALL :INITIALISESUPPORT
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: Would you like %SPELLNAME% to include support magic?
ECHO.
ECHO. 1 - Yes
ECHO. 2 - No
ECHO. 0 - Back
ECHO.
SET /P SUPPORTTRUE=
ECHO.
IF "%SUPPORTTRUE%" EQU "1" (
	GOTO :EOF
) ELSE IF "%SUPPORTTRUE%" EQU "2" (
	GOTO :EOF
) ELSE IF "%SUPPORTTRUE%" EQU "0" (
	SET /a SUPPORTBACK = 1
) ELSE (
	GOTO :SUPPORT
)
CLS
GOTO :EOF

:SUPPORTVALUE
CLS
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: Which support spells would you like?
ECHO.
ECHO. 1 - Protect          (%SUPP1MPCOST% MP) (%SUPP1GILCOST% Gil)
ECHO. 2 - Shell            (%SUPP2MPCOST% MP) (%SUPP2GILCOST% Gil)
ECHO. 3 - Haste            (%SUPP3MPCOST% MP) (%SUPP3GILCOST% Gil)
ECHO. 4 - Reflect          (%SUPP4MPCOST% MP) (%SUPP4GILCOST% Gil)
ECHO. 0 - Done
ECHO.
ECHO. %SPELLNAME% currently casts:
ECHO.
IF %PROTECT% EQU 1 (
	ECHO. Protect
)
IF %SHELL% EQU 1 (
	ECHO. Shell
)
IF %HASTE% EQU 1 (
	ECHO. Haste
)
IF %REFLECT% EQU 1 (
	ECHO. Reflect
)
ECHO.
SET /P SUPPORTID=
ECHO.
IF "%SUPPORTID%" EQU "1" (
	IF %PLAYERPROTECTENABLED% EQU 1 (
		SET SUPPORT=PROTECT
		CALL :SUPPORTALREADYSET
		SET /a PROTECT=1
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%SUPPORTID%" EQU "2" ( 
	IF %PLAYERSHELLENABLED% EQU 1 (
		SET SUPPORT=SHELL
		CALL :SUPPORTALREADYSET
		SET /a SHELL=1
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%SUPPORTID%" EQU "3" ( 
	IF %PLAYERHASTEENABLED% EQU 1 (
		SET SUPPORT=HASTE
		CALL :SUPPORTALREADYSET
		SET /a HASTE=1
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%SUPPORTID%" EQU "4" ( 
	IF %PLAYERREFLECTENABLED% EQU 1 (
		SET SUPPORT=REFLECT
		CALL :SUPPORTALREADYSET
		SET /a REFLECT=1
	) ELSE (
		ECHO %PRIESTNAME%: Sorry, you need to unlock this spell before you can use it to make a new one^^
		CALL :WAITFORTHREE
	)
) ELSE IF "%SUPPORTID%" EQU "0" ( 
	GOTO :EOF
) ELSE (
	GOTO :SUPPORTVALUE
)
GOTO :SUPPORTVALUE

:SUPPORTALREADYSET
CLS
IF !%SUPPORT%! EQU 1 (
	ECHO. You already have this
	ECHO.
	CALL :WAITFORONE
	GOTO :EOF
)
SET /a SUPPORTMPCOST = %SUPPORTMPCOST% + !SUPP%SUPPORTID%MPCOST!
SET /a SUPPORTGILCOST = %SUPPORTGILCOST% + !SUPP%SUPPORTID%GILCOST!
GOTO :EOF

:ACCURACY
CLS
CALL :INITIALISEBACKS
CALL :MPCOSTCALCULATION
CALL :GILCOSTCALCULATION
CALL :MPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: What would you like the accuracy of your spell to be?
ECHO           Must be between 1 and 200
ECHO           Current costs based on accuracy of 50.
ECHO.
ECHO           0 to go back
ECHO.
SET /P ACCURACY=
ECHO.
IF "%ACCURACY%" EQU "" (
	GOTO :ACCURACY
)
IF %ACCURACY% LSS 0 (
	GOTO :ACCURACY
) ELSE IF %ACCURACY% GTR 200 (
	GOTO :ACCURACY
) ELSE IF %ACCURACY% EQU 0 (
	SET /a ACCURACYBACK = 1
)
GOTO :EOF

:MPREDUCTION
CLS
CALL :INITIALISEBACKS
CALL :ACCURACYCALCULATION
CALL :ACCMPGILDISPLAY
ECHO.
ECHO %PRIESTNAME%: You can spend extra Gil to reduce the MP cost of your spell.
ECHO           It costs %MPREDUCTIONPOINTCOST% Gil to reduce the MP cost by 1.
ECHO           How much would you like to reduce the MP cost by?
ECHO.
ECHO           Press B to go back
ECHO.
SET /P MPREDUCTION=
ECHO.
IF "%MPREDUCTION%" EQU "" (
	GOTO :MPREDUCTION
)
IF /I %MPREDUCTION% EQU B (
	SET /a MPREDUCTIONBACK = 1
	GOTO :EOF
)
SET /a MPREDUCTIONGILCOST = %MPREDUCTION%*%MPREDUCTIONPOINTCOST%
SET /a SPELLMPCOST = %ACCMPCOST% - %MPREDUCTION%
SET /a SPELLGILCOST = %ACCGILCOST% + %MPREDUCTIONGILCOST%
IF %SPELLGILCOST% LSS 0 (
	ECHO. Don't try to cheat or I'll delete your saved games.
	ECHO.
	CALL :WAITFORTWO
	GOTO :MPREDUCTION
) ELSE IF %SPELLMPCOST% LEQ 0 (
	ECHO %PRIESTNAME%: Ohhhhh, I see what you're trying to do^^!
	ECHO.
	CALL :WAITFORTWO
	ECHO           I don't think I can allow that, I'll just ask God...
	ECHO.
	CALL :WAITFORTWO
	ECHO           ...
	ECHO.
	CALL :WAITFORTWO
	ECHO           ...
	ECHO.
	CALL :WAITFORTWO
	ECHO           Turns out I can^^! I hope you can afford the spell.
	ECHO.
	CALL :WAITFORTWO
) ELSE IF %MPREDUCTION% LSS -9999 (
	ECHO. Don't try to cheat or I'll delete your saved games.
	ECHO.
	CALL :WAITFORTWO
	GOTO :MPREDUCTION
) ELSE IF %MPREDUCTION% LSS 0 (
	ECHO. Ooooo cleverrrrr.
	ECHO.
	CALL :WAITFORTWO
	ECHO. I guess I'll allow that...
	ECHO.
	CALL :WAITFORTWO
)
GOTO :EOF

:CONFIRMATION
CLS
SET /a REFLECTABLE = 0
IF %DAMAGETRUE% EQU 1 (
	SET /a REFLECTABLE = 1
)
IF %STATUSTRUE% EQU 1 (
	SET /a REFLECTABLE = 1
)
CALL :INITIALISEBACKS
CALL :TIDYUP
ECHO.
ECHO %PRIESTNAME%: Okay so, here's the final stats for %SPELLNAME%
ECHO.
ECHO MP Cost            %SPELLMPCOST%
ECHO Cost to create     %SPELLGILCOST%
ECHO Accuracy           %ACCURACY%
ECHO.
ECHO Target:            %TARGET%
IF %TARGET% EQU BOTH (
	ECHO The following applies to the enemy
	ECHO.
)
IF %DAMAGETRUE% EQU 1 (
	ECHO Base Damage:       %BASEDAM%
)
IF %STATUSTRUE% EQU 1 (
	IF %BLIND% EQU 1 (
		ECHO Casts Blind
	)
	IF %SILENCE% EQU 1 (
		ECHO Casts Silence
	)
	IF %POISON% EQU 1 (
		ECHO Casts Poison
	)
	IF %SLOW% EQU 1 (
		ECHO Casts Slow
	)
	IF %SLEEP% EQU 1 (
		ECHO Casts Sleep
	)
	IF %CONFUSION% EQU 1 (
		ECHO Casts Confusion
	)
	IF %STOP% EQU 1 (
		ECHO Casts Stop
	)
)
IF %DISPEL% EQU 1 (
	ECHO Casts Dispel
)
IF %TARGET% EQU BOTH (
	ECHO.
	ECHO The following applies to %PLAYERNAME%
	ECHO.
)
IF %CURETRUE% EQU 1 (
	ECHO Base Heal:         %BASEHEAL%
)
IF %STATUSCURETRUE% EQU 1 (
	ECHO Heals Blind, Poison and Slow
)
IF %SUPPORTTRUE% EQU 1 (
	IF %PROTECT% EQU 1 (
		ECHO Casts Protect
	)
	IF %SHELL% EQU 1 (
		ECHO Casts Shell
	)
	IF %HASTE% EQU 1 (
		ECHO Casts Haste
	)
	IF %REFLECT% EQU 1 (
		ECHO Casts Reflect
	)
)
IF %REFLECTABLE% EQU 1 (
	ECHO.
	ECHO Note: Your spell is reflectable
)
ECHO.
ECHO %PRIESTNAME%: Are you happy with this spell?
ECHO.
ECHO. 1 - Yes, create %SPELLNAME%
ECHO. 2 - No, don't create %SPELLNAME%
ECHO. 0 - Back
ECHO.
:CONFIRMATIONCHOICE
SET /P CONFIRMATIONCHOICE=
ECHO.
SET /a TOTALCOST = (%SPELLMPCOST%*%MPREDUCTIONPOINTCOST%)+%SPELLGILCOST%
SET /a TOTALAVAILABLE = (%PLAYERMAXMP%*%MPREDUCTIONPOINTCOST%)+%PLAYERGIL%
IF "%CONFIRMATIONCHOICE%" EQU "1" (
	IF %TOTALCOST% GTR %TOTALAVAILABLE% (
		ECHO. You don't have enough MP or Gil to create this spell.
		ECHO.
		CALL :WAITFORTWO
		ECHO. You'll need to go back and adjust what the spell does.
		ECHO.
		CALL :WAITFORTWO
		GOTO :ACCURACY
	) ELSE IF %PLAYERGIL% LSS %SPELLGILCOST% (
		ECHO. You don't have enough Gil for this
		ECHO.
		CALL :WAITFORTWO
		GOTO :MPREDUCTIONSET
	) ELSE IF %PLAYERMAXMP% LSS %SPELLMPCOST% (
		ECHO. Your MP isn't high enough to create this spell
		ECHO.
		CALL :WAITFORTWO
		GOTO :MPREDUCTIONSET
	) ELSE (
		CALL :CREATESPELL
	)
) ELSE IF "%CONFIRMATIONCHOICE%" EQU "2" (
	ECHO %PRIESTNAME%: That's fine, I love having my time wasted...
	ECHO.
	CALL :WAITFORTHREE
) ELSE IF "%CONFIRMATIONCHOICE%" EQU "0" (
	SET /a CONFIRMATIONBACK = 1
) ELSE (
	GOTO :CONFIRMATIONCHOICE
)
ECHO.
GOTO :EOF

:INITIALISEVARIABLES
SET /a DAMAGETRUE = 0
SET /a BASEDAM = 0
SET /a STATUSTRUE = 0
SET /a BLIND = 0
SET /a SILENCE = 0
SET /a POISON = 0
SET /a SLOW = 0
SET /a SLEEP = 0
SET /a CONFUSION = 0
SET /a STOP = 0
SET /a DISPEL = 0
SET /a CURETRUE = 0
SET /a BASEHEAL = 0
SET /a STATUSCURETRUE = 0
SET /a SUPPORTTRUE = 0
SET /a PROTECT = 0
SET /a SHELL = 0
SET /a HASTE = 0
SET /a REFLECT = 0
GOTO :EOF

:INITIALISEBACKS
SET /a TARGETBACK = 0
SET /a DAMAGEBACK = 0
SET /a DAMAGEVALUEBACK = 0
SET /a STATUSBACK = 0
SET /a DISPELBACK = 0
SET /a CUREBACK = 0
SET /a CUREVALUEBACK = 0
SET /a STATUSCUREBACK = 0
SET /a SUPPORTBACK = 0
SET /a ACCURACYBACK = 0
SET /a MPREDUCTIONBACK = 0
SET /a CONFIRMATIONBACK = 0
GOTO :EOF

:INITIALISEMPCOST
SET /a DAMAGEMPCOST = 0
SET /a STATUSMPCOST = 0
SET /a DISPELMPCOST = 0
SET /a CUREMPCOST = 0
SET /a STATUSCUREMPCOST = 0
SET /a SUPPORTMPCOST = 0
SET /a ACCURACYMPCOST = 0
SET /a MPREDUCTION = 0
GOTO :EOF

:INITIALISEGILCOST
SET /a DAMAGEGILCOST = 0
SET /a STATUSGILCOST = 0
SET /a DISPELGILCOST = 0
SET /a CUREGILCOST = 0
SET /a STATUSCUREGILCOST = 0
SET /a SUPPORTGILCOST = 0
SET /a ACCURACYGILCOST = 0
SET /a MPREDUCTIONGILCOST = 0
GOTO :EOF

:MPCOSTCALCULATION
SET /a MPCOST = %DAMAGEMPCOST% + %STATUSMPCOST% + %DISPELMPCOST% + %CUREMPCOST% + %STATUSCUREMPCOST% + %SUPPORTMPCOST%
GOTO :EOF

:GILCOSTCALCULATION
SET /a GILCOST = %DAMAGEGILCOST% + %STATUSGILCOST% + %DISPELGILCOST% + %CUREGILCOST% + %STATUSCUREGILCOST% + %SUPPORTGILCOST%
GOTO :EOF

:INITIALISESTATUS
SET /a BLIND = 0
SET /a SILENCE = 0
SET /a POISON = 0
SET /a SLOW = 0
SET /a SLEEP = 0
SET /a CONFUSION = 0
SET /a STOP = 0
GOTO :EOF


:INITIALISESUPPORT
SET /a PROTECT = 0
SET /a SHELL = 0
SET /a HASTE = 0
SET /a REFLECT = 0
GOTO :EOF

:MPGILDISPLAY
ECHO.
IF %MPCOST% GTR %PLAYERMAXMP% (
	<nul set /p ".= " > "%MPCOST%" 
	findstr /v /a:0C /R "^$" "%MPCOST%" nul 
	del "%MPCOST%" > nul 2>&1
	ECHO. - Current MP Cost
) ELSE (
	<nul set /p ".= " > "%MPCOST%" 
	findstr /v /a:0A /R "^$" "%MPCOST%" nul 
	del "%MPCOST%" > nul 2>&1
	ECHO. - Current MP Cost
)
IF %GILCOST% GTR %PLAYERGIL% (
	<nul set /p ".= " > "%GILCOST%" 
	findstr /v /a:0C /R "^$" "%GILCOST%" nul 
	del "%GILCOST%" > nul 2>&1
	ECHO. - Current Gil Cost
) ELSE (
	<nul set /p ".= " > "%GILCOST%" 
	findstr /v /a:0A /R "^$" "%GILCOST%" nul 
	del "%GILCOST%" > nul 2>&1
	ECHO. - Current Gil Cost
)
GOTO :EOF

:ACCMPGILDISPLAY
ECHO.
IF %ACCMPCOST% GTR %PLAYERMAXMP% (
	<nul set /p ".= " > "%ACCMPCOST%" 
	findstr /v /a:0C /R "^$" "%ACCMPCOST%" nul 
	del "%ACCMPCOST%" > nul 2>&1
	ECHO. - Current MP Cost
) ELSE (
	<nul set /p ".= " > "%ACCMPCOST%" 
	findstr /v /a:0A /R "^$" "%ACCMPCOST%" nul 
	del "%ACCMPCOST%" > nul 2>&1
	ECHO. - Current MP Cost
)
IF %ACCGILCOST% GTR %PLAYERGIL% (
	<nul set /p ".= " > "%ACCGILCOST%" 
	findstr /v /a:0C /R "^$" "%ACCGILCOST%" nul 
	del "%ACCGILCOST%" > nul 2>&1
	ECHO. - Current Gil Cost
) ELSE (
	<nul set /p ".= " > "%ACCGILCOST%" 
	findstr /v /a:0A /R "^$" "%ACCGILCOST%" nul 
	del "%ACCGILCOST%" > nul 2>&1
	ECHO. - Current Gil Cost
)
GOTO :EOF

:ACCURACYCALCULATION
SET /a ACCMPCOST = (%MPCOST%*%ACCURACY%)/50
SET /a ACCGILCOST = (%GILCOST%*%ACCURACY%)/50
GOTO :EOF

:TIDYUP
IF %STATUSMPCOST% EQU 0 (
	SET /a STATUSTRUE = 0
)
IF %SUPPORTMPCOST% EQU 0 (
	SET /a SUPPORTTRUE = 0
)
GOTO :EOF


:CREATESPELL
SET /a PLAYERGIL = %PLAYERGIL% - %SPELLGILCOST%
SET /a PLAYERCUSTOMSPELLCREATIONCOUNT = %PLAYERCUSTOMSPELLCREATIONCOUNT% + 1
CALL :SPELLNAMELENGTH
CALL :SETGENERAL
CALL :SETEFFECTS

ECHO SET PLAYERSPELLNAME=%SPELLNAME%>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
ECHO SET /a PLAYERSPELLACCURACY=%ACCURACY%>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
ECHO SET /a PLAYERSPELLMPCOST=%SPELLMPCOST%>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
ECHO SET /a PLAYERSPELLREFLECTABLE=%REFLECTABLE% >> "%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
ECHO SET /a PLAYERSPELLEFFECTSNUM=%PLAYERSPELLEFFECTSNUM% >> "%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
SET /a EFFECTNUM = 0
:EFFECTSLOOPSTART
SET /a EFFECTNUM = %EFFECTNUM% + 1
ECHO SET PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT=!PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT!>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
ECHO SET PLAYERSPELLEFFECT%EFFECTNUM%TYPE=!PLAYERSPELLEFFECT%EFFECTNUM%TYPE!>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
ECHO SET PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE=!PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE!>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
ECHO SET PLAYERSPELLEFFECT%EFFECTNUM%TARGET=!PLAYERSPELLEFFECT%EFFECTNUM%TARGET!>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
ECHO SET /a PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT=!PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT!>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
ECHO SET /a PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT=!PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT!>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
ECHO SET /a PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT=!PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT!>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
ECHO SET /a PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT=!PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT!>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
ECHO SET PLAYERSPELLEFFECT%EFFECTNUM%TEXT=!PLAYERSPELLEFFECT%EFFECTNUM%TEXT!>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%SPELLSLOTNUM%.bat"
IF %EFFECTNUM% LSS %PLAYERSPELLEFFECTSNUM% (
	GOTO :EFFECTSLOOPSTART
)
ECHO.
ECHO %SPELLNAME% created successfully
ECHO.
pause
GOTO :EOF

:SPELLNAMELENGTH
SET /a SPELLNAMELENGTH = 0
:SPELLNAMELOOPSTART
SET /a SPELLNAMELENGTH += 1
SET SUBSTR=!SPELLNAME:~%SPELLNAMELENGTH%,1!
IF "%SUBSTR%" NEQ "" (
	GOTO :SPELLNAMELOOPSTART
) ELSE (
	SET /a TEMPCOUNT = %SPELLNAMELENGTH% + 1
	SET SUBSTR=!SPELL:~%TEMPCOUNT%,1!
	IF "%SUBSTR%" NEQ "" (
		GOTO :SPELLNAMELOOPSTART
	)
)
REM Need to have a think about the below bit
SET /a SPELLTABS = ((34-%SPELLNAMELENGTH%)/8)+1
GOTO :EOF

:SETGENERAL
ECHO SET SPELLSLOT%SPELLSLOTNUM%NAME=%SPELLNAME%>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspelllist.bat"
ECHO SET /a SPELLSLOT%SPELLSLOTNUM%NAMELENGTH=%SPELLNAMELENGTH% >> "%CUSTOMSPELLRESOURCEPATH%\Temp\customspelllist.bat"
ECHO SET /a SPELLSLOT%SPELLSLOTNUM%MPCOST=%SPELLMPCOST%>>"%CUSTOMSPELLRESOURCEPATH%\Temp\customspelllist.bat"
GOTO :EOF

:SETEFFECTS
SET /a EFFECTNUM = 0
IF %DAMAGETRUE% EQU 1 (
	CALL :ADDDAMAGEEFFECT
)
IF %STATUSTRUE% EQU 1 (
	CALL :ADDSTATUSEFFECTS
)
IF %DISPEL% EQU 1 (
	CALL :ADDDISPELEFFECT
)
IF %CURETRUE% EQU 1 (
	CALL :ADDCUREEFFECT
)
IF %STATUSCURETRUE% EQU 1 (
	CALL :ADDSTATUSCUREEFFECT
)
IF %SUPPORTTRUE% EQU 1 (
	CALL :ADDSUPPORTEFFECTS
)
SET /a PLAYERSPELLEFFECTSNUM = %EFFECTNUM%
GOTO :EOF

:ADDDAMAGEEFFECT
SET /a EFFECTNUM = %EFFECTNUM%+1
SET PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT=NONE
SET PLAYERSPELLEFFECT%EFFECTNUM%TYPE=STAT
SET PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE=HP
SET PLAYERSPELLEFFECT%EFFECTNUM%TARGET=DEFENDER
SET /a PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT = -%BASEDAM%
SET /a PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT = 0
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT = 0
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT = 0
SET PLAYERSPELLEFFECT%EFFECTNUM%TEXT=Your spell did
GOTO :EOF

:ADDSTATUSEFFECTS
SET TEMPMAGTYPE=STAT
SET TEMPSTATUS=BLIND
CALL :SETEFFECTQUERY
SET TEMPSTATUS=SILENCE
CALL :SETEFFECTQUERY
SET TEMPSTATUS=POISON
CALL :SETEFFECTQUERY
SET TEMPSTATUS=SLOW
CALL :SETEFFECTQUERY
SET TEMPSTATUS=SLEEP
CALL :SETEFFECTQUERY
SET TEMPSTATUS=CONFUSION
CALL :SETEFFECTQUERY
SET TEMPSTATUS=STOP
CALL :SETEFFECTQUERY
GOTO :EOF

:SETEFFECTQUERY
IF !%TEMPSTATUS%! EQU 1 (
	CALL :SETEFFECT
)
GOTO :EOF
:SETEFFECT
SET /a EFFECTNUM = %EFFECTNUM%+1
SET TEMPID=!%TEMPSTATUS%ID!
SET PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT=!%TEMPMAGTYPE%%TEMPID%EFFECT1ELEMENT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TYPE=!%TEMPMAGTYPE%%TEMPID%EFFECT1TYPE!
SET PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE=!%TEMPMAGTYPE%%TEMPID%EFFECT1ATTRIBUTE!
SET PLAYERSPELLEFFECT%EFFECTNUM%TARGET=!%TEMPMAGTYPE%%TEMPID%EFFECT1TARGET!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT = !%TEMPMAGTYPE%%TEMPID%EFFECT1AMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT = !%TEMPMAGTYPE%%TEMPID%EFFECT1FIXEDAMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT = !%TEMPMAGTYPE%%TEMPID%EFFECT1MINTURNCOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT = !%TEMPMAGTYPE%%TEMPID%EFFECT1MAXTURNCOUNT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TEXT=!%TEMPMAGTYPE%%TEMPID%EFFECT1PLAYERTEXT!
GOTO :EOF

:ADDDISPELEFFECT
SET /a EFFECTNUM = %EFFECTNUM%+1
SET PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT=!SUPP%DISPELID%EFFECT1ELEMENT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TYPE=!SUPP%DISPELID%EFFECT1TYPE!
SET PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE=!SUPP%DISPELID%EFFECT1ATTRIBUTE!
SET PLAYERSPELLEFFECT%EFFECTNUM%TARGET=!SUPP%DISPELID%EFFECT1TARGET!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT = !SUPP%DISPELID%EFFECT1AMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT = !SUPP%DISPELID%EFFECT1FIXEDAMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT = !SUPP%DISPELID%EFFECT1MINTURNCOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT = !SUPP%DISPELID%EFFECT1MAXTURNCOUNT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TEXT=!SUPP%DISPELID%EFFECT1PLAYERTEXT!
SET /a EFFECTNUM = %EFFECTNUM%+1
SET PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT=!SUPP%DISPELID%EFFECT2ELEMENT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TYPE=!SUPP%DISPELID%EFFECT2TYPE!
SET PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE=!SUPP%DISPELID%EFFECT2ATTRIBUTE!
SET PLAYERSPELLEFFECT%EFFECTNUM%TARGET=!SUPP%DISPELID%EFFECT2TARGET!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT = !SUPP%DISPELID%EFFECT2AMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT = !SUPP%DISPELID%EFFECT2FIXEDAMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT = !SUPP%DISPELID%EFFECT2MINTURNCOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT = !SUPP%DISPELID%EFFECT2MAXTURNCOUNT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TEXT=!SUPP%DISPELID%EFFECT2PLAYERTEXT!
SET /a EFFECTNUM = %EFFECTNUM%+1
SET PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT=!SUPP%DISPELID%EFFECT3ELEMENT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TYPE=!SUPP%DISPELID%EFFECT3TYPE!
SET PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE=!SUPP%DISPELID%EFFECT3ATTRIBUTE!
SET PLAYERSPELLEFFECT%EFFECTNUM%TARGET=!SUPP%DISPELID%EFFECT3TARGET!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT = !SUPP%DISPELID%EFFECT3AMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT = !SUPP%DISPELID%EFFECT3FIXEDAMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT = !SUPP%DISPELID%EFFECT3MINTURNCOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT = !SUPP%DISPELID%EFFECT3MAXTURNCOUNT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TEXT=!SUPP%DISPELID%EFFECT3PLAYERTEXT!
SET /a EFFECTNUM = %EFFECTNUM%+1
SET PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT=!SUPP%DISPELID%EFFECT4ELEMENT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TYPE=!SUPP%DISPELID%EFFECT4TYPE!
SET PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE=!SUPP%DISPELID%EFFECT4ATTRIBUTE!
SET PLAYERSPELLEFFECT%EFFECTNUM%TARGET=!SUPP%DISPELID%EFFECT4TARGET!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT = !SUPP%DISPELID%EFFECT4AMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT = !SUPP%DISPELID%EFFECT4FIXEDAMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT = !SUPP%DISPELID%EFFECT4MINTURNCOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT = !SUPP%DISPELID%EFFECT4MAXTURNCOUNT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TEXT=!SUPP%DISPELID%EFFECT4PLAYERTEXT!
GOTO :EOF



:ADDCUREEFFECT
SET /a EFFECTNUM = %EFFECTNUM%+1
SET PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT=NONE
SET PLAYERSPELLEFFECT%EFFECTNUM%TYPE=STAT
SET PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE=HP
SET PLAYERSPELLEFFECT%EFFECTNUM%TARGET=ATTACKER
SET /a PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT = %BASEHEAL%
SET /a PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT = 0
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT = 0
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT = 0
SET PLAYERSPELLEFFECT%EFFECTNUM%TEXT=Your spell did
GOTO :EOF

:ADDSTATUSCUREEFFECT
SET /a EFFECTNUM = %EFFECTNUM%+1
SET PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT=!REST%ESUNAID%EFFECT1ELEMENT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TYPE=!REST%ESUNAID%EFFECT1TYPE!
SET PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE=!REST%ESUNAID%EFFECT1ATTRIBUTE!
SET PLAYERSPELLEFFECT%EFFECTNUM%TARGET=!REST%ESUNAID%EFFECT1TARGET!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT = !REST%ESUNAID%EFFECT1AMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT = !REST%ESUNAID%EFFECT1FIXEDAMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT = !REST%ESUNAID%EFFECT1MINTURNCOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT = !REST%ESUNAID%EFFECT1MAXTURNCOUNT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TEXT=!REST%ESUNAID%EFFECT1PLAYERTEXT!
SET /a EFFECTNUM = %EFFECTNUM%+1
SET PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT=!REST%ESUNAID%EFFECT2ELEMENT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TYPE=!REST%ESUNAID%EFFECT2TYPE!
SET PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE=!REST%ESUNAID%EFFECT2ATTRIBUTE!
SET PLAYERSPELLEFFECT%EFFECTNUM%TARGET=!REST%ESUNAID%EFFECT2TARGET!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT = !REST%ESUNAID%EFFECT2AMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT = !REST%ESUNAID%EFFECT2FIXEDAMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT = !REST%ESUNAID%EFFECT2MINTURNCOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT = !REST%ESUNAID%EFFECT2MAXTURNCOUNT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TEXT=!REST%ESUNAID%EFFECT2PLAYERTEXT!
SET /a EFFECTNUM = %EFFECTNUM%+1
SET PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT=!REST%ESUNAID%EFFECT3ELEMENT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TYPE=!REST%ESUNAID%EFFECT3TYPE!
SET PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE=!REST%ESUNAID%EFFECT3ATTRIBUTE!
SET PLAYERSPELLEFFECT%EFFECTNUM%TARGET=!REST%ESUNAID%EFFECT3TARGET!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT = !REST%ESUNAID%EFFECT3AMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT = !REST%ESUNAID%EFFECT3FIXEDAMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT = !REST%ESUNAID%EFFECT3MINTURNCOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT = !REST%ESUNAID%EFFECT3MAXTURNCOUNT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TEXT=!REST%ESUNAID%EFFECT3PLAYERTEXT!
SET /a EFFECTNUM = %EFFECTNUM%+1
SET PLAYERSPELLEFFECT%EFFECTNUM%ELEMENT=!REST%ESUNAID%EFFECT4ELEMENT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TYPE=!REST%ESUNAID%EFFECT4TYPE!
SET PLAYERSPELLEFFECT%EFFECTNUM%ATTRIBUTE=!REST%ESUNAID%EFFECT4ATTRIBUTE!
SET PLAYERSPELLEFFECT%EFFECTNUM%TARGET=!REST%ESUNAID%EFFECT4TARGET!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%AMOUNT = !REST%ESUNAID%EFFECT4AMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%FIXEDAMOUNT = !REST%ESUNAID%EFFECT4FIXEDAMOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MINTURNCOUNT = !REST%ESUNAID%EFFECT4MINTURNCOUNT!
SET /a PLAYERSPELLEFFECT%EFFECTNUM%MAXTURNCOUNT = !REST%ESUNAID%EFFECT4MAXTURNCOUNT!
SET PLAYERSPELLEFFECT%EFFECTNUM%TEXT=!REST%ESUNAID%EFFECT4PLAYERTEXT!
GOTO :EOF

:ADDSUPPORTEFFECTS
SET TEMPMAGTYPE=SUPP
SET TEMPSTATUS=HASTE
CALL :SETEFFECTQUERY
SET TEMPSTATUS=PROTECT
CALL :SETEFFECTQUERY
SET TEMPSTATUS=SHELL
CALL :SETEFFECTQUERY
SET TEMPSTATUS=REFLECT
CALL :SETEFFECTQUERY
GOTO :EOF



:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF
CALL :SPELLTYPEQUERY
SET DISPLAYTYPE=MP
IF %OFFSPELLPRESENT% EQU 0 (
	IF %STATSPELLPRESENT% EQU 0 (
		IF %RESTSPELLPRESENT% EQU 0 (
			IF %SUPPSPELLPRESENT% EQU 0 (
				IF %CUSTOMSPELLPRESENT% EQU 0 (
					CLS
					ECHO.
					ECHO %MENUNAME%: You don't have any spells yet^^!
					ECHO.
					pause
					GOTO :EOF
				)
			)
		)
	)
)
CALL :DISPLAY
GOTO :EOF






:SPELLTYPEQUERY
SET TYPE=OFF
CALL :SPELLTYPEQUERY2
SET TYPE=STAT
CALL :SPELLTYPEQUERY2
SET TYPE=SUPP
CALL :SPELLTYPEQUERY2
SET TYPE=REST
CALL :SPELLTYPEQUERY2
CALL :CUSTOMSPELLQUERY
GOTO :EOF

:SPELLTYPEQUERY2
SET /a %TYPE%SPELLPRESENT = 0
SET /a COUNT = 0
:LOOP1
SET /a COUNT = %COUNT%+1
SET TEMP=!%TYPE%%COUNT%SPELL!
IF !PLAYER%TEMP%ENABLED! EQU 1 (
	SET /a %TYPE%SPELLPRESENT = 1
) ELSE IF %COUNT% LSS !%TYPE%SPELLTOT! (
	GOTO :LOOP1
)
GOTO :EOF

:CUSTOMSPELLQUERY
SET /a AVAILABLESPELLSLOTS = %PLAYERLVL%/5
IF EXIST "%CUSTOMSPELLRESOURCEPATH%\Temp\customspelllist.bat" (
	CALL "%CUSTOMSPELLRESOURCEPATH%\Temp\customspelllist.bat"
)
SET /a CUSTOMSPELLPRESENT = 0
IF %AVAILABLESPELLSLOTS% EQU 0 (
	GOTO :EOF
)
SET /a COUNT = 0
:LOOP2
SET /a COUNT = %COUNT% + 1
IF DEFINED SPELLSLOT%COUNT%NAME (
	SET /a CUSTOMSPELLPRESENT = 1
)
IF %COUNT% LSS %AVAILABLESPELLSLOTS% (
	GOTO :LOOP2
)
GOTO :EOF


:DISPLAY
CALL "%MENURESOURCEPATH%\menudisplay.bat"
ECHO %MENUNAME%: The spells you currently have are...
ECHO.
IF %OFFSPELLPRESENT% EQU 1 (
	SET TYPE=OFF
	SET DISPLAYTYPEVAR=Offensive
	CALL :SPELLDISPLAY
)
IF %STATSPELLPRESENT% EQU 1 (
	SET TYPE=STAT
	SET DISPLAYTYPEVAR=Status Ailments
	CALL :SPELLDISPLAY
)
IF %RESTSPELLPRESENT% EQU 1 (
	SET TYPE=REST
	SET DISPLAYTYPEVAR=Restoration
	CALL :SPELLDISPLAY
)
IF %SUPPSPELLPRESENT% EQU 1 (
	SET TYPE=SUPP
	SET DISPLAYTYPEVAR=Support
	CALL :SPELLDISPLAY
)
IF %CUSTOMSPELLPRESENT% EQU 1 (
	CALL :CUSTOMSPELLDISPLAY
)
IF %DISPLAYTYPE% EQU MP (
	ECHO. 1 - Descriptions
) ELSE IF %DISPLAYTYPE% EQU DESC (
	ECHO. 1 - MP Cost
)
ECHO. 0 - Back
ECHO.
CHOICE /C:01 /N
IF "%ERRORLEVEL%" EQU "1" (
	GOTO :EOF
) ELSE IF "%ERRORLEVEL%" EQU "2" (
	CALL :CHANGEDISPLAY
)
GOTO :DISPLAY

:SPELLDISPLAY
ECHO %DISPLAYTYPEVAR%
ECHO.
SET /a COUNT = 0
:LOOP3
SET /a COUNT = %COUNT% + 1
SET TEMP=!%TYPE%%COUNT%SPELL!
IF !PLAYER%TEMP%ENABLED! EQU 1 (
	CALL :SPELLDISPLAY2
)
IF %COUNT% LSS !%TYPE%SPELLTOT! (
	GOTO :LOOP3
)
ECHO.
GOTO :EOF

:SPELLDISPLAY2
IF %DISPLAYTYPE% EQU MP (
	ECHO. %TEMP% - !%TYPE%%COUNT%MPCOST! MP
) ELSE (
	ECHO. %TEMP% - !%TYPE%%COUNT%DESCRIPTION!
)
GOTO :EOF

:CUSTOMSPELLDISPLAY
ECHO Custom
ECHO.
SET /a COUNT = 0
:CUSTLOOPSTART
SET /a COUNT = %COUNT%+1
IF EXIST "%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%COUNT%.bat" (
	CALL "%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%COUNT%.bat"
)
IF EXIST "%CUSTOMSPELLRESOURCEPATH%\Temp\customspell%COUNT%.bat" (
	ECHO. %PLAYERSPELLNAME% - %PLAYERSPELLMPCOST% MP
)
IF %COUNT% LSS %AVAILABLESPELLSLOTS% (
	GOTO :CUSTLOOPSTART
)
ECHO.
GOTO :EOF


:CHANGEDISPLAY
IF %DISPLAYTYPE% EQU MP (
	SET DISPLAYTYPE=DESC
) ELSE IF %DISPLAYTYPE% EQU DESC (
	SET DISPLAYTYPE=MP
)
GOTO :EOF
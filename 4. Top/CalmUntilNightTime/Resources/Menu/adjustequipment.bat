CALL "%DYNAMICRESOURCEPATH%\dynamicequipmentstatsequip.bat"
SET /a SELECTED = 1
:START
CLS
ECHO.
IF %TUTORIAL% EQU 1 (
	CALL :TUTORIAL
	GOTO :MENUSTART
)
IF %OPTIONTOT% EQU 0 (
	ECHO %MENUNAME%: You don't have any %TYPE% to equip^^!
	CALL :WAITFORTHREE
	GOTO :EOF
)
IF !PLAYER%TYPE%ID! NEQ 0 (
	ECHO %MENUNAME%: You currently have the !PLAYER%TYPE%! Equipped
) ELSE (
	ECHO %MENUNAME%: You currently have no %TYPE% equipped
)
ECHO.
ECHO %MENUNAME%: Which %TYPE% would you like to equip?
ECHO.
:MENUSTART
CALL "%MISCRESOURCEPATH%\menu2.bat"
IF %VARSELECTED% EQU 1 (
	CALL :CONFIRMATION
)
IF %BACK% EQU 1 (
	SET /a BACK = 0
	GOTO :EOF
)
GOTO :START

:CONFIRMATION
CALL :SETSTATICID
IF %CHOICE% EQU !PLAYER%TYPE%ID! (
	CALL :UNEQUIPCONFIRMATION
) ELSE (
	CALL :EQUIPCONFIRMATION
)
GOTO :EOF

:SETSTATICID
SET /a COUNT = 0
:LOOPSTART
SET /a COUNT = %COUNT% + 1
IF "!%TYPE%%COUNT%NAME!" EQU "!TEXT%SELECTED%NAME1!" (
	SET /a CHOICE = %COUNT%
	GOTO :EOF
)
GOTO :LOOPSTART

:UNEQUIPCONFIRMATION
CALL "%MENURESOURCEPATH%\menudisplay.bat"
ECHO %MENUNAME%: Are you sure you want to unequip your !%TYPE%%CHOICE%NAME!?
ECHO.
ECHO. 1 - Yes
ECHO. 0 - Actually I've changed my mind
ECHO.
SET /P UNEQUIPCHOICE=
ECHO.
IF "%UNEQUIPCHOICE%" EQU "1" (
	SET /a BACK = 1
	ECHO !%TYPE%%CHOICE%NAME! Unequipped^^!
	CALL :WAITFORTWO
	CALL :UNEQUIP%TYPE%
	
) ELSE IF "%UNEQUIPCHOICE%" EQU "0" (
	GOTO :EOF
) ELSE (
	GOTO :UNEQUIPCONFIRMATION
)
GOTO :EOF

:EQUIPCONFIRMATION
CALL "%MENURESOURCEPATH%\menudisplay.bat"
ECHO %MENUNAME%: Are you sure you want to equip the !%TYPE%%CHOICE%NAME!?
ECHO.
ECHO. 1 - Yes
ECHO. 0 - Actually I've changed my mind
ECHO.
SET /P EQUIPCHOICE=
ECHO.
IF "%EQUIPCHOICE%" EQU "1" (
	SET /a BACK = 1
	CALL :UNEQUIP%TYPE%
	CALL :EQUIP%TYPE%
) ELSE IF "%EQUIPCHOICE%" EQU "0" (
	GOTO :EOF
) ELSE (
	GOTO :EQUIPCONFIRMATION
)
GOTO :EOF

:UNEQUIPWEAPON
IF !WEAPON%PLAYERWEAPONID%SKILLTOT! EQU 0 (
	GOTO :UNEQUIPWEAPONLOOPEND
)
SET /a COUNT = 0
:UNEQUIPWEAPONLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !WEAPON%PLAYERWEAPONID%SKILL%COUNT%!
IF !PLAYERSKILL%TEMP%EARNED! EQU 0 (
	SET /a PLAYERSKILL%TEMP%ENABLED = 0
)
IF %COUNT% LSS !WEAPON%PLAYERWEAPONID%SKILLTOT! (
	GOTO :UNEQUIPWEAPONLOOP
)
:UNEQUIPWEAPONLOOPEND
SET PLAYERWEAPON=NONE
SET /a PLAYERWEAPONID = 0
SET /a PLAYEREQUIPSTR = 0
SET /a PLAYEREQUIPMAG = 0
SET /a PLAYEREQUIPACC = 0
GOTO :EOF

:EQUIPWEAPON
SET PLAYERWEAPON=!WEAPON%CHOICE%NAME!
SET /a PLAYERWEAPONID = %CHOICE%
SET /a PLAYEREQUIPSTR=!WEAPON%CHOICE%STR!
SET /a PLAYEREQUIPMAG=!WEAPON%CHOICE%MAG!
SET /a PLAYEREQUIPACC=!WEAPON%CHOICE%ACC!
IF !WEAPON%PLAYERWEAPONID%SKILLTOT! EQU 0 (
	GOTO :EQUIPWEAPONLOOPEND
)
SET /a COUNT = 0
:EQUIPWEAPONLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !WEAPON%PLAYERWEAPONID%SKILL%COUNT%!
SET /a PLAYERSKILL%TEMP%ENABLED = 1
IF %COUNT% LSS !WEAPON%PLAYERWEAPONID%SKILLTOT! (
	GOTO :EQUIPWEAPONLOOP
)
:EQUIPWEAPONLOOPEND
ECHO. %PLAYERWEAPON% Equipped^^!
ECHO. 
CALL :WAITFORTWO
GOTO :EOF

:UNEQUIPARMOUR
SET PLAYERARMOUR=NONE
SET /a PLAYERARMOURID=0
SET /a PLAYEREQUIPEND=0
SET /a PLAYEREQUIPSPR=0
SET /a PLAYEREQUIPEVA=0
SET /a PLAYEREQUIPSPD=0
SET /a PLAYERWEAKNESSNUM=0
SET /a PLAYERSTRENGTHNUM=0
SET /a PLAYERELEMENTIMMUNENUM=0
SET /a PLAYERABSORBNUM=0
SET /a PLAYERSTATUSIMMUNENUM=0
GOTO :EOF

:EQUIPARMOUR
SET PLAYERARMOUR=!ARMOUR%CHOICE%NAME!
SET /a PLAYERARMOURID=%CHOICE%
SET /a PLAYEREQUIPEND=!ARMOUR%CHOICE%END!
SET /a PLAYEREQUIPSPR=!ARMOUR%CHOICE%SPR!
SET /a PLAYEREQUIPEVA=!ARMOUR%CHOICE%EVA!
SET /a PLAYEREQUIPSPD=!ARMOUR%CHOICE%SPD!
SET /a PLAYERWEAKNESSNUM=!ARMOUR%CHOICE%WEAKNESSNUM!
SET /a PLAYERSTRENGTHNUM=!ARMOUR%CHOICE%STRENGTHNUM!
SET /a PLAYERELEMENTIMMUNENUM=!ARMOUR%CHOICE%ELEMENTIMMUNENUM!
SET /a PLAYERABSORBNUM=!ARMOUR%CHOICE%ABSORBNUM!
SET /a PLAYERSTATUSIMMUNENUM=!ARMOUR%CHOICE%STATUSIMMUNENUM!
CALL :SETRESISTANCES
ECHO. %PLAYERARMOUR% Equipped^^!
ECHO. 
CALL :WAITFORTWO
GOTO :EOF

:SETRESISTANCES
SET /a COUNT = 0
IF %PLAYERWEAKNESSNUM% EQU 0 (
	GOTO :STRENGTHS3
)
:WEAKNESSLOOP3
SET /a COUNT = %COUNT% + 1
SET PLAYERWEAKNESS%COUNT%=!ARMOUR%CHOICE%WEAKNESS%COUNT%!
IF %COUNT% LSS %PLAYERWEAKNESSNUM% (
	GOTO :WEAKNESSLOOP3
)
:STRENGTHS3
SET /a COUNT = 0
IF %PLAYERSTRENGTHNUM% EQU 0 (
	GOTO :IMMUNES3
)
:STRENGTHLOOP3
SET /a COUNT = %COUNT% + 1
SET PLAYERSTRENGTH%COUNT%=!ARMOUR%CHOICE%STRENGTH%COUNT%!
IF %COUNT% LSS %PLAYERSTRENGTHNUM% (
	GOTO :STRENGTHLOOP3
)
:IMMUNES3
SET /a COUNT = 0
IF %PLAYERELEMENTIMMUNENUM% EQU 0 (
	GOTO :ABSORBS3
)
:IMMUNELOOP3
SET /a COUNT = %COUNT% + 1
SET PLAYERELEMENTIMMUNE%COUNT%=!ARMOUR%CHOICE%ELEMENTIMMUNE%COUNT%!
IF %COUNT% LSS %PLAYERELEMENTIMMUNENUM% (
	GOTO :IMMUNELOOP3
)
:ABSORBS3
SET /a COUNT = 0
IF %PLAYERABSORBNUM% EQU 0 (
	GOTO :STATUSIMMUNES3
)
:ABSORBLOOP3
SET /a COUNT = %COUNT% + 1
SET PLAYERABSORB%COUNT%=!ARMOUR%CHOICE%ABSORB%COUNT%!
IF %COUNT% LSS %PLAYERABSORBNUM% (
	GOTO :ABSORBLOOP3
)
:STATUSIMMUNES3
SET /a COUNT = 0
IF %PLAYERSTATUSIMMUNENUM% EQU 0 (
	GOTO :EOF
)
:STATUSIMMUNELOOP3
SET /a COUNT = %COUNT% + 1
SET PLAYERSTATUSIMMUNE%COUNT%=!ARMOUR%CHOICE%STATUSIMMUNE%COUNT%!
IF %COUNT% LSS %PLAYERSTATUSIMMUNENUM% (
	GOTO :STATUSIMMUNELOOP3
)
GOTO :EOF


:UNEQUIPACCESSORY
IF !ACCESSORY%PLAYERACCESSORYID%SPECIALTOT! EQU 0 (
	GOTO :UNEQUIPACCESSORYEND
)
SET /a COUNT = 0
:UNEQUIPACCESSORYLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !ACCESSORY%PLAYERACCESSORYID%SPECIAL%COUNT%!
SET /a PLAYER!SPECIAL%TEMP%NICKNAME! = 0
IF %COUNT% LSS !ACCESSORY%PLAYERACCESSORYID%SPECIALTOT! (
	GOTO :UNEQUIPACCESSORYLOOP
)
:UNEQUIPACCESSORYEND
SET PLAYERACCESSORY=NONE
SET /a PLAYERACCESSORYID = 0
SET /a PLAYEREQUIPMAXHP = 0
SET /a PLAYEREQUIPMAXMP = 0
SET /a PLAYEREQUIPLCK = 0
GOTO :EOF

:EQUIPACCESSORY
SET PLAYERACCESSORY=!ACCESSORY%CHOICE%NAME!
SET /a PLAYERACCESSORYID = %CHOICE%
SET /a PLAYEREQUIPMAXHP=!ACCESSORY%CHOICE%MAXHP!
SET /a PLAYEREQUIPMAXMP=!ACCESSORY%CHOICE%MAXMP!
SET /a PLAYEREQUIPLCK=!ACCESSORY%CHOICE%LCK!
SET /a PLAYERMAXHP = %PLAYERBASEMAXHP% + %PLAYEREQUIPMAXHP%
SET /a PLAYERMAXMP = %PLAYERBASEMAXMP% + %PLAYEREQUIPMAXMP%
IF %PLAYERHP% GTR %PLAYERMAXHP% (
	SET /a PLAYERHP = %PLAYERMAXHP%
)
IF %PLAYERMP% GTR %PLAYERMAXMP% (
	SET /a PLAYERMP = %PLAYERMAXMP%
)
IF !ACCESSORY%PLAYERACCESSORYID%SPECIALTOT! EQU 0 (
	GOTO :EQUIPACCESSORYLOOPEND
)
SET /a COUNT = 0
:EQUIPACCESSORYLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !ACCESSORY%PLAYERACCESSORYID%SPECIAL%COUNT%!
SET /a PLAYER!SPECIAL%TEMP%NICKNAME! = 1
IF %COUNT% LSS !ACCESSORY%PLAYERACCESSORYID%SPECIALTOT! (
	GOTO :EQUIPACCESSORYLOOP
)
:EQUIPACCESSORYLOOPEND
ECHO. %PLAYERACCESSORY% Equipped^^!
ECHO. 
CALL :WAITFORTWO
GOTO :EOF


:TUTORIAL
ECHO %UNCLENAME%: This is a new menu type, you can tell because of the arrows on the left of the options (¯).
ECHO Here are the controls:
ECHO.
ECHO. 8 or W - Up
ECHO. 5 or S - Down
ECHO. 6 or D - Select
ECHO. 0 - Back           
ECHO.
ECHO Try going up and down a bit and then select a weapon with 6 or D
ECHO.
GOTO :EOF


:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF
SET TARGET=PLAYER
CALL "%ENEMYSKILLRESOURCEPATH%\standarddam.bat"
CALL "%ENEMYSKILLRESOURCEPATH%\dam.bat"
IF %MISS% EQU 1 (
	GOTO :EOF
)
CALL :STEALQUERY
IF %STEAL% EQU 0 (
	ECHO %ENEMYNAME% failed to steal from %PLAYERNAME%
	ECHO.
	CALL :WAITFORTWO
	GOTO :EOF
)
CALL :ITEMQUERY
IF %ITEMSTOLEN% EQU %ALIFEID% (
	GOTO :EOF
)
CALL :STEALTEXT
GOTO :EOF

:STEALQUERY
SET /a STEAL = 0
SET /a RANPERC = %RANDOM% %% 100
IF %RANPERC% GTR %PLAYERLCK% (
	SET /a STEAL = 1
)
GOTO :EOF

:ITEMQUERY
SET /a COUNT = 0
SET /a ITEMMUGCHANCETOT = 0
SET /a ITEM0MUGCHANCECUM = 0
:ITEMLOOP
SET /a OLDCOUNT = %COUNT%
SET /a COUNT = %COUNT% + 1
SET /a ITEM%COUNT%MUGCHANCECUM = !ITEM%OLDCOUNT%MUGCHANCECUM! + ((!PLAYERITEM%COUNT%NUM!*!ITEM%COUNT%COST!)/10)
IF %COUNT% LSS %ITEMTOTAL% (
	GOTO :ITEMLOOP
)
SET /a ITEMMUGCHANCETOT = !ITEM%ITEMTOTAL%MUGCHANCECUM!
SET /a RANITEM = %RANDOM% %% %ITEMMUGCHANCETOT%
SET /a COUNT = 0
:ITEMLOOP2
SET /a COUNT = %COUNT% + 1
IF !ITEM%COUNT%MUGCHANCECUM! GTR %RANITEM% (
	SET /a ITEMSTOLEN = %COUNT%
) ELSE IF %COUNT% LSS %ITEMTOTAL% (
	GOTO :ITEMLOOP2
)
IF %ITEMSTOLEN% EQU %ALIFEID% (
	ECHO %ENEMYNAME% failed to steal from %PLAYERNAME%
	ECHO.
	CALL :WAITFORTWO
	GOTO :EOF
)
SET /a ENEMYITEMCHANCE = 100
SET /a ENEMYITEMNUM = 1
SET /a ENEMYITEM1 = %ITEMSTOLEN%
SET /a ENEMYITEM%ITEMSTOLEN%NUM = 1
SET /a ENEMYITEM1CHANCE = 100
SET /a PLAYERITEM%ITEMSTOLEN%NUM = !PLAYERITEM%ITEMSTOLEN%NUM! - 1
GOTO :EOF

:STEALTEXT
ECHO The %ENEMYNAME% stole one !ITEM%ITEMSTOLEN%NAME!
ECHO.
CALL :WAITFORTWO
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

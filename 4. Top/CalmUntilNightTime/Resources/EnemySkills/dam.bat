::Needs %TARGET,%DAM%,%MISSCHANCE%,%CRITICALCHANCE%

SET /a MISS = 0
CALL :RANDOMISE
CALL :MISSQUERY
CALL :PROTECTQUERY
CALL :CRITICALQUERY
CALL :SKILLTEXT
CALL :ENDBATTLEQUERY
GOTO :EOF

:RANDOMISE
SET /a RANFACT = (%RANDOM% %% 40) + 80
SET /a HITCHANCE = (%HITCHANCE%*100)/%RANFACT%
SET /a RANFACT = (%RANDOM% %% 40) + 80
SET /a CRITICALCHANCE = (%CRITICALCHANCE%*100)/%RANFACT%
IF /I %ENEMYSKILLNICKNAME% EQU CHOMP (
	GOTO :RANDOMEND
) ELSE IF /I %ENEMYSKILLNICKNAME% EQU SUBOMB (
	GOTO :RANDOMEND
)
SET /a RANFACT = (%RANDOM% %% 40) + 80
SET /a DAM = (%DAM%*100)/%RANFACT%
:RANDOMEND
IF %DAM% LSS 1 (
	SET /a DAM = 1
)
GOTO :EOF

:MISSQUERY
SET /a RANMISSCHANCE = %RANDOM% %% 100
IF %HITCHANCE% LSS %RANMISSCHANCE% (
	SET /a MISS = 1
)
GOTO :EOF

:PROTECTQUERY
IF %MISS% EQU 1 (
	GOTO :EOF
)
IF !ENEMYSKILL%ENEMYSKILLCHOICE%PROTECTABLE! EQU 1 (
	IF %PLAYERPROTECT% EQU 1 (
		SET /a DAM = %DAM%/2
		ECHO Protect halved the effectiveness of the %ENEMYSKILLNAME%^^!
		ECHO.
		CALL :WAITFORTWO
	)
)
GOTO :EOF

:CRITICALQUERY
IF %MISS% EQU 1 (
	GOTO :EOF
)
SET /a RANCRITICALCHANCE = %RANDOM% %% 100
IF %CRITICALCHANCE% GTR %RANCRITICALCHANCE% (
	CALL :CRITICAL
)
GOTO :EOF

:CRITICAL
SET /a DAM = (%DAM%*3)/2
ECHO Critical Hit^^!
ECHO.
CALL :WAITFORTWO
GOTO :EOF

:SKILLTEXT
IF %MISS% EQU 1 (
	ECHO The %ENEMYSKILLNAME% missed^^!
	ECHO.
	GOTO :EOF
)
SET /a %TARGET%HP = !%TARGET%HP! - %DAM%
IF /I %TARGET% EQU PLAYER (
	ECHO The %ENEMYSKILLNAME% did %DAM% damage to !%TARGET%NAME!
	ECHO.
	IF %PLAYERLIMITBREAKACTIVE% EQU 0 (
		SET /a PLAYERLIMITBREAK = %PLAYERLIMITBREAK%+%DAM%
	)
	IF %PLAYERDOUBLELIM% EQU 1 (
		SET /a PLAYERLIMITBREAK = %PLAYERLIMITBREAK%+%DAM%
	)
	IF %PLAYERLIMITBREAK% LSS 0 (
		SET /a PLAYERLIMITBREAK = 0
	) ELSE IF %PLAYERLIMITBREAK% GTR %PLAYERMAXLIMITBREAK% (
		SET /a PLAYERLIMITBREAK = %PLAYERMAXLIMITBREAK%
	)
) ELSE (
	ECHO %ENEMYNAME% was hurt by %DAM% HP whilst performing %ENEMYSKILLNAME%
	ECHO.
)
CALL :WAITFORTWO
GOTO :EOF

:ENDBATTLEQUERY
IF !%TARGET%HP! LEQ 0 (
	SET /a %TARGET%HP = 0
	IF %TARGET% EQU ENEMY (
		SET /a WINBATTLE = 1
	) ELSE (
		SET /a LOSEBATTLE = 1
	)
)
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF
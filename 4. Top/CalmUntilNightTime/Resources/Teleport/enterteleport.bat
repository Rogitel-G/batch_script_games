GOTO :START
::Testing
@ECHO OFF
SETLOCAL ENABLEDELAYEDEXPANSION
SET RESOURCEPATH=D:\Users\NDSisson\Desktop\Desktop 2\Recent\Game\Resources
CALL "%RESOURCEPATH%\Setup\Initial\teleportlocations.bat"
CALL "%RESOURCEPATH%\Setup\Initial\mapnicknames.bat"
CALL "%RESOURCEPATH%\Setup\New\initialteleport.bat"
SET MAP=TOWN2
SET /a TOWN2TELEPORTACTIVE = 1
SET /a TOWN4TELEPORTACTIVE = 1
:::::

:START
CALL :SETLOCATION
IF /I %MAP% EQU TOWN1 (
	CALL :TOWN1
	GOTO :EOF
)
CALL :DYNAMIC
CALL "%TELEPORTRESOURCEPATH%\teleportdisplay.bat"
ECHO Where would you like to teleport to?
ECHO.
SET /a COUNT = 0
:TELEPORTCHOICELOOP
SET /a COUNT = %COUNT% + 1
ECHO. %COUNT% - !DYNAMICTOWN%COUNT%NICKNAME!
IF %COUNT% LSS %DYNAMICTOWNTOT% (
	GOTO :TELEPORTCHOICELOOP
)
ECHO. 0 - Back
ECHO.
:CHOICE
SET /P DYNAMICCHOICE=
ECHO.
IF "%DYNAMICCHOICE%" EQU "" (
	GOTO :CHOICE
)
IF %DYNAMICCHOICE% EQU 0 (
	GOTO :EOF
) ELSE IF %DYNAMICCHOICE% GTR %DYNAMICTOWNTOT% (
	GOTO :CHOICE
) ELSE IF %DYNAMICCHOICE% LSS 0 (
	GOTO :CHOICE
)
CALL :SETSTATICID
CALL :TELEPORT
GOTO :EOF



:DYNAMIC
SET /a COUNT = 0
SET /a STATICCOUNT = 0
:LOOPSTART
SET /a STATICCOUNT = %STATICCOUNT%+1
IF !TOWN%STATICCOUNT%TELEPORTACTIVE! EQU 1 (
	CALL :SET
)
IF %STATICCOUNT% LSS %TOWNTOT% (
	GOTO :LOOPSTART
)
SET /a DYNAMICTOWNTOT = %COUNT%
GOTO :EOF

:SET
SET /a COUNT=%COUNT%+1
SET /a DYNAMICTOWN%COUNT%ID=%STATICCOUNT%
SET DYNAMICTOWN%COUNT%NICKNAME=!TOWN%STATICCOUNT%NICKNAME!
GOTO :EOF


:SETSTATICID
SET /a CHOICE = 0
SET /a TOWNCOUNT = 0
:SETSTATICIDLOOP
SET /a TOWNCOUNT = %TOWNCOUNT% + 1
IF /I !DYNAMICTOWN%DYNAMICCHOICE%NICKNAME! EQU !TOWN%TOWNCOUNT%NICKNAME! (
	SET /a CHOICE = %TOWNCOUNT%
) ELSE IF %TOWNCOUNT% LSS %TOWNTOT% (
	GOTO :SETSTATICIDLOOP
)
GOTO :EOF

:SETLOCATION
IF /I %MAP% EQU TOWN1 (
	SET /a PLAYERXCOORD = 8
	SET /a PLAYERYCOORD = 3
) ELSE IF /I %MAP% EQU TOWN2 (
	SET /a PLAYERXCOORD = 5
	SET /a PLAYERYCOORD = 7
) ELSE IF /I %MAP% EQU TOWN3 (
	SET /a PLAYERXCOORD = 7
	SET /a PLAYERYCOORD = 5
) ELSE IF /I %MAP% EQU TOWN4 (
	SET /a PLAYERXCOORD = 10
	SET /a PLAYERYCOORD = 4
) ELSE IF /I %MAP% EQU TOWN5 (
	SET /a PLAYERXCOORD = 2
	SET /a PLAYERYCOORD = 4
) ELSE IF /I %MAP% EQU TOWN6 (
	SET /a PLAYERXCOORD = 10
	SET /a PLAYERYCOORD = 3
)
GOTO :EOF

:TELEPORT
SET /a EXITMAP = 1
SET MAP=town%CHOICE%
SET /a PLAYERXCOORD = !TOWN%CHOICE%TELEPORTXCOORD!
SET /a PLAYERYCOORD = !TOWN%CHOICE%TELEPORTYCOORD!
ECHO Teleporting to !TOWN%CHOICE%NICKNAME!...
ECHO.
GOTO :EOF

:TOWN1
CLS
ECHO.
ECHO This old thing's broken and always has been. Uncle %UNCLENAME% said that
ECHO it used to let people travel between towns, but I'll believe that when I see it
ECHO.
TIMEOUT /T 5 > nul
GOTO :EOF
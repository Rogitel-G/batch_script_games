:HIRESTART1
CALL "%MINERESOURCEPATH%\generatedwarves.bat"
CALL "%DYNAMICRESOURCEPATH%\dynamicdwarfhirelist.bat"
SET /a SELECTED = 1
:HIRESTART2
SET /a RESETDWARVES = 0
CLS
IF %DWARFSLOTS% EQU 0 (
	ECHO.
	ECHO You need to invest in your mine before you can employ dwarves
	ECHO.
	pause
	GOTO :EOF
)
ECHO.
ECHO Here are some dwarves who are looking for work, due to lazy programming
ECHO you can only pick one. Choose wisely...
ECHO.
CALL "%MISCRESOURCEPATH%\menu2.bat"
IF %VARSELECTED% EQU 1 (
	CALL :SELECTED
)
IF %BACK% EQU 1 (
	GOTO :EOF
)
IF %RESETDWARVES% EQU 1 (
	GOTO :HIRESTART1
) ELSE (
	GOTO :HIRESTART2
)


:SELECTED
SET /a NEWDWARFCHOICE = %SELECTED%
CLS
ECHO.
ECHO !NEWDWARF%NEWDWARFCHOICE%NAME! has the following stats:
ECHO Level        !NEWDWARF%NEWDWARFCHOICE%LVL!
ECHO Strength     !NEWDWARF%NEWDWARFCHOICE%BASESTR!
ECHO Skill        !NEWDWARF%NEWDWARFCHOICE%BASESKL!
ECHO Persistence  !NEWDWARF%NEWDWARFCHOICE%BASEPER!
ECHO Satisfaction !NEWDWARF%NEWDWARFCHOICE%SAT!
ECHO Speciality   !NEWDWARF%NEWDWARFCHOICE%SPECIALDISPLAY!
ECHO.
ECHO How much would you like to pay !NEWDWARF%NEWDWARFCHOICE%NAME!?
ECHO.
ECHO. 0 - Actually I don't want to hire !NEWDWARF%NEWDWARFCHOICE%NAME!, his name's stupid
ECHO.
:HIRECHOICE
SET /P SALARY=
ECHO.
IF "%SALARY%" EQU "" (
	GOTO :HIRECHOICE
) ELSE IF "%SALARY%" EQU "0" (
	GOTO :EOF
)
IF %SALARY% LSS 0 (
	ECHO You evil cunt, choose again
	ECHO.
	GOTO :HIRECHOICE
) ELSE IF %SALARY% GTR 99999 (
	ECHO That's a bit excessive don't you think. Let's be sensible
	ECHO.
	GOTO :HIRECHOICE
)
CALL :HIREDWARF
GOTO :EOF

:HIREDWARF
IF %DWARFTOT% GEQ %DWARFSLOTS% (
	CALL :REPLACEDWARFQUERY
) ELSE (
	CALL :HIREDWARF2
)
GOTO :EOF

:REPLACEDWARFQUERY
CLS
ECHO.
ECHO It looks like you have too many dwarves in your employ
ECHO.
ECHO Choose one to replace
ECHO.
SET /a REPLACECOUNT = 0
:REPLACELOOP
SET /a REPLACECOUNT = %REPLACECOUNT% + 1
ECHO. %REPLACECOUNT% - !DWARF%REPLACECOUNT%NAME!
IF %REPLACECOUNT% LSS %DWARFTOT% (
	GOTO :REPLACELOOP
)
ECHO. 0 - Back
ECHO.
:REPLACECHOICE
SET /P DWARFCHOICE=
IF "%DWARFCHOICE%" EQU "" (
	GOTO :REPLACECHOICE
) ELSE IF "%DWARFCHOICE%" EQU "0" (
	GOTO :EOF
)
IF %DWARFCHOICE% GTR %DWARFTOT% (
	GOTO :REPLACECHOICE
) ELSE IF %DWARFCHOICE% LSS 0 (
	GOTO :REPLACECHOICE
)
CALL :HIREDWARF2
GOTO :EOF

:HIREDWARF2
IF %DWARFTOT% LSS %DWARFSLOTS% (
	SET /a DWARFCHOICE = %DWARFTOT% + 1
	SET /a DWARFTOT = %DWARFTOT% + 1
)
SET DWARF%DWARFCHOICE%NAME=!NEWDWARF%NEWDWARFCHOICE%NAME!
SET /a DWARF%DWARFCHOICE%NAMELENGTH = !NEWDWARF%NEWDWARFCHOICE%NAMELENGTH!
SET /a DWARF%DWARFCHOICE%LVL = !NEWDWARF%NEWDWARFCHOICE%LVL!
SET /a DWARF%DWARFCHOICE%EXP = !NEWDWARF%NEWDWARFCHOICE%EXP!
SET /a DWARF%DWARFCHOICE%BASESTR = !NEWDWARF%NEWDWARFCHOICE%BASESTR!
SET /a DWARF%DWARFCHOICE%BASESKL = !NEWDWARF%NEWDWARFCHOICE%BASESKL!
SET /a DWARF%DWARFCHOICE%BASEPER = !NEWDWARF%NEWDWARFCHOICE%BASEPER!
SET /a DWARF%DWARFCHOICE%SAT = !NEWDWARF%NEWDWARFCHOICE%SAT!
SET /a DWARF%DWARFCHOICE%SALARY = %SALARY%
SET /a DWARF%DWARFCHOICE%PICKAXEID = 0
SET /a DWARF%DWARFCHOICE%SACKID = 0
SET /a DWARF%DWARFCHOICE%PICKAXESTR = 0
SET /a DWARF%DWARFCHOICE%PICKAXESKL = 0
SET /a DWARF%DWARFCHOICE%PICKAXEPER = 0
SET /a DWARF%DWARFCHOICE%SACKSTR = 0
SET /a DWARF%DWARFCHOICE%SACKSKL = 0
SET /a DWARF%DWARFCHOICE%SACKPER = 0
SET DWARF%DWARFCHOICE%SPECIAL=!NEWDWARF%NEWDWARFCHOICE%SPECIAL!
SET DWARF%DWARFCHOICE%SPECIALDISPLAY=!NEWDWARF%NEWDWARFCHOICE%SPECIALDISPLAY!
SET DWARF%DWARFCHOICE%TYPE=%MINEITEM1TYPE%
SET DWARF%DWARFCHOICE%TYPE2=%MINEITEM1SPECIAL%
SET DWARF%DWARFCHOICE%TYPEDISPLAY=%MINEITEM1SPECIALDISPLAY%
SET TEMP=!DWARF%DWARFCHOICE%TYPE2!
SET /a DWARF%DWARFCHOICE%MINID = !%TEMP%START!
SET /a DWARF%DWARFCHOICE%MAXID = !%TEMP%END!
SET /a DWARF%DWARFCHOICE%TRAINING = 0
SET DWARF%DWARFCHOICE%TRAININGSTAT=NONE
SET /a RESETDWARVES = 1
SET /a DWARFCOUNT = %DWARFCHOICE%
CALL "%MINERESOURCEPATH%\resetminetimes.bat"
SET PREFIX=DWARF%DWARFCHOICE%TRAINING
CALL "%MISCRESOURCEPATH%\calculatedatetime.bat"
GOTO :EOF



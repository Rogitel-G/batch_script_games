@ECHO OFF
SET /a MAXSPACENUMBEFORE = 43
SET /a MAXSPACENUMAFTER = 43
::SET /a MAXSPACENUMAFTER = 52
SET /a MAXSPACENUMBEFORE2 = %MAXSPACENUMAFTER% - %MAXSPACENUMBEFORE%
:START
CLS
ECHO.
IF %DWARFTOT% EQU 0 (
	ECHO You don't have any dwarves in your employ
	ECHO.
	CALL :WAITFORTHREE
	GOTO :EOF
)
ECHO Dwarf Sultan: Who would you like to collect items from?
ECHO.
SET /a COUNT = 0
:COLLECTLOOP
SET /a COUNT = %COUNT% + 1
SET /a SPACESNUM = 12-!DWARF%COUNT%NAMELENGTH!
ECHO. %COUNT% - !DWARF%COUNT%NAME!!SPACES%SPACESNUM%!(Level !DWARF%COUNT%LVL!)
IF %COUNT% LSS %DWARFTOT% (
	GOTO :COLLECTLOOP
)
ECHO. A - All
ECHO. 0 - Back
ECHO.
:COLLECTCHOICE
SET /P DWARFCOUNT=
ECHO.
IF "%DWARFCOUNT%" EQU "" (
	GOTO :COLLECTCHOICE
) ELSE IF /I "%DWARFCOUNT%" EQU "A" (
	CALL :COLLECTALL
	GOTO :EOF
) ELSE IF /I "%DWARFCOUNT%" EQU "0" (
	GOTO :EOF
)
IF %DWARFCOUNT% LSS 0 (
	GOTO :COLLECTCHOICE
) ELSE IF %DWARFCOUNT% GTR %DWARFTOT% (
	GOTO :COLLECTCHOICE
)
SET /a DWARVENREVOLT = 0
SET /a REWARDCOUNT = 0
SET /a SUCCESS = 0
IF !DWARF%DWARFCOUNT%TRAINING! EQU 1 (
	CLS
	ECHO.
	ECHO Dwarf Sultan: !DWARF%DWARFCOUNT%NAME! is currently in training.
	ECHO.
	CALL :WAITFORTHREE
	GOTO :EOF
)
CALL :INITIALISE
CALL :HAPPINESS
CALL :NEWSTATS
CALL :HAULNUM
IF %HAULS% EQU 0 (
	CLS
	ECHO.
	ECHO !DWARF%DWARFCOUNT%NAME! either hasn't mined long enough to find anything or isn't happy enough to work
	ECHO.
	ECHO You'll need to be more patient
	CALL :WAITFORFIVE
	GOTO :EOF
)
CALL :TOTALHAPPINESS
CALL :COST
IF %DWARVENREVOLT% EQU 1 (
	CALL "%MINERESOURCEPATH%\dwarvenrevolution.bat"
	GOTO :EOF
)
CALL :ITEMCALCULATIONS
SET /a DWARF%DWARFCOUNT%REWARDTOT = %REWARDCOUNT%
CALL :CONSOLIDATEREWARDS
CALL "%MINERESOURCEPATH%\resetminetimes.bat"
GOTO :EOF

:COLLECTALL
SET /a DWARFCOUNT = 0
:INITIALDWARFLOOP
SET /a DWARFCOUNT = %DWARFCOUNT% + 1
SET /a DWARVENREVOLT = 0
SET /a SUCCESS = 0
IF !DWARF%DWARFCOUNT%TRAINING! EQU 1 (
	CLS
	ECHO.
	ECHO Dwarf Sultan: !DWARF%DWARFCOUNT%NAME! is currently in training.
	ECHO.
	CALL :WAITFORTHREE
	GOTO :ENDINITIALDWARFLOOP
)
CALL :INITIALISE
CALL :HAPPINESS
CALL :NEWSTATS
CALL :HAULNUM
IF %HAULS% EQU 0 (
	CLS
	ECHO.
	ECHO !DWARF%DWARFCOUNT%NAME! either hasn't mined long enough to find anything or isn't happy enough to work
	ECHO.
	ECHO You'll need to be more patient
	CALL :WAITFORTHREE
	GOTO :ENDINITIALDWARFLOOP
)
CALL :TOTALHAPPINESS
CALL :COST
IF %DWARVENREVOLT% EQU 1 (
	CALL "%MINERESOURCEPATH%\dwarvenrevolution.bat"
	GOTO :EOF
)
CALL :ITEMCALCULATIONS
SET /a DWARF%DWARFCOUNT%REWARDTOT = %REWARDCOUNT%
CALL :CONSOLIDATEREWARDS
:ENDINITIALDWARFLOOP
IF %DWARFCOUNT% LSS %DWARFTOT% (
	GOTO :INITIALDWARFLOOP
)
SET /a DWARFCOUNT = 0
:INITIALDWARFLOOP2
SET /a DWARFCOUNT = %DWARFCOUNT% + 1
CALL "%MINERESOURCEPATH%\resetminetimes.bat"
IF %DWARFCOUNT% LSS %DWARFTOT% (
	GOTO :INITIALDWARFLOOP2
)
GOTO :EOF

:INITIALISE
SET TYPE=!DWARF%DWARFCOUNT%TYPE!
SET TYPE2=!DWARF%DWARFCOUNT%TYPE2!
SET /a DWARF%DWARFCOUNT%REWARDTOT = 0
GOTO :EOF

:HAPPINESS
SET /a ARROGANCE = (2*!DWARF%DWARFCOUNT%LVL!) - !DWARF%DWARFCOUNT%SAT!
SET /a TEMP = (!DWARF%DWARFCOUNT%LVL!/2) + 1
IF %ARROGANCE% LSS %TEMP% (
	SET /a ARROGANCE = %TEMP%
)
SET /a EXPECTEDSALARY = !DWARF%DWARFCOUNT%LVL!*%ARROGANCE%/2
SET /a HAPPINESS = (!DWARF%DWARFCOUNT%SALARY!*100)/%EXPECTEDSALARY%
GOTO :EOF

:TOTALHAPPINESS
SET /a TOTALDWARFHAPPINESS = %TOTALDWARFHAPPINESS% - 100
SET /a TOTALDWARFHAPPINESS = %TOTALDWARFHAPPINESS% + %HAPPINESS%
IF %TOTALDWARFHAPPINESS% GTR %MAXTOTALDWARFHAPPINESS% (
	SET /a TOTALDWARFHAPPINESS = %MAXTOTALDWARFHAPPINESS%
)
GOTO :EOF

:NEWSTATS
IF /I !DWARF%DWARFCOUNT%SPECIAL! EQU !DWARF%DWARFCOUNT%TYPE2! (
	SET /a SPECIALMULTIPLIER = 3
) ELSE (
	SET /a SPECIALMULTIPLIER = 2
)
SET /a DWARF%DWARFCOUNT%EQUIPSTR = !DWARF%DWARFCOUNT%PICKAXESTR! + !DWARF%DWARFCOUNT%SACKSTR!
SET /a DWARF%DWARFCOUNT%EQUIPSKL = !DWARF%DWARFCOUNT%PICKAXESKL! + !DWARF%DWARFCOUNT%SACKSKL!
SET /a DWARF%DWARFCOUNT%EQUIPPER = !DWARF%DWARFCOUNT%PICKAXEPER! + !DWARF%DWARFCOUNT%SACKPER!
SET /a RANFACT = (%RANDOM% %% 40)+80
SET /a DWARF%DWARFCOUNT%STR = ((!DWARF%DWARFCOUNT%BASESTR! + !DWARF%DWARFCOUNT%EQUIPSTR!)*%HAPPINESS%*%RANFACT%*%SPECIALMULTIPLIER%)/20000
IF !DWARF%DWARFCOUNT%STR! LEQ 0 (
	SET /a DWARF%DWARFCOUNT%STR = 1
)
SET /a RANFACT = (%RANDOM% %% 40)+80
SET /a DWARF%DWARFCOUNT%SKL = ((!DWARF%DWARFCOUNT%BASESKL! + !DWARF%DWARFCOUNT%EQUIPSKL!)*%HAPPINESS%*%RANFACT%*%SPECIALMULTIPLIER%)/20000
IF !DWARF%DWARFCOUNT%SKL! LEQ 0 (
	SET /a DWARF%DWARFCOUNT%SKL = 1
)
SET /a RANFACT = (%RANDOM% %% 40)+80
SET /a DWARF%DWARFCOUNT%PER = ((!DWARF%DWARFCOUNT%BASEPER! + !DWARF%DWARFCOUNT%EQUIPPER!)*%HAPPINESS%*%RANFACT%*%SPECIALMULTIPLIER%)/20000
IF !DWARF%DWARFCOUNT%PER! LEQ 0 (
	SET /a DWARF%DWARFCOUNT%PER = 1
)
GOTO :EOF

:HAULNUM
SET PREFIX=NOW
CALL "%MISCRESOURCEPATH%\calculatedatetime.bat"
SET /a YEARDIFF = %NOWYEAR%-!DWARF%DWARFCOUNT%YEAR!
SET /a YEARDIFFMIN = %YEARDIFF%*60*24*365
SET /a MONTHDIFF = %NOWMONTH%-!DWARF%DWARFCOUNT%MONTH!
SET /a MONTHDIFFMIN = %MONTHDIFF%*60*24*30
SET /a DAYDIFF = %NOWDAY%-!DWARF%DWARFCOUNT%DAY!
SET /a DAYDIFFMIN = %DAYDIFF%*60*24
SET /a HOURDIFF = %NOWHOUR%-!DWARF%DWARFCOUNT%HOUR!
SET /a HOURDIFFMIN = %HOURDIFF%*60
SET /a MINDIFFMIN = %NOWMIN%-!DWARF%DWARFCOUNT%MIN!
SET /a MINDIFF = %YEARDIFFMIN%+%MONTHDIFFMIN%+%DAYDIFFMIN%+%HOURDIFFMIN%+%MINDIFFMIN%
REM 1 haul every 20 minutes
SET /a HAULS = %MINDIFF%/20
IF %TELEPORTREMOTEACCESS% EQU 0 (
	CALL :TELESTONE
)
SET /a MAXHAULS = %PLAYERLVL%/5
IF %HAULS% GTR %MAXHAULS% (
	SET /a HAULS = %MAXHAULS%
)
SET /a TEMP = %HAULS%*%HAULS%
IF %TEMP% GEQ !DWARF%DWARFCOUNT%PER! (
	CALL :SQRTCALC
)
GOTO :EOF

:TELESTONE
IF %PLAYERHAULTOTAL% GEQ 10 (
	SET /a PLAYERKEYITEM%TELESTONEID%ACQUIRED = 1
)
GOTO :EOF

:SQRTCALC
SET /a COUNT = 0
:SQRTCALCLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !DWARF%DWARFCOUNT%PER!/%COUNT%
IF %TEMP% LSS %COUNT% (
	SET /a HAULS = %COUNT% - 1
) ELSE (
	GOTO :SQRTCALCLOOP
)
GOTO :EOF

:COST
SET /a TOTALCOST = !DWARF%DWARFCOUNT%SALARY!*%HAULS%
IF %PLAYERGIL% LSS %TOTALCOST% (
	CALL :DWARVENREVOLT
) ELSE IF %TOTALDWARFHAPPINESS% LEQ 0 (
	SET /a TOTALDWARFHAPPINESS = %INITIALTOTALDWARFHAPPINESS%
	CALL :DWARVENREVOLT
) ELSE (
	CALL :PAYDWARVES
)
GOTO :EOF

:PAYDWARVES
SET /a PLAYERGIL = %PLAYERGIL% - %TOTALCOST%
SET /a PLAYERHAULTOTAL = %PLAYERHAULTOTAL% + %HAULS%
CLS
ECHO.
ECHO You have paid !DWARF%DWARFCOUNT%NAME! %TOTALCOST% Gil for %HAULS% hauls
ECHO.
ECHO You have %PLAYERGIL% Gil left
ECHO.
GOTO :EOF

:DWARVENREVOLT
CLS
ECHO.
IF %PLAYERGIL% LSS %TOTALCOST% (
	ECHO You cannot pay !DWARF%DWARFCOUNT%NAME!^^!
	ECHO.
)
ECHO Your dwarves have revolted^^!
ECHO.
SET /a DWARVENREVOLT = 1
GOTO :EOF


:ITEMCALCULATIONS
SET /a REWARDCOUNT = 0
:ITEMCALCULATIONSLOOP1
SET /a REWARDCOUNT = %REWARDCOUNT% + 1
CALL :DETERMINEITEM
CALL :DETERMINEITEMNUM
IF %REWARDCOUNT% LSS %HAULS% (
	GOTO :ITEMCALCULATIONSLOOP1
)
GOTO :EOF

:DETERMINEITEM
SET /a COUNT = !%TYPE2%START!
SET /a ACTIVECOUNT = 0
SET /a POTENTIALREWARD%ACTIVECOUNT%CHANCECUM = 0
:DETERMINEACTIVELOOP
SET /a COUNT = %COUNT% + 1
IF !DWARF%DWARFCOUNT%SKL! GEQ !%TYPE%%COUNT%MINSKL! (
	IF !DWARF%DWARFCOUNT%SKL! LEQ !%TYPE%%COUNT%MAXSKL! (
		CALL :ACTIVE
	)
)
IF %COUNT% LSS !%TYPE2%END! (
	GOTO :DETERMINEACTIVELOOP
)
SET /a ACTIVETOT = %ACTIVECOUNT%
CALL :DETERMINEITEMREWARD
GOTO :EOF

:ACTIVE
SET /a OLDACTIVECOUNT = %ACTIVECOUNT%
SET /a ACTIVECOUNT = %ACTIVECOUNT% + 1
SET /a POTENTIALREWARD%ACTIVECOUNT%ID = %COUNT%
REM The 2 is to make the minimum skill requirement matter more
SET /a POTENTIALREWARD%ACTIVECOUNT%CHANCE = (!%TYPE%%COUNT%MINSKL!*2)+!DWARF%DWARFCOUNT%LVL!
SET /a POTENTIALREWARD%ACTIVECOUNT%CHANCECUM = !POTENTIALREWARD%OLDACTIVECOUNT%CHANCECUM! + !POTENTIALREWARD%ACTIVECOUNT%CHANCE!
GOTO :EOF

:DETERMINEITEMREWARD
SET /a RANREWARD = %RANDOM% %% !POTENTIALREWARD%ACTIVETOT%CHANCECUM!
SET /a COUNT = 0
:DETERMINEITEMREWARDLOOP
SET /a COUNT = %COUNT% + 1
IF %RANREWARD% LEQ !POTENTIALREWARD%COUNT%CHANCECUM! (
	CALL :GETTHISREWARD
) ELSE (
	GOTO :DETERMINEITEMREWARDLOOP
)
GOTO :EOF

:GETTHISREWARD
SET /a DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%ID = !POTENTIALREWARD%COUNT%ID!
GOTO :EOF

:DETERMINEITEMNUM
SET /a ID = !DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%ID!
SET /a QUANTDIV = !%TYPE%%ID%QUANTDIV!
SET /a DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%NUM = !DWARF%DWARFCOUNT%STR!/%QUANTDIV%
GOTO :EOF

:CONSOLIDATEREWARDS
SET /a REWARDCOUNT = 0
:CONSOLIDATEREWARDSLOOP1
SET /a REWARDCOUNT = %REWARDCOUNT% + 1
SET /a TEMPCOUNT = 0
:CONSOLIDATEREWARDSLOOP2
SET /a TEMPCOUNT = %TEMPCOUNT% + 1
IF %TEMPCOUNT% EQU %REWARDCOUNT% (
	GOTO :CONSOLIDATEREWARDSLOOPEND
) ELSE IF !DWARF%DWARFCOUNT%REWARD%TEMPCOUNT%ID! EQU 0 (
	GOTO :CONSOLIDATEREWARDSLOOPEND
)
IF !DWARF%DWARFCOUNT%REWARD%TEMPCOUNT%ID! EQU !DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%ID! (
	CALL :ADDITEMS
)
:CONSOLIDATEREWARDSLOOPEND
IF %TEMPCOUNT% LSS !DWARF%DWARFCOUNT%REWARDTOT! (
	GOTO :CONSOLIDATEREWARDSLOOP2
) ELSE IF %REWARDCOUNT% LSS !DWARF%DWARFCOUNT%REWARDTOT! (
	GOTO :CONSOLIDATEREWARDSLOOP1
)
CALL :DISPLAYREWARDS
GOTO :EOF

:ADDITEMS
SET /a DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%NUM = !DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%NUM! + !DWARF%DWARFCOUNT%REWARD%TEMPCOUNT%NUM!
SET /a DWARF%DWARFCOUNT%REWARD%TEMPCOUNT%ID = 0
GOTO :EOF


:DISPLAYREWARDS
SET /a REWARDCOUNT = 0
:DISPLAYREWARDSLOOP
SET /a REWARDCOUNT = %REWARDCOUNT% + 1
:: ECHO ID !DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%ID!
:: ECHO NUM !DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%NUM!
:: pause
IF !DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%ID! GTR 0 (
	IF !DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%NUM! GTR 0 (
		CALL :CALC
	)
)
IF %REWARDCOUNT% LSS !DWARF%DWARFCOUNT%REWARDTOT! (
	GOTO :DISPLAYREWARDSLOOP
)
IF %SUCCESS% EQU 0 (
	ECHO !DWARF%DWARFCOUNT%NAME! couldn't mine anything for you, maybe you need to rethink their salary
	ECHO or increase their strength
	ECHO.
	SET /a GAINEDEXP = 5
	CALL :WAITFORTHREE
) ELSE (
	CALL :DISPLAY3
)
:: ECHO EXP BEFORE !DWARF%DWARFCOUNT%EXP!
:: ECHO GAINED EXP %GAINEDEXP%
SET /a DWARF%DWARFCOUNT%EXP = !DWARF%DWARFCOUNT%EXP! + %GAINEDEXP%
SET /a DWARFREQEXP = !DWARF%DWARFCOUNT%LVL!*%DWARFEXPFORLEVELUP%
:: ECHO EXP AFTER !DWARF%DWARFCOUNT%EXP!
:: ECHO REQUIRED EXP %DWARFREQEXP%
:: pause
IF !DWARF%DWARFCOUNT%EXP! GEQ %DWARFREQEXP% (
	CALL "%MINERESOURCEPATH%\dwarflevelup.bat"
)
GOTO :EOF

:CALC
SET /a SUCCESS = 1
SET /a ID = !DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%ID!
SET /a GAINEDEXP = !DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%NUM! * !%TYPE%%ID%EXP!
SET /a DWARF%DWARFCOUNT%EXP = !DWARF%DWARFCOUNT%EXP! + %GAINEDEXP%
SET /a DWARFREQEXP = !DWARF%DWARFCOUNT%LVL!*%DWARFEXPFORLEVELUP%
::SET TEMPVAR=BEFORE
::CALL :CALCULATELENGTH
SET /a PLAYER%TYPE%%ID%NUM = !PLAYER%TYPE%%ID%NUM! + !DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%NUM!
SET TEMPVAR=AFTER
CALL :CALCULATELENGTH
GOTO :EOF

:DISPLAY3
ECHO. 旼컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴커
ECHO.  Acquired                                   Total     
SET /a REWARDCOUNT = 0
:DISPLAY3LOOP2
SET /a REWARDCOUNT = %REWARDCOUNT% + 1
IF !DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%ID! GTR 0 (
	IF !DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%NUM! GTR 0 (
		ECHO.  !REWARDDISPLAY%REWARDCOUNT%AFTER! 
	)
)
SET /a DWARF%DWARFCOUNT%%TYPE%%REWARDCOUNT%NUM = 0
IF %REWARDCOUNT% LSS !DWARF%DWARFCOUNT%REWARDTOT! (
	GOTO :DISPLAY3LOOP2
)
ECHO. 읕컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴컴켸
ECHO.
pause
GOTO :EOF

:CALCULATELENGTH
SET REWARDDISPLAY%REWARDCOUNT%%TEMPVAR%=!DWARF%DWARFCOUNT%REWARD%REWARDCOUNT%NUM! !%TYPE%%ID%NAME!(s)
SET /a LENGTH = 0
:CALCULATELENGTHLOOP
SET /a LENGTH += 1
SET SUBSTR=!REWARDDISPLAY%REWARDCOUNT%%TEMPVAR%:~%LENGTH%!
IF DEFINED SUBSTR (
	GOTO :CALCULATELENGTHLOOP
)
SET /a SPACENUM = !MAXSPACENUM%TEMPVAR%! - %LENGTH%
SET REWARDDISPLAY%REWARDCOUNT%%TEMPVAR%=!REWARDDISPLAY%REWARDCOUNT%%TEMPVAR%!!SPACES%SPACENUM%!
IF !PLAYER%TYPE%%ID%NUM! GEQ 10 (
	SET /a LENGTH = 5
) ELSE (
	SET /a LENGTH = 4
)
SET /a SPACENUM = 12 - %LENGTH%
SET REWARDDISPLAY%REWARDCOUNT%%TEMPVAR%=!REWARDDISPLAY%REWARDCOUNT%%TEMPVAR%!!PLAYER%TYPE%%ID%NUM!!SPACES%SPACENUM%!
GOTO :EOF





:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF

:WAITFORFIVE
TIMEOUT /T 5 > nul
GOTO :EOF

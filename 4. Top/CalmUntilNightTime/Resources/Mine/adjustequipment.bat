:START
CLS
ECHO.
IF %DWARFTOT% EQU 0 (
	ECHO You don't have any dwarves in your employ
	ECHO.
	pause
	GOTO :EOF
)
ECHO Dwarf Sultan: Who's equipment would you like to tinker with?
ECHO.
SET /a COUNT = 0
:LOOP1
SET /a COUNT = %COUNT% + 1
ECHO. %COUNT% - !DWARF%COUNT%NAME!
IF %COUNT% LSS %DWARFTOT% (
	GOTO :LOOP1
)
ECHO. 0 - Back
ECHO.
:DWARFCHOICE
SET /P DWARFCHOICE=
ECHO.
IF "%DWARFCHOICE%" EQU "" (
	GOTO :DWARFCHOICE
) ELSE IF "%DWARFCHOICE%" EQU "0" (
	GOTO :EOF
)
IF %DWARFCHOICE% GTR %DWARFTOT% (
	GOTO :DWARFCHOICE
) ELSE IF %DWARFCHOICE% LSS 0 (
	GOTO :DWARFCHOICE
)
CALL :ADJUSTEQUIPMENT
GOTO :EOF



:ADJUSTEQUIPMENT
CLS
ECHO.
SET /a TEMP = !DWARF%DWARFCHOICE%PICKAXEID!
SET /a TEMP2 = !DWARF%DWARFCHOICE%SACKID!
IF %TEMP% EQU 0 (
	IF %TEMP2% EQU 0 (
		ECHO Dwarf Sultan: !DWARF%DWARFCHOICE%NAME! doesn't have anything equipped at the moment
		GOTO :EQUIPSTART
	)
)
ECHO Dwarf Sultan: !DWARF%DWARFCHOICE%NAME! currently has the following equipment:
ECHO.
IF %TEMP% NEQ 0 (
	ECHO. Pickaxe: !PICKAXE%TEMP%NAME!
	ECHO.     STR: + !PICKAXE%TEMP%STR!
	ECHO.     SKL: + !PICKAXE%TEMP%SKL!
	ECHO.     PER: + !PICKAXE%TEMP%PER!
)
IF %TEMP2% NEQ 0 (
	ECHO. Sack   : !SACK%TEMP2%NAME!
	ECHO.     STR: + !SACK%TEMP2%STR!
	ECHO.     SKL: + !SACK%TEMP2%SKL!
	ECHO.     PER: + !SACK%TEMP2%PER!
)
:EQUIPSTART
ECHO.
ECHO Dwarf Sultan: Would you like to change their pickaxes or sacks?
ECHO.
ECHO. 1 - Pickaxes
ECHO. 2 - Sacks
ECHO. 0 - Back
ECHO.
:CHOICE2
SET /P CHOICE2=
ECHO.
IF "%CHOICE2%" EQU "1" (
	SET TYPE=Pickaxe
) ELSE IF "%CHOICE2%" EQU "2" (
	SET TYPE=Sack
) ELSE IF "%CHOICE2%" EQU "0" (
	GOTO :START
)
:ADJUSTTYPE
CALL :CALCULATEAVAILABLE
CLS
ECHO.
IF !DYNAMIC%TYPE%TOT! EQU 0 (
	IF !DWARF%DWARFCHOICE%%TYPE%ID! EQU 0 (
		ECHO Dwarf Sultan: You don't have any %TYPE%s available
		ECHO.
		CALL :WAITFORTHREE
		GOTO :EOF
	)
)
SET /a TEMP = !DWARF%DWARFCHOICE%%TYPE%ID!
IF %TEMP% NEQ 0 (
	ECHO Dwarf Sultan: !DWARF%DWARFCHOICE%NAME! currently has the following %TYPE% equipped:
	ECHO.
	ECHO. %TYPE%: !%TYPE%%TEMP%NAME!
	ECHO.     STR: + !%TYPE%%TEMP%STR!
	ECHO.     SKL: + !%TYPE%%TEMP%SKL!
	ECHO.     PER: + !%TYPE%%TEMP%PER!
	ECHO.
) ELSE (
	ECHO Dwarf Sultan: !DWARF%DWARFCHOICE%NAME! doesn't have a %TYPE% equipped
	ECHO.
)
ECHO               Which %TYPE% would you like to equip?
ECHO.
IF %TEMP% NEQ 0 (
	ECHO               Choose the one you currently have equipped to unequip it
	ECHO.
)
SET /a COUNT = 0
:ADJUSTTYPELOOP
SET /a COUNT = %COUNT% + 1
SET /a SPACENUM = 20 - !DYNAMIC%TYPE%%COUNT%NAMELENGTH!
ECHO. %COUNT% - !DYNAMIC%TYPE%%COUNT%NAME!!SPACES%SPACENUM%!!DYNAMIC%TYPE%%COUNT%NUM! Available
IF %COUNT% LSS !DYNAMIC%TYPE%TOT! (
	GOTO :ADJUSTTYPELOOP
)
ECHO. 0 - Back
ECHO.
:EQUIPCHOICE
SET /P EQUIPCHOICE=
ECHO.
IF "%EQUIPCHOICE%" EQU "" (
	GOTO :EQUIPCHOICE
) ELSE IF "%EQUIPCHOICE%" EQU "0" (
	CLS
	GOTO :EQUIPSTART
)
IF %EQUIPCHOICE% LSS 0 (
	GOTO :EQUIPCHOICE
) ELSE IF %EQUIPCHOICE% GTR !DYNAMIC%TYPE%TOT! (
	GOTO :EQUICHOICE
)
CALL :SETSTATICIDS
CALL :UNEQUIPQUERY
SET /a DWARFCOUNT = %DWARFCHOICE%
CALL "%MINERESOURCEPATH%\resetminetimes.bat"
GOTO :EOF

:SETSTATICIDS
SET /a COUNT = 0
:LOOPSTART
SET /a COUNT = %COUNT%+1
IF "!%TYPE%%COUNT%NAME!" EQU "!DYNAMIC%TYPE%%EQUIPCHOICE%NAME!" (
	SET /a STATICCHOICE = %COUNT%
	GOTO :EOF
) ELSE IF %COUNT% LSS !%TYPE%TOT! (
	CALL :LOOPSTART
)
GOTO :EOF

:UNEQUIPQUERY
IF %STATICCHOICE% EQU !DWARF%DWARFCHOICE%%TYPE%ID! (
	CALL :UNEQUIP
) ELSE (
	CALL :EQUIP
)
GOTO :EOF

:UNEQUIP
CLS
ECHO.
ECHO Dwarf Sultan: !DWARF%DWARFCHOICE%NAME! has unequipped his !%TYPE%%STATICCHOICE%NAME!
ECHO.
CALL :WAITFORTHREE
SET /a DWARF%DWARFCHOICE%%TYPE%ID = 0
SET /a DWARF%DWARFCHOICE%%TYPE%STR = 0
SET /a DWARF%DWARFCHOICE%%TYPE%SKL = 0
SET /a DWARF%DWARFCHOICE%%TYPE%PER = 0
GOTO :EOF

:EQUIP
CLS
ECHO.
ECHO Dwarf Sultan: !DWARF%DWARFCHOICE%NAME! has equipped a !%TYPE%%STATICCHOICE%NAME!
ECHO.
CALL :WAITFORTHREE
SET /a DWARF%DWARFCHOICE%%TYPE%ID = %STATICCHOICE%
SET /a DWARF%DWARFCHOICE%%TYPE%STR = !%TYPE%%STATICCHOICE%STR!
SET /a DWARF%DWARFCHOICE%%TYPE%SKL = !%TYPE%%STATICCHOICE%SKL!
SET /a DWARF%DWARFCHOICE%%TYPE%PER = !%TYPE%%STATICCHOICE%PER!
GOTO :EOF


:CALCULATEAVAILABLE
SET /a COUNT = 0
:AVAILABLELOOP1
SET /a COUNT = %COUNT% + 1
SET /a EQUIPPED%TYPE%%COUNT% = 0
IF %COUNT% LSS !%TYPE%TOT! (
	GOTO :AVAILABLELOOP1
)
SET /a COUNT = 0
:AVAILABLELOOP2
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !DWARF%COUNT%%TYPE%ID!
IF %TEMP% NEQ 0 (
	SET /a EQUIPPED%TYPE%%TEMP% = !EQUIPPED%TYPE%%TEMP%! + 1
)
IF %COUNT% LSS %DWARFTOT% (
	GOTO :AVAILABLELOOP2
)
SET /a COUNT = 0
:AVAILABLELOOP3
SET /a COUNT = %COUNT% + 1
SET /a AVAILABLE%TYPE%%COUNT% = !PLAYER%TYPE%%COUNT%NUM! - !EQUIPPED%TYPE%%COUNT%!
IF %COUNT% LSS !%TYPE%TOT! (
	GOTO :AVAILABLELOOP3
)
CALL "%DYNAMICRESOURCEPATH%\dynamicmineequip.bat"
GOTO :EOF
















:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF
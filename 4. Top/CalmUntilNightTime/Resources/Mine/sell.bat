SET /a SPACESMAX = 38
:START
CALL "%DYNAMICRESOURCEPATH%\dynamicminesell.bat"
CLS
IF !DYNAMIC%TYPE%TOT! EQU 0 (
	ECHO.
	ECHO Dwarf Sultan: You don't have any %TYPE%s to sell sir
	ECHO.
	pause
	GOTO :EOF
)
ECHO.
ECHO Dwarf Sultan: Which %TYPE% would you like to sell?
ECHO.
ECHO You currently have %PLAYERGIL% Gil
ECHO.
SET /a COUNT = 0
:CHOICELOOP
SET /a COUNT = %COUNT%+1
SET /a SPACENUM = %SPACESMAX% - !DYNAMIC%TYPE%%COUNT%NAMELENGTH!
ECHO. %COUNT% - !DYNAMIC%TYPE%%COUNT%NAME!!SPACES%SPACENUM%!(!DYNAMIC%TYPE%%COUNT%SELL! Gil) (!DYNAMIC%TYPE%%COUNT%NUM! Left)
IF %COUNT% LSS !DYNAMIC%TYPE%TOT! (
	GOTO :CHOICELOOP
)
ECHO. 0 - Back
ECHO.
:CHOICE
SET /P CHOICE=
ECHO.
IF "%CHOICE%" EQU "" (
	GOTO :CHOICE
) ELSE IF "%CHOICE%" EQU "0" (
	GOTO :EOF
)
IF %CHOICE% GTR !DYNAMIC%TYPE%TOT! (
	GOTO :CHOICE
) ELSE IF %CHOICE% LSS 0 (
	GOTO :CHOICE
)
CALL :SELLABLEQUERY
GOTO :EOF

:SELLABLEQUERY
CALL :SETSTATICIDS
REM Count the number of ones you have vs the number you have equipped
CALL :NUMEQUIPPED
IF %SELLABLE% EQU 1 (
	CALL :SELL
) ELSE (
	CALL :NOSELL
)
GOTO :EOF

:SETSTATICIDS
SET /a COUNT = 0
:LOOPSTART
SET /a COUNT=%COUNT%+1
IF "!%TYPE%%COUNT%NAME!" EQU "!DYNAMIC%TYPE%%CHOICE%NAME!" (
	SET /a STATICCHOICE = %COUNT%
	GOTO :EOF
) ELSE IF %COUNT% LSS !%TYPE%TOT! (
	CALL :LOOPSTART
)
GOTO :EOF

:NUMEQUIPPED
SET /a SELLABLE = 1
SET /a COUNT = 0
SET /a NUMEQUIPPED = 0
:NUMEQUIPPEDLOOP
SET /a COUNT = %COUNT% + 1
IF !DWARF%COUNT%%TYPE%ID! EQU %STATICCHOICE% (
	SET /a NUMEQUIPPED = %NUMEQUIPPED% + 1
)
IF %COUNT% LSS %DWARFTOT% (
	GOTO :NUMEQUIPPEDLOOP
)
IF %NUMEQUIPPED% GEQ !PLAYER%TYPE%%STATICCHOICE%NUM! (
	SET /a SELLABLE = 0
)
GOTO :EOF

:SELL
SET /a PLAYERGIL = %PLAYERGIL% + !%TYPE%%STATICCHOICE%SELL!
SET /a PLAYER%TYPE%%STATICCHOICE%NUM = !PLAYER%TYPE%%STATICCHOICE%NUM! - 1
CLS
ECHO.
ECHO Dwarf Sultan: You have sold 1 !DYNAMIC%TYPE%%CHOICE%NAME! for !DYNAMIC%TYPE%%CHOICE%SELL! Gil
ECHO.
CALL :WAITFORTHREE
GOTO :EOF

:NOSELL
CLS
ECHO.
ECHO Dwarf Sultan: You can't sell the !%TYPE%%STATICCHOICE%NAME! that your dwarves are using right now^^!
ECHO.
CALL :WAITFORTHREE
GOTO :EOF

:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF
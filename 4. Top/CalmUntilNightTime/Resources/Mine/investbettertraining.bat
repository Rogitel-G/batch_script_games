:START
CLS
ECHO.
ECHO Dwarf Sultan: Which training facility would you like to buy?
ECHO.
ECHO You have %PLAYERGIL% Gil
ECHO.
:STR
IF %STRTRAININGFACILITYUNLOCKED% EQU 5 (
	GOTO :SKL
)
SET /a TEMP = %STRTRAININGFACILITYUNLOCKED% + 1
SET /a SPACENUM = 30 - !STRTRAININGFACILITY%TEMP%NAMELENGTH!
ECHO. 1 - !STRTRAININGFACILITY%TEMP%NAME!!SPACES%SPACENUM%!- !STRTRAININGFACILITY%TEMP%COST! Gil
:SKL
IF %SKLTRAININGFACILITYUNLOCKED% EQU 5 (
	GOTO :PER
)
SET /a TEMP = %SKLTRAININGFACILITYUNLOCKED% + 1
SET /a SPACENUM = 30 - !SKLTRAININGFACILITY%TEMP%NAMELENGTH!
ECHO. 2 - !SKLTRAININGFACILITY%TEMP%NAME!!SPACES%SPACENUM%!- !SKLTRAININGFACILITY%TEMP%COST! Gil
:PER
IF %PERTRAININGFACILITYUNLOCKED% EQU 5 (
	GOTO :END
)
SET /a TEMP = %PERTRAININGFACILITYUNLOCKED% + 1
SET /a SPACENUM = 30 - !PERTRAININGFACILITY%TEMP%NAMELENGTH!
ECHO. 3 - !PERTRAININGFACILITY%TEMP%NAME!!SPACES%SPACENUM%!- !PERTRAININGFACILITY%TEMP%COST! Gil
:END
ECHO. 0 - Back
ECHO.
:CHOICE
SET /P CHOICE=
ECHO.
IF "%CHOICE%" EQU "1" (
	IF %STRTRAININGFACILITYUNLOCKED% EQU 5 (
		GOTO :CHOICE
	) ELSE (
		SET STAT=STR
	)
) ELSE IF "%CHOICE%" EQU "2" (
	IF %SKLTRAININGFACILITYUNLOCKED% EQU 5 (
		GOTO :CHOICE
	) ELSE (
		SET STAT=SKL
	)
) ELSE IF "%CHOICE%" EQU "3" (
	IF %PERTRAININGFACILITYUNLOCKED% EQU 5 (
		GOTO :CHOICE
	) ELSE (
		SET STAT=PER
	)
) ELSE IF "%CHOICE%" EQU "0" (
	GOTO :EOF
) ELSE (
	GOTO :CHOICE
)
CALL :GILQUERY
GOTO :START

:GILQUERY
SET /a TEMP = !%STAT%TRAININGFACILITYUNLOCKED! + 1
IF %PLAYERGIL% GTR !%STAT%TRAININGFACILITY%TEMP%COST! (
	CALL :SUCCESS
) ELSE (
	CALL :UNSUCCESS
)
GOTO :EOF

:SUCCESS
SET /a PLAYERGIL = %PLAYERGIL% - !%STAT%TRAININGFACILITY%TEMP%COST!
SET /a %STAT%TRAININGFACILITYUNLOCKED = !%STAT%TRAININGFACILITYUNLOCKED! + 1
CLS
ECHO.
SET /a TEMP = !%STAT%TRAININGFACILITYUNLOCKED!
SET /a TEMP2 = !%STAT%TRAININGFACILITY%TEMP%TIME!*!%STAT%TRAININGFACILITY%TEMP%LIMIT!
ECHO Dwarf Sultan: You have successfully purchased the !%STAT%TRAININGFACILITY%TEMP%NAME!^^!
ECHO.
ECHO               Training your dwarves in this facility will increase their %STAT% stat by
ECHO               one point every !%STAT%TRAININGFACILITY%TEMP%TIME! hours^^! They will be able to train for
ECHO               a total of %TEMP2% hours before it stops being effective. You'll then need to
ECHO               level them up before they can train again
ECHO.
ECHO               The mathematicians amongst you will figure out that you can increase their
ECHO               %STAT% by a maximum of !%STAT%TRAININGFACILITY%TEMP%LIMIT! per level^^!
ECHO.
pause
GOTO :EOF

:UNSUCCESS
CLS
ECHO.
ECHO Dwarf Sultan: You don't have enough gil for this upgrade
ECHO.
pause
GOTO :EOF

:START
CLS
ECHO.
IF %DWARFTOT% EQU 0 (
	ECHO You don't have any dwarves in your employ
	ECHO.
	pause
	GOTO :EOF
)
ECHO Dwarf Sultan: Who's strategy would you like to adjust?
ECHO.
SET /a COUNT = 0
:DWARFLOOP
SET /a COUNT = %COUNT% + 1
SET /a SPACENUM = 12 - !DWARF%COUNT%NAMELENGTH!
ECHO. %COUNT% - !DWARF%COUNT%NAME!!SPACES%SPACENUM%!Currently mines for !DWARF%COUNT%TYPEDISPLAY!
IF %COUNT% LSS %DWARFTOT% (
	GOTO :DWARFLOOP
)
ECHO. 0 - Back
ECHO.
:CHOICE
SET /P CHOICE=
ECHO.
IF "%CHOICE%" EQU "" (
	GOTO :CHOICE
) ELSE IF "%CHOICE%" EQU "0" (
	GOTO :EOF
)
IF %CHOICE% GTR %DWARFTOT% (
	GOTO :CHOICE
) ELSE IF %CHOICE% LSS 0 (
	GOTO :CHOICE
)
CALL :ADJUSTSTRATEGYQUERY
GOTO :START


:ADJUSTSTRATEGYQUERY
CLS
ECHO.
ECHO Dwarf Sultan: !DWARF%CHOICE%NAME! currently mines for !DWARF%CHOICE%TYPEDISPLAY!
ECHO               Their speciality is !DWARF%CHOICE%SPECIALDISPLAY!
ECHO               what would you like them to mine for?
ECHO.
SET /a COUNT = 0
:ADJUSTSTRATEGYQUERYLOOP
SET /a COUNT = %COUNT% + 1
IF !MINEITEMTYPE%COUNT%UNLOCKED! EQU 1 (
	ECHO. %COUNT% - !MINEITEM%COUNT%SPECIALDISPLAY!
) ELSE (
	ECHO. %COUNT% - Locked^^!
)
IF %COUNT% LSS 9 (
	GOTO :ADJUSTSTRATEGYQUERYLOOP
)
ECHO. 0 - Back
ECHO.
:CHOICE2
SET /P CHOICE2=
ECHO.
IF "%CHOICE2%" EQU "" (
	GOTO :CHOICE2
) ELSE IF "%CHOICE2%" EQU "0" (
	GOTO :EOF
)
IF %CHOICE2% GTR 9 (
	GOTO :CHOICE2
) ELSE IF %CHOICE2% LSS 0 (
	GOTO :CHOICE2
)
CALL :LOCKEDQUERY
IF %LOCKED% EQU 1 (
	GOTO :ADJUSTSTRATEGYQUERY
)
CALL :CHANGESTRATEGY
GOTO :EOF

:LOCKEDQUERY
IF !MINEITEMTYPE%CHOICE2%UNLOCKED! EQU 0 (
	SET /a LOCKED = 1
	CLS
	ECHO.
	ECHO Dwarf Sultan: You haven't unlocked this yet, try investing money to expand your mine
	ECHO.
	pause
) ELSE (
	SET /a LOCKED = 0
)
GOTO :EOF

:CHANGESTRATEGY
SET DWARF%CHOICE%TYPE=!MINEITEM%CHOICE2%TYPE!
SET DWARF%CHOICE%TYPE2=!MINEITEM%CHOICE2%SPECIAL!
SET DWARF%CHOICE%TYPEDISPLAY=!MINEITEM%CHOICE2%SPECIALDISPLAY!
SET TEMP=!DWARF%CHOICE%TYPE2!
SET /a DWARF%CHOICE%MINID = !%TEMP%START!
SET /a DWARF%CHOICE%MAXID = !%TEMP%END!
CLS
ECHO.
ECHO Dwarf Sultan: !DWARF%CHOICE%NAME! will now mine for !DWARF%CHOICE%TYPEDISPLAY!
ECHO.
pause
GOTO :EOF


















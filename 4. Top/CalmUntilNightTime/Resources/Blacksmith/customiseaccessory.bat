SET /a SPACESMAX = 38
SET /a STATINCREASEGIL = 500
SET /a GILCOST = 0
SET /a MAXHPSTONECOST = 0
SET /a MAXMPSTONECOST = 0
SET /a LCKSTONECOST = 0
SET /a BASECHOICE = 1000
CLS
:SLOTSET
CALL :SLOT
IF %SLOTNUM% EQU 0 (
	GOTO :EOF
)
:ACCESSORYEXISTQUERYSET
CALL :ACCESSORYEXISTQUERY
IF %ACCESSORYEXIST% EQU 1 (
	GOTO :ACCESSORYEXIST
) ELSE (
	GOTO :ACCESSORYNOTEXIST
)
:ACCESSORYNOTEXIST
:NAMESET
CALL :NAME
IF %NAMEBACK% EQU 1 (
	GOTO :SLOTSET
)
:BASESET
CALL :BASE
IF %BASEBACK% EQU 1 (
	GOTO :NAMESET
)
:BASESTATSET
CALL :CALCULATEBASESTATS
GOTO :STATINCREASESET


:ACCESSORYEXIST
:CUSTOMSTATSET
CALL :CALCULATECUSTOMSTATS
:STATINCREASESET
CALL :STATINCREASE
IF %STATINCREASEBACK% EQU 1 (
	IF %ACCESSORYEXIST% EQU 0 (
		GOTO :BASESET
	) ELSE (
		GOTO :SLOTSET
	)
)
:SPECIALSSET
CALL :SPECIALS
IF %SPECIALSBACK% EQU 1 (
	IF %ACCESSORYEXIST% EQU 0 (
		GOTO :BASESTATSET
	) ELSE (
		GOTO :CUSTOMSTATSET
	)
)
:CONFIRMATIONSET
CALL :CONFIRMATION
IF %CONFIRMATIONBACK% EQU 1 (
	IF %DYNAMICSPECIALITEMTOT% EQU 0 (
		IF %ACCESSORYEXIST% EQU 0 (
			GOTO :BASESTATSET
		) ELSE (
			GOTO :CUSTOMSTATSET
		)
	) ELSE (
		GOTO :SPECIALSSET
	)
)
IF %CONFIRMATIONCHOICE% EQU 1 (
	CALL :CREATEACCESSORY
) ELSE IF %CONFIRMATIONCHOICE% EQU 2 (
	GOTO :EOF
)
:EQUIPSET
CALL :EQUIP
GOTO :EOF


:SLOT
CALL :INITIALISEBACKS
::SET /a AVAILABLESLOTS = 5
SET /a AVAILABLESLOTS = %PLAYERLVL%/10
IF %AVAILABLESLOTS% GTR 9 (
	SET /a AVAILABLESLOTS = 9
)
IF EXIST "%CUSTOMACCESSORYRESOURCEPATH%\Temp\customaccessorylist.bat" (
	CALL "%CUSTOMACCESSORYRESOURCEPATH%\Temp\customaccessorylist.bat"
)
CLS
ECHO.
IF %AVAILABLESLOTS% EQU 0 (
	ECHO %BLACKSMITHNAME%: Sorry you're not a high enough level to customise accessories yet...
	ECHO.
	SET /a SLOTNUM = 0
	pause
	GOTO :EOF
) ELSE IF %AVAILABLESLOTS% EQU 1 (
	ECHO %BLACKSMITHNAME%: You can hold %AVAILABLESLOTS% custom accessory
) ELSE IF %AVAILABLESLOTS% GEQ 2 (	
	ECHO %BLACKSMITHNAME%: You can hold %AVAILABLESLOTS% custom accessories
)
ECHO.
ECHO            Select a custom accessory slot
ECHO.
SET /a COUNT = 0
:SLOTSLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = %ACCESSORYTOTAL% + %COUNT%
IF %AVAILABLESLOTS% GEQ %COUNT% (
ECHO. %COUNT% - !ACCESSORY%TEMP%NAME!
)
IF %COUNT% LSS %AVAILABLESLOTS% (
	GOTO :SLOTSLOOP
)
ECHO. 0 - Back
ECHO.
:SLOTCHOICE
SET /P SLOTNUM=
ECHO.
IF "%SLOTNUM%" EQU "" (
	GOTO :SLOTCHOICE
)
IF %SLOTNUM% GTR %AVAILABLESLOTS% (
	GOTO :SLOTCHOICE
) ELSE IF %SLOTNUM% LSS 0 (
	GOTO :SLOTCHOICE
) ELSE IF %SLOTNUM% EQU 0 (
	GOTO :EOF
)
GOTO :EOF


:ACCESSORYEXISTQUERY
SET /a TEMP = %ACCESSORYTOTAL% + %SLOTNUM%
IF DEFINED ACCESSORY%TEMP%NAME (
	CALL :EXISTTRUE
) ELSE (
	SET /a ACCESSORYEXIST = 0
)
GOTO :EOF

:EXISTTRUE
SET /a ACCESSORYEXIST = 1
CALL "%CUSTOMACCESSORYRESOURCEPATH%\Temp\customaccessory%SLOTNUM%.bat"
SET ACCESSORYNAME=!ACCESSORY%TEMP%NAME!
SET /a ACCESSORYID = %TEMP%
GOTO :EOF

:NAME
CLS
CALL :INITIALISEBACKS
ECHO.
ECHO %BLACKSMITHNAME%: What would you like to call your accessory? (1 word)
ECHO.
ECHO             0 to exit
ECHO.
:NAMECHOICE
SET /P ACCESSORYNAME=
ECHO.
IF "%ACCESSORYNAME%" EQU "" (
	GOTO :NAMECHOICE
) ELSE IF "%ACCESSORYNAME%" EQU "0" (
	SET /a NAMEBACK = 1
)
GOTO :EOF

:BASE
CLS
CALL "%DYNAMICRESOURCEPATH%\dynamicaccessorystatscustomise.bat"
CALL :INITIALISEBACKS
ECHO.
ECHO %BLACKSMITHNAME%: Choose a base accessory to customise
ECHO.
ECHO             0 to exit
ECHO.
SET /a COUNT = 0
SET /a PLAYERACCESSORYTOTAL = 0
:ACCESSORYCHECKLOOP
SET /a COUNT = %COUNT%+1
SET /a PLAYERACCESSORYTOTAL = %PLAYERACCESSORYTOTAL% + !PLAYERACCESSORY%COUNT%NUM!
IF %COUNT% LSS %ACCESSORYTOTAL% (
	GOTO :ACCESSORYCHECKLOOP
)
IF %PLAYERACCESSORYTOTAL% EQU 0 (
	GOTO :NOACCESSORIES
)
SET /a COUNT = 0
:LOOPSTART
SET /a COUNT = %COUNT%+1
SET /a SPACENUM = %SPACESMAX% - !DYNAMICACCESSORY%COUNT%NAMELENGTH!
ECHO. %COUNT% - !DYNAMICACCESSORY%COUNT%NAME!!SPACES%SPACENUM%!(!DYNAMICACCESSORY%COUNT%NUM!)
IF %COUNT% LSS %DYNAMICACCESSORYTOTAL% (
	GOTO :LOOPSTART
) 
:NOACCESSORIES
ECHO. 0 - Back
ECHO.
:BASECHOICE
SET /P DYNAMICBASECHOICE=
IF "%DYNAMICBASECHOICE%" EQU "" (
	GOTO :BASECHOICE
)
IF %DYNAMICBASECHOICE% LSS 0 (
	GOTO :BASECHOICE
) ELSE IF %DYNAMICBASECHOICE% GTR %DYNAMICACCESSORYTOTAL% (
	GOTO :BASECHOICE
) ELSE IF %DYNAMICBASECHOICE% EQU 0 (
	SET /a BASEBACK = 1
	GOTO :EOF
)
REM This is like SETSTATICID
SET /a COUNT = 0
:LOOP2START
SET /a COUNT = %COUNT%+1
IF "!ACCESSORY%COUNT%NAME!" EQU "!DYNAMICACCESSORY%DYNAMICBASECHOICE%NAME!" (
	SET /a BASECHOICE = %COUNT%
) ELSE IF %COUNT% LSS %ACCESSORYTOTAL% (
	GOTO :LOOP2START
) ELSE IF %COUNT% EQU %ACCESSORYTOTAL% (
	GOTO :BASECHOICE
)
GOTO :EOF

:CALCULATEBASESTATS
SET /a TEMPMAXHP = !ACCESSORY%BASECHOICE%MAXHP!
SET /a TEMPMAXMP = !ACCESSORY%BASECHOICE%MAXMP!
SET /a TEMPLCK = !ACCESSORY%BASECHOICE%LCK!
GOTO :EOF

:CALCULATECUSTOMSTATS
SET /a TEMP = %ACCESSORYTOTAL% + %SLOTNUM%
SET /a TEMPMAXHP = !ACCESSORY%TEMP%MAXHP!
SET /a TEMPMAXMP = !ACCESSORY%TEMP%MAXMP!
SET /a TEMPLCK = !ACCESSORY%TEMP%LCK!
GOTO :EOF

:STATINCREASE
CALL :INITIALISEBACKS
SET /a GILCOST = 0
SET /a MAXHPSTONECOST = 0
SET /a MAXMPSTONECOST = 0
SET /a LCKSTONECOST = 0
:STATINCREASE2
CALL :SETTEMPSTATVARS
CLS
ECHO.
ECHO %BLACKSMITHNAME%: This accessory currently give you the following stat bonuses:
ECHO.
ECHO. Max HP   +%TEMPMAXHP%
ECHO. Max MP   +%TEMPMAXMP%
ECHO. Luck     +%TEMPLCK%
ECHO.
ECHO %BLACKSMITHNAME%: Select the stat you'd like to increase.
ECHO             Each stat costs 1 stone and %STATINCREASEGIL% Gil
ECHO             You have %TEMPGIL% Gil remaining
ECHO.
IF %TEMPMAXHPSTONE% EQU 1 (
	ECHO. 1 - Max HP                     %TEMPMAXHPSTONE% !SPECIALITEM%MAXHPSTONEID%NAME!
) ELSE (
	ECHO. 1 - Max HP                     %TEMPMAXHPSTONE% !SPECIALITEM%MAXHPSTONEID%NAME!s
)
IF %TEMPMAXMPSTONE% EQU 1 (
	ECHO. 2 - Max MP                     %TEMPMAXMPSTONE% !SPECIALITEM%MAXMPSTONEID%NAME!
) ELSE (
	ECHO. 2 - Max MP                     %TEMPMAXMPSTONE% !SPECIALITEM%MAXMPSTONEID%NAME!s
)
IF %TEMPLCKSTONE% EQU 1 (
	ECHO. 3 - Luck                       %TEMPLCKSTONE% !SPECIALITEM%LCKSTONEID%NAME!
) ELSE (
	ECHO. 3 - Luck                       %TEMPLCKSTONE% !SPECIALITEM%LCKSTONEID%NAME!s
)
ECHO. 0 - Done
ECHO. B - Back
ECHO.
:STATINCREASECHOICE
SET /P STATINCREASECHOICE=
ECHO.
IF "%STATINCREASECHOICE%" EQU "1" (
	IF %TEMPMAXHPSTONE% GTR 0 (
		IF %PLAYERGIL% GEQ %TEMPGILCOST% (
			SET /a GILCOST = %GILCOST% + %STATINCREASEGIL%
			SET /a MAXHPSTONECOST = %MAXHPSTONECOST% + 1
			SET /a TEMPMAXHP = %TEMPMAXHP% + %STATMAXHPINCREASE%
		) ELSE (
			ECHO %BLACKSMITHNAME%: You don't have enough Gil for this
			ECHO.
			CALL :WAITFORTWO
		)
	) ELSE (
		ECHO %BLACKSMITHNAME%: You don't have enough !SPECIALITEM%MAXHPSTONEID%NAME!s for this
		ECHO.
		CALL :WAITFORTWO
	)
) ELSE IF "%STATINCREASECHOICE%" EQU "2" (
	IF %TEMPMAXMPSTONE% GTR 0 (
		IF %PLAYERGIL% GEQ %TEMPGILCOST% (
			SET /a GILCOST = %GILCOST% + %STATINCREASEGIL%
			SET /a MAXMPSTONECOST = %MAXMPSTONECOST% + 1
			SET /a TEMPMAXMP = %TEMPMAXMP% + %STATMAXMPINCREASE%
		) ELSE (
			ECHO %BLACKSMITHNAME%: You don't have enough Gil for this
			ECHO.
			CALL :WAITFORTWO
		)
	) ELSE (
		ECHO %BLACKSMITHNAME%: You don't have enough !SPECIALITEM%MAXMPSTONEID%NAME!s for this
		ECHO.
		CALL :WAITFORTWO
	)
) ELSE IF "%STATINCREASECHOICE%" EQU "3" (
	IF %TEMPLCKSTONE% GTR 0 (
		IF %PLAYERGIL% GEQ %TEMPGILCOST% (
			SET /a GILCOST = %GILCOST% + %STATINCREASEGIL%
			SET /a LCKSTONECOST = %LCKSTONECOST% + 1
			SET /a TEMPLCK = %TEMPLCK% + 1
		) ELSE (
			ECHO %BLACKSMITHNAME%: You don't have enough Gil for this
			ECHO.
			CALL :WAITFORTWO
		)
	) ELSE (
		ECHO %BLACKSMITHNAME%: You don't have enough !SPECIALITEM%LCKSTONEID%NAME!s for this
		ECHO.
		CALL :WAITFORTWO
	)
) ELSE IF "%STATINCREASECHOICE%" EQU "0" (
	SET /a STATGILCOST = %GILCOST%
	GOTO :EOF
) ELSE IF /I "%STATINCREASECHOICE%" EQU "B" (
	SET /a STATINCREASEBACK = 1
	GOTO :EOF
) ELSE (
	GOTO :STATINCREASECHOICE
)
GOTO :STATINCREASE2

:SETTEMPSTATVARS
SET /a TEMPGILCOST = %GILCOST% + %STATINCREASEGIL%
SET /a TEMPGIL = %PLAYERGIL% - %GILCOST%
SET /a TEMPMAXHPSTONE = !PLAYERSPECIALITEM%MAXHPSTONEID%NUM! - %MAXHPSTONECOST%
SET /a TEMPMAXMPSTONE = !PLAYERSPECIALITEM%MAXMPSTONEID%NUM! - %MAXMPSTONECOST%
SET /a TEMPLCKSTONE = !PLAYERSPECIALITEM%LCKSTONEID%NUM! - %LCKSTONECOST%
GOTO :EOF



:SPECIALS
IF %ACCESSORYEXIST% EQU 0 (
	CALL :INITIALBASESPECIALSET
	GOTO :SPECIALSET
) ELSE (
	CALL :INITIALCUSTOMSPECIALSET
	GOTO :SPECIALSET
)
GOTO :EOF

:SPECIALSET
SET /a GILCOST = %STATGILCOST%
CALL :INITIALISEBACKS
CALL "%DYNAMICRESOURCEPATH%\dynamicspecialcustomlist.bat"
:SPECIALSET2
CLS
ECHO.
IF %TEMPSPECIALTOT% EQU 0 (
	ECHO %BLACKSMITHNAME%: This accessory currently doesn't give you any special abilities
	GOTO :ENDSPECIALLOOP3
)
ECHO %BLACKSMITHNAME%: This accessory currently gives you the following special abilities:
ECHO.
SET /a COUNT = 0
:SPECIALLOOP3
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !TEMPSPECIAL%COUNT%!
ECHO. !SPECIAL%TEMP%NAME!
IF %COUNT% LSS %TEMPSPECIALTOT% (
	GOTO :SPECIALLOOP3
)
:ENDSPECIALLOOP3
IF %DYNAMICSPECIALITEMTOT% EQU 0 (
	ECHO.
	ECHO %BLACKSMITHNAME%: You can't imbue your accessories with skills because you don't have any special stones
	ECHO.
	pause
	GOTO :EOF
)
ECHO.
ECHO %BLACKSMITHNAME%: You have the appropriate items to apply these special abilities to your accessory:
ECHO.
ECHO. You have %TEMPGIL% Gil remaining
ECHO.
SET /a COUNT = 0
:SPECIALSTONELOOP
SET /a COUNT = %COUNT% + 1
SET /a SPACENUM = %SPACESMAX% - !DYNAMICSPECIAL%COUNT%NAMELENGTH!
ECHO. %COUNT% - !DYNAMICSPECIAL%COUNT%NAME!!SPACES%SPACENUM%!(1 !DYNAMICSPECIALITEM%COUNT%NAME! and !DYNAMICSPECIAL%COUNT%GILCOST! Gil)
IF %COUNT% LSS %DYNAMICSPECIALITEMTOT% (
	GOTO :SPECIALSTONELOOP
)
ECHO. 0 - Done
ECHO. D - Descriptions
ECHO. B - Back
ECHO.
:SPECIALCHOICE
SET /P DYNAMICSPECIALCHOICE=
ECHO.
IF "%DYNAMICSPECIALCHOICE%" EQU "" (
	GOTO :SPECIALCHOICE
) ELSE IF /I "%DYNAMICSPECIALCHOICE%" EQU "B" (
	SET /a SPECIALSBACK = 1
	GOTO :EOF
) ELSE IF /I "%DYNAMICSPECIALCHOICE%" EQU "0" (
	GOTO :EOF
) ELSE IF /I "%DYNAMICSPECIALCHOICE%" EQU "D" (
	CALL :SPECIALDESCRIPTIONS
	GOTO :SPECIALSET2
)
IF %DYNAMICSPECIALCHOICE% GTR %DYNAMICSPECIALITEMTOT% (
	GOTO :SPECIALCHOICE
) ELSE IF %DYNAMICSPECIALCHOICE% LSS 0 (
	GOTO :SPECIALCHOICE
)
REM SETSTATICID
SET /a COUNT = 0
:SPECIALLOOP2START
SET /a COUNT = %COUNT%+1
IF !SPECIAL%COUNT%NAME! EQU !DYNAMICSPECIAL%DYNAMICSPECIALCHOICE%NAME! (
	SET /a SPECIALCHOICE = %COUNT%
) ELSE (
	GOTO :SPECIALLOOP2START
)
CALL :SPECIALALREADYSETQUERY
GOTO :SPECIALSET2

:SPECIALDESCRIPTIONS
CLS
ECHO.
ECHO %BLACKSMITHNAME%: Here's a description of the special abilities
ECHO.
SET /a COUNT = 0
:SPECIALDESCRIPTIONSLOOP
SET /a COUNT = %COUNT% + 1
SET /a SPACENUM = %SPACESMAX% - !DYNAMICSPECIAL%COUNT%NAMELENGTH!
ECHO. %COUNT% - !DYNAMICSPECIAL%COUNT%NAME!!SPACES%SPACENUM%!- !DYNAMICSPECIAL%COUNT%DESCRIPTION!
IF %COUNT% LSS %DYNAMICSPECIALITEMTOT% (
	GOTO :SPECIALDESCRIPTIONSLOOP
)
ECHO.
pause
GOTO :EOF

:INITIALBASESPECIALSET
SET /a NEWSPECIALTOT = 0
SET /a COUNT = 0
SET /a TEMPSPECIALTOT = !ACCESSORY%BASECHOICE%SPECIALTOT!
IF %TEMPSPECIALTOT% EQU 0 (
	GOTO :EOF
)
:INITIALBASESPECIALSETLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMPSPECIAL%COUNT% = !ACCESSORY%BASECHOICE%SPECIAL%COUNT%!
IF %COUNT% LSS %TEMPSPECIALTOT% (
	GOTO :INITIALBASESPECIALSETLOOP
)
GOTO :EOF


:INITIALCUSTOMSPECIALSET
SET /a NEWSPECIALTOT = 0
SET /a COUNT = 0
SET /a TEMP = %ACCESSORYTOTAL% + %SLOTNUM%
SET /a TEMPSPECIALTOT = !ACCESSORY%TEMP%SPECIALTOT!
IF %TEMPSPECIALTOT% EQU 0 (
	GOTO :EOF
)
:INITIALCUSTOMSPECIALSETLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMPSPECIAL%COUNT% = !ACCESSORY%TEMP%SPECIAL%COUNT%!
IF %COUNT% LSS %TEMPSPECIALTOT% (
	GOTO :INITIALCUSTOMSPECIALSETLOOP
)
GOTO :EOF


:SPECIALALREADYSETQUERY
IF %TEMPSPECIALTOT% EQU 0 (
	GOTO :ENDSPECIALALREADYSETQUERY
)
SET /a COUNT = 0
:SPECIALALREADYSETLOOP
SET /a COUNT = %COUNT% + 1
IF !TEMPSPECIAL%COUNT%! EQU %SPECIALCHOICE% (
	ECHO %BLACKSMITHNAME%: You already have this special ability on your accessory
	ECHO.
	CALL :WAITFORTWO
	GOTO :EOF
)
IF %COUNT% LSS %TEMPSPECIALTOT% (
	GOTO :SPECIALALREADYSETLOOP
)
:ENDSPECIALALREADYSETQUERY
CALL :GILCOSTQUERY
GOTO :EOF

:GILCOSTQUERY
SET /a TEMPGILCOST = %GILCOST% + !SPECIAL%SPECIALCHOICE%GILCOST!
IF %PLAYERGIL% LSS %TEMPGILCOST% (
	ECHO %BLACKSMITHNAME%: You don't have enough Gil to add this skill
	ECHO.
	CALL :WAITFORTWO
	GOTO :EOF
)
SET /a TEMPSPECIALTOT = %TEMPSPECIALTOT% + 1
SET /a TEMPSPECIAL%TEMPSPECIALTOT% = %SPECIALCHOICE%
SET /a NEWSPECIALTOT = %NEWSPECIALTOT% + 1
SET /a NEWSPECIAL%NEWSPECIALTOT% = %SPECIALCHOICE%
SET /a GILCOST = %TEMPGILCOST%
SET /a TEMPGIL = %PLAYERGIL% - %GILCOST%
GOTO :EOF


:CONFIRMATION
CLS
CALL :INITIALISEBACKS
ECHO.
ECHO %BLACKSMITHNAME%: Ok, here's a summary^^!
ECHO.
ECHO. Accessory Name:    %ACCESSORYNAME%
ECHO. HP Bonus:          %TEMPMAXHP%
ECHO. MP Bonus:          %TEMPMAXMP%
ECHO. Luck Bonus:        %TEMPLCK%
SET /a COUNT = 0
:CONFIRMATIONLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !TEMPSPECIAL%COUNT%!
ECHO. Special %COUNT%:         !SPECIAL%TEMP%NAME!
IF %COUNT% LSS %TEMPSPECIALTOT% (
	GOTO :CONFIRMATIONLOOP
)
ECHO.
ECHO %BLACKSMITHNAME%: It'll cost you %GILCOST% Gil
IF %MAXHPSTONECOST% EQU 1 (
	ECHO             %MAXHPSTONECOST% !SPECIALITEM%MAXHPSTONEID%NAME!
) ELSE IF %MAXHPSTONECOST% GTR 1 (
	ECHO             %MAXHPSTONECOST% !SPECIALITEM%MAXHPSTONEID%NAME!s
)
IF %MAXMPSTONECOST% EQU 1 (
	ECHO             %MAXMPSTONECOST% !SPECIALITEM%MAXMPSTONEID%NAME!
) ELSE IF %MAXMPSTONECOST% GTR 1 (
	ECHO             %MAXMPSTONECOST% !SPECIALITEM%MAXMPSTONEID%NAME!s
)
IF %LCKSTONECOST% EQU 1 (
	ECHO             %LCKSTONECOST% !SPECIALITEM%LCKSTONEID%NAME!
) ELSE IF %LCKSTONECOST% GTR 1 (
	ECHO             %LCKSTONECOST% !SPECIALITEM%LCKSTONEID%NAME!s
)
IF %NEWSPECIALTOT% EQU 0 (
	GOTO :CONFIRMATIONLOOP2END
)
SET /a COUNT = 0
:CONFIRMATIONLOOP2
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !NEWSPECIAL%COUNT%!
SET TEMP2=!SPECIAL%TEMP%NICKNAME!
SET /a TEMP3 = !%TEMP2%STONEID!
ECHO             1 !SPECIALITEM%TEMP3%NAME!
IF %COUNT% LSS %NEWSPECIALTOT% (
	GOTO :CONFIRMATIONLOOP2
)
:CONFIRMATIONLOOP2END
ECHO.
ECHO %BLACKSMITHNAME%: Would you like to create this accessory?
ECHO.
ECHO. 1 - Yes
ECHO. 2 - No
ECHO. 0 - Back
ECHO.
:CONFIRMATIONCHOICE
SET /P CONFIRMATIONCHOICE=
ECHO.
IF "%CONFIRMATIONCHOICE%" EQU "1" (
	GOTO :EOF
) ELSE IF "%CONFIRMATIONCHOICE%" EQU "2" (
	GOTO :EOF
) ELSE IF "%CONFIRMATIONCHOICE%" EQU "0" (
	SET /a CONFIRMATIONBACK = 1
) ELSE (
	GOTO :CONFIRMATIONCHOICE
)
GOTO :EOF

:CREATEACCESSORY
SET /a ACCESSORIESCRAFTED = %ACCESSORIESCRAFTED% + 1
SET /a EQUIPMENTCRAFTED = %EQUIPMENTCRAFTED% + 1
CALL :REDUCERESOURCES
SET /a ACCESSORYID = %ACCESSORYTOTAL% + %SLOTNUM%
SET ACCESSORY%ACCESSORYID%NAME=%ACCESSORYNAME%
CALL :ACCESSORYNAMELENGTH
SET /a ACCESSORY%ACCESSORYID%MAXHP = %TEMPMAXHP%
SET /a ACCESSORY%ACCESSORYID%MAXMP = %TEMPMAXMP%
SET /a ACCESSORY%ACCESSORYID%LCK = %TEMPLCK%
SET /a ACCESSORY%ACCESSORYID%SPECIALTOT = %TEMPSPECIALTOT%
SET /a COUNT = 0
:CREATEACCESSORYLOOP
SET /a COUNT = %COUNT% + 1
SET /a ACCESSORY%ACCESSORYID%SPECIAL%COUNT% = !TEMPSPECIAL%COUNT%!
IF %COUNT% LSS !ACCESSORY%ACCESSORYID%SPECIALTOT! (
	GOTO :CREATEACCESSORYLOOP
)
ECHO SET ACCESSORY%ACCESSORYID%NAME=!ACCESSORY%ACCESSORYID%NAME!>>"%CUSTOMACCESSORYRESOURCEPATH%\Temp\customaccessorylist.bat"
ECHO SET /a ACCESSORY%ACCESSORYID%NAMELENGTH = !ACCESSORY%ACCESSORYID%NAMELENGTH! >> "%CUSTOMACCESSORYRESOURCEPATH%\Temp\customaccessorylist.bat"
ECHO SET ACCESSORY%ACCESSORYID%NAME=!ACCESSORY%ACCESSORYID%NAME!>>"%CUSTOMACCESSORYRESOURCEPATH%\Temp\customaccessory%SLOTNUM%.bat"
ECHO SET /a ACCESSORY%ACCESSORYID%MAXHP = !ACCESSORY%ACCESSORYID%MAXHP! >> "%CUSTOMACCESSORYRESOURCEPATH%\Temp\customaccessory%SLOTNUM%.bat"
ECHO SET /a ACCESSORY%ACCESSORYID%MAXMP = !ACCESSORY%ACCESSORYID%MAXMP! >> "%CUSTOMACCESSORYRESOURCEPATH%\Temp\customaccessory%SLOTNUM%.bat"
ECHO SET /a ACCESSORY%ACCESSORYID%LCK = !ACCESSORY%ACCESSORYID%LCK! >> "%CUSTOMACCESSORYRESOURCEPATH%\Temp\customaccessory%SLOTNUM%.bat"
ECHO SET /a ACCESSORY%ACCESSORYID%SPECIALTOT = !ACCESSORY%ACCESSORYID%SPECIALTOT! >> "%CUSTOMACCESSORYRESOURCEPATH%\Temp\customaccessory%SLOTNUM%.bat"
SET /a COUNT = 0
:CREATEACCESSORYLOOP2
SET /a COUNT = %COUNT% + 1
ECHO SET /a ACCESSORY%ACCESSORYID%SPECIAL%COUNT% = !ACCESSORY%ACCESSORYID%SPECIAL%COUNT%! >> "%CUSTOMACCESSORYRESOURCEPATH%\Temp\customaccessory%SLOTNUM%.bat"
IF %COUNT% LSS !ACCESSORY%ACCESSORYID%SPECIALTOT! (
	GOTO :CREATEACCESSORYLOOP2
)
GOTO :EOF


:ACCESSORYNAMELENGTH
SET /a ACCESSORY%ACCESSORYID%NAMELENGTH = 0
:ACCESSORYNAMELENGTHLOOPSTART
SET /a ACCESSORY%ACCESSORYID%NAMELENGTH += 1
SET TEMP1=!ACCESSORY%ACCESSORYID%NAMELENGTH!
SET SUBSTR=!ACCESSORY%ACCESSORYID%NAME:~%TEMP1%,1!
IF "%SUBSTR%" NEQ "" (
	GOTO :ACCESSORYNAMELENGTHLOOPSTART
) ELSE (
	SET /a TEMPCOUNT = !ACCESSORY%ACCESSORYID%NAMELENGTH! + 1
	SET SUBSTR=!ACCESSORY%ACCESSORYID%NAME:~%TEMPCOUNT%,1!
	IF "%SUBSTR%" NEQ "" (
		GOTO :ACCESSORYNAMELENGTHLOOPSTART
	)
)
GOTO :EOF



:EQUIP
CLS
ECHO.
ECHO %BLACKSMITHNAME%: You have successfully customised the !ACCESSORY%ACCESSORYID%NAME!^^!
ECHO.
ECHO             Would like like to equip it now?
ECHO.
ECHO. 1 - Yes
ECHO. 0 - No
ECHO.
:EQUIPCHOICE
SET /P EQUIPCHOICE=
ECHO.
IF "%EQUIPCHOICE%" EQU "1" (
	GOTO :EQUIPTRUE
) ELSE IF "%EQUIPCHOICE%" EQU "0" (
	GOTO :EQUIPFALSE
) ELSE (
	GOTO :EQUIPCHOICE
)
GOTO :EOF

:EQUIPTRUE
CALL :UNEQUIPACCESSORY
SET PLAYERACCESSORY=!ACCESSORY%ACCESSORYID%NAME!
SET /a PLAYERACCESSORYID = %ACCESSORYID%
SET /a PLAYEREQUIPMAXHP = !ACCESSORY%ACCESSORYID%MAXHP!
SET /a PLAYEREQUIPMAXMP = !ACCESSORY%ACCESSORYID%MAXMP!
SET /a PLAYEREQUIPLCK = !ACCESSORY%ACCESSORYID%LCK!
SET /a PLAYERMAXHP = %PLAYERBASEMAXHP% + %PLAYEREQUIPMAXHP%
SET /a PLAYERMAXMP = %PLAYERBASEMAXMP% + %PLAYEREQUIPMAXMP%
IF %PLAYERHP% GTR %PLAYERMAXHP% (
	SET /a PLAYERHP = %PLAYERMAXHP%
)
IF %PLAYERMP% GTR %PLAYERMAXMP% (
	SET /a PLAYERMP = %PLAYERMAXMP%
)
IF !ACCESSORY%ACCESSORYID%SPECIALTOT! EQU 0 (
	GOTO :ENDEQUIPLOOP
)
SET /a COUNT = 0
:EQUIPSPECIALLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !ACCESSORY%ACCESSORYID%SPECIAL%COUNT%!
SET /a PLAYER!SPECIAL%TEMP%NICKNAME! = 1
IF %COUNT% LSS !ACCESSORY%ACCESSORYID%SPECIALTOT! (
	GOTO :EQUIPSPECIALLOOP
)
:ENDEQUIPLOOP
ECHO.
ECHO You have equipped the %PLAYERACCESSORY%
ECHO.
pause
GOTO :EOF

:EQUIPFALSE
IF %ACCESSORYEXIST% EQU 1 (
	IF %PLAYERACCESSORYID% EQU %ACCESSORYID% (
		CALL :UNEQUIPACCESSORY
	)
) ELSE (
	IF %PLAYERACCESSORYID% EQU %BASECHOICE% (
		IF !PLAYERACCESSORY%PLAYERACCESSORYID%NUM! EQU 0 (
			CALL :UNEQUIPACCESSORY
		)
	)
)
GOTO :EOF
		


:UNEQUIPACCESSORY
IF !ACCESSORY%PLAYERACCESSORYID%SPECIALTOT! EQU 0 (
	GOTO :UNEQUIPLOOPEND
)
SET /a COUNT = 0
:UNEQUIPLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !ACCESSORY%PLAYERACCESSORYID%SPECIAL%COUNT%!
SET /a PLAYER!SPECIAL%TEMP%NICKNAME! = 0
IF %COUNT% LSS !ACCESSORY%PLAYERACCESSORYID%SPECIALTOT! (
	GOTO :UNEQUIPLOOP
)
:UNEQUIPLOOPEND
SET PLAYERACCESSORY=NONE
SET /a PLAYERACCESSORYID = 0
SET /a PLAYEREQUIPMAXHP = 0
SET /a PLAYEREQUIPMAXMP = 0
SET /a PLAYEREQUIPLCK = 0
GOTO :EOF


:REDUCERESOURCES
SET /a PLAYERGIL = %PLAYERGIL% - %GILCOST%
IF %ACCESSORYEXIST% EQU 0 (
	SET /a PLAYERACCESSORY%BASECHOICE%NUM = !PLAYERACCESSORY%BASECHOICE%NUM! - 1
)
SET /a PLAYERSPECIALITEM%MAXHPSTONEID%NUM = !PLAYERSPECIALITEM%MAXHPSTONEID%NUM! - %MAXHPSTONECOST%
SET /a PLAYERSPECIALITEM%MAXMPSTONEID%NUM = !PLAYERSPECIALITEM%MAXMPSTONEID%NUM! - %MAXMPSTONECOST%
SET /a PLAYERSPECIALITEM%LCKSTONEID%NUM = !PLAYERSPECIALITEM%LCKSTONEID%NUM! - %LCKSTONECOST%
IF %NEWSPECIALTOT% EQU 0 (
	GOTO :EOF
)
SET /a COUNT = 0
:REDUCERESOURCESLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !NEWSPECIAL%COUNT%!
SET TEMP2=!SPECIAL%TEMP%NICKNAME!
SET /a TEMP3 = !%TEMP2%STONEID!
SET /a PLAYERSPECIALITEM%TEMP3%NUM = !PLAYERSPECIALITEM%TEMP3%NUM! - 1
IF %COUNT% LSS %NEWSPECIALTOT% (
	GOTO :REDUCERESOURCESLOOP
)
GOTO :EOF

:INITIALISEBACKS
SET /a NAMEBACK = 0
SET /a BASEBACK = 0
SET /a STATINCREASEBACK = 0
SET /a SPECIALSBACK = 0
SET /a CONFIRMATIONBACK = 0
GOTO :EOF





:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF
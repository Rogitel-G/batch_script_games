SET /a COUNT = 0
SET /a STATICCOUNT = 0
:LOOPSTART
SET /a STATICCOUNT = %STATICCOUNT% + 1
IF !PLAYER%TYPE%%STATICCOUNT%NUM! GEQ 1 (
	CALL :SETVARS
)
IF %STATICCOUNT% LSS !%TYPE%TOTAL! (
	GOTO :LOOPSTART
)
SET /a OPTIONTOT = %COUNT%
GOTO :EOF

:SETVARS
SET /a COUNT=%COUNT%+1
SET TEXT%COUNT%NAME1=!%TYPE%%STATICCOUNT%NAME!
SET /a TEXT%COUNT%NAMELENGTH = !%TYPE%%STATICCOUNT%NAMELENGTH!
SET /a SPACENUM = 34-!TEXT%COUNT%NAMELENGTH!
SET TEXT%COUNT%NAME2=!%TYPE%%STATICCOUNT%SELL!
SET TEXT%COUNT%NAME3=!PLAYER%TYPE%%STATICCOUNT%NUM!
SET OPTION%COUNT%DISPLAYSELECTEDPRE=¯ 
SET OPTION%COUNT%DISPLAYINITIALPRE=  
SET OPTION%COUNT%DISPLAYSUFF=!TEXT%COUNT%NAME1!!SPACES%SPACENUM%!!TEXT%COUNT%NAME2! Gil, !TEXT%COUNT%NAME3! Left
SET OPTION%COUNT%DISPLAY=!OPTION%COUNT%DISPLAYINITIALPRE!!OPTION%COUNT%DISPLAYSUFF!
CALL :%TYPE%DESCRIPTIONS
GOTO :EOF


:WEAPONDESCRIPTIONS
SET /a TEXT%COUNT%DESCRIPTIONLINETOT = 4
SET TEXT%COUNT%DESCRIPTION1=Strength %PLAYEREQUIPSTR% ¯ !%TYPE%%STATICCOUNT%STR!
SET TEXT%COUNT%DESCRIPTION2=Magic    %PLAYEREQUIPMAG% ¯ !%TYPE%%STATICCOUNT%MAG!
SET TEXT%COUNT%DESCRIPTION3=Accuracy %PLAYEREQUIPACC% ¯ !%TYPE%%STATICCOUNT%ACC!
IF !%TYPE%%STATICCOUNT%SKILLTOT! GTR 0 (
	SET TEXT%COUNT%DESCRIPTION4=Skills:
) ELSE (
	SET TEXT%COUNT%DESCRIPTION4=No skills
	GOTO :EOF
)
SET /a TEXT%COUNT%DESCRIPTIONLINETOT = !TEXT%COUNT%DESCRIPTIONLINETOT! + !%TYPE%%STATICCOUNT%SKILLTOT!
SET /a SKILLCOUNT = 0
:SKILLLOOP
SET /a SKILLCOUNT = %SKILLCOUNT% + 1
SET /a TEMPCOUNT = %SKILLCOUNT% + 4
SET /a TEMP = !%TYPE%%STATICCOUNT%SKILL%SKILLCOUNT%!
SET TEXT%COUNT%DESCRIPTION%TEMPCOUNT%=!SKILL%TEMP%NAME! - !SKILL%TEMP%DESCRIPTION!
IF %SKILLCOUNT% LSS !%TYPE%%STATICCOUNT%SKILLTOT! (
	GOTO :SKILLLOOP
)
GOTO :EOF

:ARMOURDESCRIPTIONS
SET /a TEXT%COUNT%DESCRIPTIONLINETOT = 5
SET TEXT%COUNT%DESCRIPTION1=Endurance %PLAYEREQUIPEND% ¯ !%TYPE%%STATICCOUNT%END!
SET TEXT%COUNT%DESCRIPTION2=Spirit    %PLAYEREQUIPSPR% ¯ !%TYPE%%STATICCOUNT%SPR!
SET TEXT%COUNT%DESCRIPTION3=Evasion   %PLAYEREQUIPEVA% ¯ !%TYPE%%STATICCOUNT%EVA!
SET TEXT%COUNT%DESCRIPTION4=Speed     %PLAYEREQUIPSPD% ¯ !%TYPE%%STATICCOUNT%SPD!
SET /a TEMP = !%TYPE%%STATICCOUNT%WEAKNESSNUM! + !%TYPE%%STATICCOUNT%STRENGTHNUM! + !%TYPE%%STATICCOUNT%ELEMENTIMMUNENUM! + !%TYPE%%STATICCOUNT%ABSORBNUM! + !%TYPE%%STATICCOUNT%STATUSIMMUNENUM!
IF %TEMP% GTR 0 (
	SET TEXT%COUNT%DESCRIPTION5=Elemental and Status properties:
) ELSE (
	SET TEXT%COUNT%DESCRIPTION5=No Elemental or Status properties
	GOTO :EOF
)
SET /a TEXT%COUNT%DESCRIPTIONLINETOT = !TEXT%COUNT%DESCRIPTIONLINETOT! + %TEMP%
SET /a TEMPCOUNT = 5
SET RESISTANCE=WEAKNESS
SET RESISTANCEDISPLAY=Weakness to
CALL :ADDRESISTANCE
SET RESISTANCE=STRENGTH
SET RESISTANCEDISPLAY=Resistance to
CALL :ADDRESISTANCE
SET RESISTANCE=ELEMENTIMMUNE
SET RESISTANCEDISPLAY=Immunity to
CALL :ADDRESISTANCE
SET RESISTANCE=ABSORB
SET RESISTANCEDISPLAY=Absorption of
CALL :ADDRESISTANCE
SET RESISTANCE=STATUSIMMUNE
SET RESISTANCEDISPLAY=Status Immmunity to
CALL :ADDRESISTANCE
GOTO :EOF



:ADDRESISTANCE
IF !%TYPE%%STATICCOUNT%%RESISTANCE%NUM! EQU 0 (
	GOTO :EOF
)
SET /a RESISTANCECOUNT = 0
:RESISTANCELOOP
SET /a RESISTANCECOUNT = %RESISTANCECOUNT% + 1
SET /a TEMPCOUNT = %TEMPCOUNT% + 1
SET TEMP=!%TYPE%%STATICCOUNT%%RESISTANCE%%RESISTANCECOUNT%!
SET TEXT%COUNT%DESCRIPTION%TEMPCOUNT%=%RESISTANCEDISPLAY% %TEMP%
IF %RESISTANCECOUNT% LSS !%TYPE%%STATICCOUNT%%RESISTANCE%NUM! (
	GOTO :RESISTANCELOOP
)
GOTO :EOF

:ACCESSORYDESCRIPTIONS
SET /a TEXT%COUNT%DESCRIPTIONLINETOT = 4
SET TEXT%COUNT%DESCRIPTION1=Max HP %PLAYEREQUIPMAXHP% ¯ !%TYPE%%STATICCOUNT%MAXHP!
SET TEXT%COUNT%DESCRIPTION2=Max MP %PLAYEREQUIPMAXMP% ¯ !%TYPE%%STATICCOUNT%MAXMP!
SET TEXT%COUNT%DESCRIPTION3=Luck   %PLAYEREQUIPLCK% ¯ !%TYPE%%STATICCOUNT%LCK!
IF !%TYPE%%STATICCOUNT%SPECIALTOT! GTR 0 (
	SET TEXT%COUNT%DESCRIPTION4=Specials:
) ELSE (
	SET TEXT%COUNT%DESCRIPTION4=No specials
	GOTO :EOF
)
SET /a TEXT%COUNT%DESCRIPTIONLINETOT = !TEXT%COUNT%DESCRIPTIONLINETOT! + !%TYPE%%STATICCOUNT%SPECIALTOT!
SET /a SPECIALCOUNT = 0
:SPECIALLOOP
SET /a SPECIALCOUNT = %SPECIALCOUNT% + 1
SET /a TEMPCOUNT = %SPECIALCOUNT% + 4
SET /a TEMP = !%TYPE%%STATICCOUNT%SPECIAL%SPECIALCOUNT%!
SET TEXT%COUNT%DESCRIPTION%TEMPCOUNT%=!SPECIAL%TEMP%NAME!
IF %SPECIALCOUNT% LSS !%TYPE%%STATICCOUNT%SPECIALTOT! (
	GOTO :SPECIALLOOP
)
GOTO :EOF

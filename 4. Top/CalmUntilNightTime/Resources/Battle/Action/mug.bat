:ATTACK
CALL "%BATTLEDISPLAYRESOURCEPATH%\battledisplay.bat"
CALL :INITIALISE
CALL :MISSATTACK
CALL :CONFUSEATTACK
CALL :ATTACKDAM
CALL :CRITICALQUERY
CALL :PROTECTQUERY
CALL :HPCALCULATION
CALL :LIMITBREAKCALCULATION
CALL :CONFUSIONCURE
CALL :SLEEPCURE
CALL :BUFFBREAK
CALL :RECORDCOUNTINCREASE
CALL :ENDBATTLEQUERY
CALL :DISPLAYTEXT
CALL :STEALSTUFF
GOTO :EOF


:INITIALISE
SET /a MISS = 0
SET /a CRITICAL = 0
SET /a ATTACKSELF = 0
SET /a ENDSLEEP = 0
SET /a ENDCONFUSION = 0
SET /a BUFFBREAK = 0
SET /a PLAYERREVIVED = 0
GOTO :EOF

:MISSATTACK
SET EQUATION=ATTACKMISS
CALL "%BATTLERESOURCEPATH%\equations.bat"
REM SET /a MISSVAR = !%ATTACKER%ACC!*80/!%DEFENDER%EVA!
IF %RANPERC% GEQ %MISSVAR% (
	SET /a MISS = 1
)
GOTO :EOF

:CONFUSEATTACK
IF %MISS% EQU 1 (
	GOTO :EOF
)
CALL "%BATTLERESOURCEPATH%\ranresources.bat"
IF !%ATTACKER%CONFUSION! EQU 1 (
	IF %RANPERC% GTR 50 (
		SET /a ATTACKSELF = 1
		SET DEFENDER=%ATTACKER%
	) 
)
GOTO :EOF

:ATTACKDAM
IF %MISS% EQU 1 (
	GOTO :EOF
)
CALL "%BATTLERESOURCEPATH%\ranresources.bat"
SET EQUATION=ATTACKDAM
CALL "%BATTLERESOURCEPATH%\equations.bat"
REM SET /a DAM=(((((!%ATTACKER%STR!*5)+!%ATTACKER%LVL!)/((!%DEFENDER%END!*3)+!%DEFENDER%LVL!)*(!%ATTACKER%STR!+5)*!%ATTACKER%LVL!/!%DEFENDER%LVL!)*100)*%RANFACT%)/10000
IF %DAM% LSS 0 (
	SET /a DAM = %MAXDAM%
) ELSE IF %DAM% EQU 0 (
	SET /a DAM = 1
)
IF %ATTACKER% EQU PLAYER (
	IF %PLAYERMED% EQU 1 (
		SET /a DAM = %DAM%*5/2
	)
)
IF %ATTACKER% EQU PLAYER (
	IF %PLAYERBREAKDAM% EQU 1 (
		GOTO :EOF
	)
)
IF %DAM% GTR %MAXDAM% (
	SET /a DAM = %MAXDAM%
)
GOTO :EOF

:PROTECTQUERY
IF %MISS% EQU 1 (
	GOTO :EOF
)
IF !%DEFENDER%PROTECT! EQU 1 (
	SET /a DAM = %DAM%/2
	ECHO Protect halved the effectiveness of the attack^^!
	ECHO.
	CALL :WAITFORONE
)
GOTO :EOF


:CRITICALQUERY
IF %MISS% EQU 1 (
	GOTO :EOF
)
IF %ATTACKER% EQU PLAYER (
	IF %PLAYERINCCRITCHANCE% EQU 1 (
		SET /a RANCRITICALFACTOR = %RANCRITICALFACTOR%/2
	)
)
SET /a RANCRITICALFACTOR=%RANDOM% %% %CRITICALHITFACTOR%
SET /a CRITICALCHANCE = !%ATTACKER%LCK!*10/!%DEFENDER%LCK!
IF %CRITICALCHANCE% GTR %RANCRITICALFACTOR% (
	CALL :CRITICALSUCCESS
)
GOTO :EOF

:CRITICALSUCCESS
SET /a CRITICAL = 1
SET /a DAM = %DAM%*2
IF %ATTACKER% EQU PLAYER (
	IF %PLAYERINCCRITDAM% EQU 1 (
		SET /a DAM = %DAM%*2
	)
)
GOTO :EOF

:HPCALCULATION
IF %MISS% EQU 1 (
	GOTO :EOF
)
SET /a %DEFENDER%HP=!%DEFENDER%HP!-%DAM%
IF %ATTACKER% EQU PLAYER (
	CALL :APCALCULATION
)
IF !%DEFENDER%HP! LEQ 0 (
	SET /a %DEFENDER%HP=0
)
GOTO :EOF

:APCALCULATION
SET /a PLAYERAPGAIN = %DAM%/10
IF %PLAYERAPGAIN% GTR %MAXAPGAIN% (
	SET /a PLAYERAPGAIN = %MAXAPGAIN%
)
IF %PLAYERFASTAP% EQU 1 (
	SET /a PLAYERAPGAIN = %PLAYERAPGAIN%*2
)
SET /a PLAYERAP = %PLAYERAP% + %PLAYERAPGAIN%
IF %PLAYERAP% GTR %PLAYERMAXAP% (
	SET /a PLAYERAP = %PLAYERMAXAP%
)
GOTO :EOF

:LIMITBREAKCALCULATION
IF %MISS% EQU 1 (
	GOTO :EOF
)
IF %ATTACKER% EQU PLAYER (
	GOTO :EOF
)
IF %PLAYERLIMITBREAKACTIVE% EQU 0 (
	SET /a PLAYERLIMITBREAK = %PLAYERLIMITBREAK%+%DAM%
)
IF %PLAYERDOUBLELIM% EQU 1 (
	SET /a PLAYERLIMITBREAK = %PLAYERLIMITBREAK%+%DAM%
)
IF %PLAYERLIMITBREAK% LSS 0 (
	SET /a PLAYERLIMITBREAK = 0
) ELSE IF %PLAYERLIMITBREAK% GTR %PLAYERMAXLIMITBREAK% (
	SET /a PLAYERLIMITBREAK = %PLAYERMAXLIMITBREAK%
)
GOTO :EOF

:CONFUSIONCURE
IF %MISS% EQU 1 (
	GOTO :EOF
)
IF !%DEFENDER%CONFUSION! EQU 1 (
	SET /a %DEFENDER%CONFUSION = 0
	SET /a ENDCONFUSION = 1
)
GOTO :EOF

:SLEEPCURE
IF %MISS% EQU 1 (
	GOTO :EOF
)
IF !%DEFENDER%SLEEP! EQU 1 (
	SET /a %DEFENDER%SLEEP = 0
	SET /a ENDSLEEP = 1
)
GOTO :EOF

:BUFFBREAK
IF %PLAYERMED% EQU 0 (
	IF %PLAYERFOC% EQU 0 (
		IF %PLAYERAIM% EQU 0 (
			IF %PLAYERHIDE% EQU 0 (
				GOTO :EOF
			)
		)
	)
)
IF %ATTACKER% EQU ENEMY (
	IF %MISS% EQU 1 (
		GOTO :EOF
	)
	CALL "%BATTLERESOURCEPATH%\ranresources.bat"
	IF %RANPERC% LSS 30 (
		SET /a PLAYERMED = 0
		SET /a PLAYERFOC = 0
		SET /a PLAYERAIM = 0
		SET /a PLAYERHIDE = 0
		SET /a BUFFBREAK = 1
	)
)
IF %ATTACKER% EQU PLAYER (
	IF %MISS% EQU 1 (
		GOTO :EOF
	)
	SET /a PLAYERMED = 0
)
GOTO :EOF

:RECORDCOUNTINCREASE
IF %MISS% EQU 1 (
	IF %ATTACKER% EQU ENEMY (	
		SET /a DODGESUCCESSCOUNT = %DODGESUCCESSCOUNT% + 1
	) ELSE (
		SET /a PLAYERMISSCOUNT = %PLAYERMISSCOUNT% + 1
	)
	GOTO :EOF
)
IF %ATTACKER% EQU PLAYER (
	SET /a PLAYERATTACKCOUNT = %PLAYERATTACKCOUNT% + 1
	IF %CRITICAL% EQU 1 (
		SET /a CRITICALHITSUCCESSCOUNT = %CRITICALHITSUCCESSCOUNT% + 1
	)
)
GOTO :EOF


:ENDBATTLEQUERY
IF !%DEFENDER%HP! LEQ 0 (
	SET /a %DEFENDER%HP = 0
	IF %DEFENDER% EQU ENEMY (
		SET /a WINBATTLE = 1
	) ELSE IF %DEFENDER% EQU PLAYER (
		SET /a LOSEBATTLE = 1
	)
)
GOTO :EOF

:DISPLAYTEXT
IF %AUTOBATTLE% EQU 1 (
	SET WAITTIME=ZERO
) ELSE (
	SET WAITTIME=ONE
)
SET TEMPPATH=%MUSICPATH%\Battle\Spells
SET FILE=Attack.mp3
IF EXIST "%TEMPPATH%\%FILE%" (
	CALL "%MUSICPATH%\soundeffect.bat"
)
ECHO !%ATTACKER%NAME! attacks
ECHO.
SET ANIMATION=%ATTACKER%ATTACK
CALL "%BATTLEDISPLAYRESOURCEPATH%\animation.bat"
IF %MISS% EQU 1 (
	ECHO !%ATTACKER%NAME!'s attack missed^^!
	ECHO.
	CALL :WAITFOR%WAITTIME%
	GOTO :EOF
)
IF %CRITICAL% EQU 1 (
	ECHO Critical hit^^!
	ECHO.
	CALL :WAITFOR%WAITTIME%
)
ECHO !%ATTACKER%NAME!'s attack did %DAM% damage^^!
ECHO.
CALL :WAITFOR%WAITTIME%
IF %ATTACKSELF% EQU 1 (
	ECHO ...to himself^^!
	ECHO.
	CALL :WAITFOR%WAITTIME%
)
IF %BUFFBREAK% EQU 1 (
	ECHO The attack caused !%DEFENDER%NAME! to lose their buff^^!
	ECHO.
	CALL :WAITFOR%WAITTIME%
)
IF %ENDSLEEP% EQU 1 (
	ECHO The attack caused !%DEFENDER%NAME! to wake up
	ECHO.
	CALL :WAITFOR%WAITTIME%
)
IF %ENDCONFUSION% EQU 1 (
	ECHO The attack caused !%DEFENDER%NAME! to snap out of their confusion
	ECHO.
	CALL :WAITFOR%WAITTIME%
)
IF %PLAYERREVIVED% EQU 1 (
	ECHO You were brought back from the brink of death by Auto Life
	ECHO.
	CALL :WAITFOR%WAITTIME%
	ECHO Your HP and MP have been fully restored^^!
	ECHO.
	CALL :WAITFOR%WAITTIME%
)
GOTO :EOF


:STEALSTUFF
IF %MISS% EQU 1 (
	GOTO :EOF
)
CALL :NOTHINGTOSTEALQUERY
IF %NOTHINGTOSTEAL% EQU 1 (
	GOTO :EOF
)
CALL :STEALQUERY
IF %STEALSUCCESS% EQU 1 (
	CALL :SUCCESS
) ELSE (
	CALL :FAIL
)
GOTO :EOF

:NOTHINGTOSTEALQUERY
SET /a NOTHINGTOSTEAL = 0
IF !ENEMY%ENEMYID%SET%REWARDSET%STEALNUM! EQU 0 (
	CALL :NOTHINGTOSTEAL
	GOTO :EOF
) 
SET /a TEMPCOUNT = 0
:NOTHINGTOSTEALQUERYLOOP
SET /a TEMPCOUNT = %TEMPCOUNT% + 1
IF !STEALITEM%TEMPCOUNT%SUCCESS! EQU 0 (
	GOTO :EOF
)
IF %TEMPCOUNT% LSS !ENEMY%ENEMYID%SET%REWARDSET%STEALNUM! (
	GOTO :NOTHINGTOSTEALQUERYLOOP
)
CALL :NOTHINGTOSTEAL
GOTO :EOF

:NOTHINGTOSTEAL
SET /a NOTHINGTOSTEAL = 1
ECHO The enemy has nothing to steal^^!
ECHO.
CALL :WAITFORTWO
GOTO :EOF

:STEALQUERY
SET /a STEALSUCCESS = 0
SET /a TEMPCOUNT = 0
:STEALLOOP
SET /a TEMPCOUNT = %TEMPCOUNT% + 1
CALL :STEALITEMQUERY
IF %STEALSUCCESS% EQU 1 (
	GOTO :EOF
)
IF %TEMPCOUNT% LSS !ENEMY%ENEMYID%SET%REWARDSET%STEALNUM! (
	GOTO :STEALLOOP
)
GOTO :EOF


:STEALITEMQUERY
IF !STEALITEM%TEMPCOUNT%SUCCESS! EQU 1 (
	GOTO :EOF
)
SET /a RANSTEAL=%RANDOM% %% %BASESTEALCHANCE%
IF !ITEM%TEMPCOUNT%STEALCHANCE! GTR %RANSTEAL% (
	CALL :STEALSUCCESS
)
GOTO :EOF



:STEALSUCCESS
SET /a STEALSUCCESS = 1
SET /a STEALSUCCESSCOUNT = %STEALSUCCESSCOUNT% + 1
SET /a STEALITEM%TEMPCOUNT%SUCCESS = 1
SET /a TEMP = !ENEMY%ENEMYID%SET%REWARDSET%STEAL%TEMPCOUNT%!
SET TEMPTYPE=!ENEMY%ENEMYID%SET%REWARDSET%STEAL%TEMPCOUNT%TYPE!
SET /a PLAYER%TEMPTYPE%%TEMP%NUM = !PLAYER%TEMPTYPE%%TEMP%NUM! + 1
SET /a MAXSPACENUM = 49
SET STEALREWARD%TEMPCOUNT%=!SPACES%MAXSPACENUM%!
GOTO :EOF

:SUCCESS
ECHO You have successfully stolen one !%TEMPTYPE%%TEMP%NAME!^^!
ECHO.
CALL :WAITFORTHREE
GOTO :EOF

:FAIL
ECHO You failed to steal from the %ENEMYNAME%
ECHO.
CALL :WAITFORTWO
GOTO :EOF





:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF
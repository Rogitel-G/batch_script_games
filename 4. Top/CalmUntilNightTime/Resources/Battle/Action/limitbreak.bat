SET /a LIMITBREAKUSED = %LIMITBREAKUSED% + 1
CALL :SPECIALBATTLEQUERY
CALL :LIMITBREAKQUERY
CALL :FIRSTDISPLAY
CALL :USELIMITBREAK
GOTO :EOF

:SPECIALBATTLEQUERY
::To stop people catching bosses
IF %SPECIALFIGHT% EQU 1 (
	SET /a LIMITBREAK%AUTOCATCHID%BATTLECOUNT = 0
)
GOTO :EOF


:LIMITBREAKQUERY
IF %PLAYERMORELIMITBREAK% EQU 1 (
	SET /a LIMITBREAKNUM = 3
) ELSE (
	SET /a LIMITBREAKNUM = 2
)
SET /a PLAYERLIMITBREAK = 0
SET /a PLAYERLIMITBREAKACTIVE = 0
SET /a LIMITBREAKCOUNT = 1
SET /a LIMITBREAK1COUNT = 0
SET /a LIMITBREAK2COUNT = 0
::This defaults it to non-elemental damage if you haven't used any skills
SET /a LIMITBREAK1 = 1
SET /a LIMITBREAK2 = 0
SET /a LIMITBREAK3 = 0
SET /a COUNT = 0
:LIMITBREAKLOOP
SET /a COUNT = %COUNT% + 1
::GTR means that it will prioritise earlier limit breaks, GEQ will prioritise later ones.
::We can't use GEQ because LIMITBREAK2COUNT is set to 0. If we use GEQ then it'll reset that every time.
IF !LIMITBREAK%COUNT%BATTLECOUNT! GTR !LIMITBREAK%LIMITBREAKCOUNT%COUNT! (
	SET /a LIMITBREAK%LIMITBREAKCOUNT% = %COUNT%
	SET /a LIMITBREAK%LIMITBREAKCOUNT%COUNT = !LIMITBREAK%COUNT%BATTLECOUNT!
)
IF %COUNT% LSS %LIMITBREAKTOT% (
	GOTO :LIMITBREAKLOOP
)
IF !LIMITBREAK%LIMITBREAKCOUNT%! EQU 0 (
	SET /a LIMITBREAKNUM = %LIMITBREAKNUM% - 1
)
IF %LIMITBREAKCOUNT% LSS %LIMITBREAKNUM% (
	SET /a LIMITBREAK!LIMITBREAK%LIMITBREAKCOUNT%!BATTLECOUNT = 0
	SET /a LIMITBREAKCOUNT = %LIMITBREAKCOUNT% + 1
	SET /a COUNT = 0
	GOTO :LIMITBREAKLOOP
)
GOTO :EOF

:FIRSTDISPLAY
CALL "%BATTLEDISPLAYRESOURCEPATH%\battledisplay.bat"
ECHO Because of the skills used in this battle, your limit break will take the form of:
SET /a COUNT = 0
:FIRSTDISPLAYLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !LIMITBREAK%COUNT%!
ECHO !LIMITBREAK%TEMP%NAME!
IF %COUNT% LSS %LIMITBREAKNUM% (
	GOTO :FIRSTDISPLAYLOOP
)
ECHO.
CALL :WAITFORTWO
GOTO :EOF

:USELIMITBREAK
SET /a USECOUNT = 0
:USELIMITBREAKLOOP
SET /a USECOUNT = %USECOUNT% + 1
SET /a LIMITCOUNT = !LIMITBREAK%USECOUNT%!
SET /a LIMITBREAK%LIMITCOUNT%USED = !LIMITBREAK%LIMITCOUNT%USED! + 1
CALL "%BATTLEACTIONRESOURCEPATH%\LimitBreaks\!LIMITBREAK%LIMITCOUNT%NICKNAME!.bat"
SET ANIMATION=%ATTACKER%!LIMITBREAK%LIMITCOUNT%ANIMATION!
CALL "%BATTLEDISPLAYRESOURCEPATH%\animation.bat"
IF %USECOUNT% LSS %LIMITBREAKNUM% (
	GOTO :USELIMITBREAKLOOP
)
GOTO :EOF


:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF
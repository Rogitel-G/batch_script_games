CALL "%BATTLEDISPLAYRESOURCEPATH%\battledisplay.bat"
CALL :SETITEM
CALL :INITIALISERESISTANCES
CALL :REDUCEITEMNUM
CALL :CALCULATIONS
IF %ATTACKER% EQU PLAYER (
	SET /a PLAYERUSEITEMCOUNT = %PLAYERUSEITEMCOUNT% + 1
)
CALL :ITEMTEXT
GOTO :EOF

:SETITEM
IF %ATTACKER% EQU ENEMY (
	SET /a ENEMYCUSTOMITEM = 0
)
SET /a CUSTOMITEM = !%ATTACKER%CUSTOMITEM!
IF %ATTACKER% EQU PLAYER (
	IF %PLAYERCUSTOMITEM% EQU 1 (
		CALL "%CUSTOMITEMRESOURCEPATH%\Temp\customitem%ITEMCUSTCHOICE%.bat"
		GOTO :EOF
	)
)
SET /a ITEMCHOICE = !%ATTACKER%ITEMCHOICE!
SET ITEMNAME=!ITEM%ITEMCHOICE%NAME!
SET /a ITEMEFFECTSNUM = !ITEM%ITEMCHOICE%EFFECTSNUM!
SET /a EFFECTNUM = %ITEMEFFECTSNUM%
:LOOP1START
SET ITEMEFFECT%EFFECTNUM%TYPE=!ITEM%ITEMCHOICE%EFFECT%EFFECTNUM%TYPE!
SET ITEMEFFECT%EFFECTNUM%TARGET=!ITEM%ITEMCHOICE%EFFECT%EFFECTNUM%TARGET!
SET ITEMEFFECT%EFFECTNUM%ATTRIBUTE=!ITEM%ITEMCHOICE%EFFECT%EFFECTNUM%ATTRIBUTE!
SET ITEMEFFECT%EFFECTNUM%ELEMENT=!ITEM%ITEMCHOICE%EFFECT%EFFECTNUM%ELEMENT!
SET /a ITEMEFFECT%EFFECTNUM%TURNCOUNT = !ITEM%ITEMCHOICE%EFFECT%EFFECTNUM%TURNCOUNT!
SET /a ITEMEFFECT%EFFECTNUM%AMOUNT = !ITEM%ITEMCHOICE%EFFECT%EFFECTNUM%AMOUNT!
SET /a ITEMEFFECT%EFFECTNUM%FIXEDAMOUNT = !ITEM%ITEMCHOICE%EFFECT%EFFECTNUM%FIXEDAMOUNT!
SET ITEMEFFECT%EFFECTNUM%TEXT=!ITEM%ITEMCHOICE%EFFECT%EFFECTNUM%TEXT!
SET /a EFFECTNUM = %EFFECTNUM% - 1
IF %EFFECTNUM% GTR 0 (
	GOTO :LOOP1START
)
GOTO :EOF


:INITIALISERESISTANCES
SET /a EFFECTNUM = 0
:LOOP2START
SET /a EFFECTNUM = %EFFECTNUM% + 1
SET /a ITEMEFFECT%EFFECTNUM%WEAK = 0
SET /a ITEMEFFECT%EFFECTNUM%STRONG = 0
SET /a ITEMEFFECT%EFFECTNUM%IMMUNE = 0
SET /a ITEMEFFECT%EFFECTNUM%ABSORB = 0
SET /a ITEMEFFECT%EFFECTNUM%STATUSIMMUNE = 0
SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = !ITEMEFFECT%EFFECTNUM%AMOUNT!
IF %EFFECTNUM% LSS %ITEMEFFECTSNUM% (
	GOTO :LOOP2START
)
GOTO :EOF

:REDUCEITEMNUM
IF %CUSTOMITEM% EQU 0 (
	CALL :STANDARD
) ELSE (
	DEL "%CUSTOMITEMRESOURCEPATH%\Temp\customitem%ITEMCUSTCHOICE%.bat"
	ECHO SET ITEMSLOT%ITEMCUSTCHOICE%NAME=>>"%CUSTOMITEMRESOURCEPATH%\Temp\customitemlist.bat"
	ECHO SET ITEMSLOT%ITEMCUSTCHOICE%NAMELENGTH= >> "%CUSTOMITEMRESOURCEPATH%\Temp\customitemlist.bat"
)
GOTO :EOF

:STANDARD
SET /a %ATTACKER%ITEM%ITEMCHOICE%NUM = !%ATTACKER%ITEM%ITEMCHOICE%NUM!-1
IF %ATTACKER% EQU PLAYER (
	IF !PLAYERITEM%ITEMCHOICE%NUM! EQU 0 (
		SET /a AUTOBATTLE = 0
	)
)
GOTO :EOF

:CALCULATIONS
SET /a EFFECTNUM = 0
:LOOP3START
SET /a EFFECTNUM = %EFFECTNUM% + 1
SET TEMP=!ITEMEFFECT%EFFECTNUM%TARGET!
SET TEMP=!%TEMP%!
SET TEMP2=!ITEMEFFECT%EFFECTNUM%ATTRIBUTE!
IF !ITEMEFFECT%EFFECTNUM%TYPE! EQU STAT (
	CALL :FIXEDAMOUNTCALCULATION
	IF !ITEMEFFECT%EFFECTNUM%ELEMENT! NEQ NONE (
		CALL :ELEMENTS
	)
	CALL :SPECIALCASES
	CALL :SHELL
	CALL :DAMAGELIMITCHECK
	CALL :STATCALCULATION
	CALL :LIMITCHECK
) ELSE IF !ITEMEFFECT%EFFECTNUM%TYPE! EQU STATUS (
	IF !ITEMEFFECT%EFFECTNUM%TEMPAMOUNT! EQU 1 (
		CALL :IMMUNITIES
	)
	CALL :STATUSCALCULATION
	CALL :TURNCOUNTS
)
IF %EFFECTNUM% LSS %ITEMEFFECTSNUM% (
	GOTO :LOOP3START
)
GOTO :EOF

:FIXEDAMOUNTCALCULATION
IF !ITEMEFFECT%EFFECTNUM%FIXEDAMOUNT! EQU 0 (
	IF !ITEMEFFECT%EFFECTNUM%TARGET! EQU DEFENDER (
		CALL :NEWAMOUNTCALC
	)
)
GOTO :EOF

:NEWAMOUNTCALC
SET /a TEMPAMOUNT = -!ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!
SET EQUATION=ITEMS
CALL "%BATTLERESOURCEPATH%\equations.bat"
REM SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = -(((((!%ATTACKER%MAG!*%TEMPAMOUNT%)+!%ATTACKER%LVL!)/((!%DEFENDER%SPR!*3)+!%DEFENDER%LVL!)*(!%ATTACKER%MAG!+%TEMPAMOUNT%)*!%ATTACKER%LVL!/!%DEFENDER%LVL!)*100)*%RANFACT%)/10000
GOTO :EOF


:ELEMENTS
IF /I !ITEMEFFECT%EFFECTNUM%ELEMENT! EQU NONE (
	GOTO :EOF
)
:WEAKNESSES
SET /a COUNT = 0
IF !%TEMP%WEAKNESSNUM! EQU 0 (
	GOTO :STRENGTHS
)
:WEAKNESSLOOP
SET /a COUNT = %COUNT% + 1
IF /I !ITEMEFFECT%EFFECTNUM%ELEMENT! EQU !%TEMP%WEAKNESS%COUNT%! (
	SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = !ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!*2
	SET /a ITEMEFFECT%EFFECTNUM%WEAK = 1
) 
IF %COUNT% LSS !%TEMP%WEAKNESSNUM! (
	GOTO :WEAKNESSLOOP
)
:STRENGTHS
SET /a COUNT = 0
IF !%TEMP%STRENGTHNUM! EQU 0 (
	GOTO :ABSORBS
)
:STRENGTHLOOP
SET /a COUNT = %COUNT% + 1
IF /I !ITEMEFFECT%EFFECTNUM%ELEMENT! EQU !%TEMP%STRENGTH%COUNT%! (
	SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = !ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!/2
	SET /a ITEMEFFECT%EFFECTNUM%STRONG = 1
)
IF %COUNT% LSS !%TEMP%STRENGTHNUM! (
	GOTO :STRENGTHLOOP
)
:ABSORBS
SET /a COUNT = 0
IF !%DEFENDER%ABSORBNUM! EQU 0 (
	GOTO :ELEMENTIMMUNES
)
:ABSORBLOOP
SET /a COUNT = %COUNT% + 1
IF /I !ITEMEFFECT%EFFECTNUM%ELEMENT! EQU !%TEMP%ABSORB%COUNT%! (
	SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = -!ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!
	SET /a ITEMEFFECT%EFFECTNUM%ABSORB = 1
)
:ELEMENTIMMUNES
SET /a COUNT = 0
IF !%DEFENDER%ELEMENTIMMUNENUM! EQU 0 (
	GOTO :EOF
)
:ELEMENTIMMUNELOOP
SET /a COUNT = %COUNT% + 1
IF /I !ITEMEFFECT%EFFECTNUM%ELEMENT! EQU !%TEMP%ELEMENTIMMUNE%COUNT%! (
	SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = 0
	SET /a ITEMEFFECT%EFFECTNUM%IMMUNE = 1
)
IF %COUNT% LSS !%DEFENDER%ELEMENTIMMUNENUM! (
	GOTO :ELEMENTIMMUNELOOP
)
GOTO :EOF

:SPECIALCASES
IF /I !ITEMEFFECT%EFFECTNUM%ELEMENT! EQU Demi (
	CALL :DEMI
)
GOTO :EOF

:DEMI
IF /I %ITEMCHOICE% EQU %DEMIITEMID% (
	SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = -!%DEFENDER%HP!/4
) ELSE IF /I %ITEMCHOICE% EQU %DEMIRAITEMID% (
	SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = -!%DEFENDER%HP!/2
) ELSE IF /I %ITEMCHOICE% EQU %DEMIGAITEMID% (
	SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = -!%DEFENDER%HP!*3/4
)
IF !ITEMEFFECT%EFFECTNUM%WEAK! EQU 1 (
	SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = !ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!*2
)
IF !ITEMEFFECT%EFFECTNUM%STRONG! EQU 1 (
	SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = !ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!/2
)
IF !ITEMEFFECT%EFFECTNUM%ABSORB! EQU 1 (
	SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = -!ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!
)
IF !ITEMEFFECT%EFFECTNUM%IMMUNE! EQU 1 (
	SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = 0
)

:SHELL
IF !ITEMEFFECT%EFFECTNUM%FIXEDAMOUNT! EQU 0 (
	IF !ITEMEFFECT%EFFECTNUM%TARGET! EQU DEFENDER (
		IF !%DEFENDER%SHELL! EQU 1 (
			SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = !ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!/2
			ECHO Shell halved the effectiveness of the item
			ECHO.
			CALL :WAITFORONE
		)
	)
)
GOTO :EOF

:DAMAGELIMITCHECK
SET /a TEMPAMOUNT = -!ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!
IF %ATTACKER% EQU PLAYER (
	IF %PLAYERBREAKDAM% EQU 1 (
		GOTO :EOF
	)
)
IF %TEMPAMOUNT% GTR %MAXDAM% (
	SET /a ITEMEFFECT%EFFECTNUM%TEMPAMOUNT = -%MAXDAM%
	SET /a TEMPAMOUNT = %MAXDAM%
)
GOTO :EOF

:STATCALCULATION
SET TEMP=!ITEMEFFECT%EFFECTNUM%TARGET!
SET TEMP=!%TEMP%!
SET TEMP2=!ITEMEFFECT%EFFECTNUM%ATTRIBUTE!
SET /a %TEMP%!ITEMEFFECT%EFFECTNUM%ATTRIBUTE! = !%TEMP%%TEMP2%! + !ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!
GOTO :EOF

:LIMITCHECK
IF !%TEMP%%TEMP2%! GTR !%TEMP%MAX%TEMP2%! (
	SET /a %TEMP%%TEMP2% = !%TEMP%MAX%TEMP2%!
) ELSE IF !%TEMP%%TEMP2%! LEQ 0 (
	SET /a %TEMP%%TEMP2% = 0
	CALL :WINORLOSE
)
GOTO :EOF

:WINORLOSE
IF %PLAYERHP% LEQ 0 (
	SET /a LOSEBATTLE = 1
) ELSE IF %ENEMYHP% LEQ 0 (
	SET /a WINBATTLE = 1
)
GOTO :EOF






:IMMUNITIES
SET TEMP=!ITEMEFFECT%EFFECTNUM%TARGET!
SET TEMP=!%TEMP%!
IF !%TEMP%STATUSIMMUNENUM! EQU 0 (
	GOTO :EOF
)
SET /a COUNT = 0
:STATUSIMMUNELOOP
SET /a COUNT = %COUNT% + 1
IF !ITEMEFFECT%EFFECTNUM%ATTRIBUTE! EQU !%TEMP%STATUSIMMUNE%COUNT%! (
	SET /a ITEMEFFECT%EFFECTNUM%STATUSIMMUNE = 1
)
IF %COUNT% LSS !%TEMP%STATUSIMMUNENUM! (
	GOTO :STATUSIMMUNELOOP
)
IF !ITEMEFFECT%EFFECTNUM%TEMPAMOUNT! EQU 0 (
	SET /a ITEMEFFECT%EFFECTNUM%STATUSIMMUNE = 0
)
GOTO :EOF


:STATUSCALCULATION
IF !ITEMEFFECT%EFFECTNUM%STATUSIMMUNE! EQU 0 (
	SET /a %TEMP%!ITEMEFFECT%EFFECTNUM%ATTRIBUTE! = !ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!
)
GOTO :EOF

:TURNCOUNTS
IF !ITEMEFFECT%EFFECTNUM%TEMPAMOUNT! EQU 1 (
	IF !ITEMEFFECT%EFFECTNUM%STATUSIMMUNE! EQU 0 (
		IF !ITEMEFFECT%EFFECTNUM%TURNCOUNT! GTR 0 (
			SET /a %TEMP%!ITEMEFFECT%EFFECTNUM%ATTRIBUTE!TURNCOUNT = !ITEMEFFECT%EFFECTNUM%TURNCOUNT!
		)
	)
)
GOTO :EOF


:ITEMTEXT
ECHO !%ATTACKER%NAME! used a %ITEMNAME%
ECHO.
SET ANIMATION=%ATTACKER%!ITEM%ITEMCHOICE%ANIMATION!
CALL "%BATTLEDISPLAYRESOURCEPATH%\animation.bat"
SET /a EFFECTNUM = 0
:LOOP8START
SET /a EFFECTNUM = %EFFECTNUM% + 1
CALL :WAITFORONE
ECHO !%ATTACKER%NAME!'s !ITEMEFFECT%EFFECTNUM%TEXT!
ECHO.
CALL :WAITFORONE
CALL :RESISTANCES
IF /I !ITEMEFFECT%EFFECTNUM%TARGET! EQU DEFENDER (
	IF !ITEMEFFECT%EFFECTNUM%FIXEDAMOUNT! EQU 0 (
		CALL :DAMAGETEXT
		IF %DEFENDER% EQU PLAYER (
			CALL :LIMITBREAKCALCULATION
		)
	)
)
IF %EFFECTNUM% LSS %ITEMEFFECTSNUM% (
	GOTO :LOOP8START
)
IF %CUSTOMITEM% EQU 0 (
	ECHO !%ATTACKER%NAME! has !%ATTACKER%ITEM%ITEMCHOICE%NUM! %ITEMNAME%s remaining
	ECHO.
)
CALL :WAITFORTHREE
GOTO :EOF

:DAMAGETEXT
SET /a TEMPAMOUNT = -!ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!
ECHO !%ATTACKER%NAME! did %TEMPAMOUNT% damage^^!
ECHO.
CALL :WAITFORONE
GOTO :EOF

:LIMITBREAKCALCULATION
SET /a TEMPAMOUNT = -!ITEMEFFECT%EFFECTNUM%TEMPAMOUNT!
IF %PLAYERLIMITBREAKACTIVE% EQU 0 (
	SET /a PLAYERLIMITBREAK = %PLAYERLIMITBREAK%+%TEMPAMOUNT%
)
IF %PLAYERDOUBLELIM% EQU 1 (
	SET /a PLAYERLIMITBREAK = %PLAYERLIMITBREAK%+%TEMPAMOUNT%
)
IF %PLAYERLIMITBREAK% LSS 0 (
	SET /a PLAYERLIMITBREAK = 0
) ELSE IF %PLAYERLIMITBREAK% GTR %PLAYERMAXLIMITBREAK% (
	SET /a PLAYERLIMITBREAK = %PLAYERMAXLIMITBREAK%
)
GOTO :EOF

:RESISTANCES
IF !ITEMEFFECT%EFFECTNUM%WEAK! EQU 1 (
	ECHO It's super effective^^!
	ECHO.
	CALL :WAITFORONE
)
IF !ITEMEFFECT%EFFECTNUM%STRONG! EQU 1 (
	ECHO It's not very effective...
	ECHO.
	CALL :WAITFORONE
)
IF !ITEMEFFECT%EFFECTNUM%ABSORB! EQU 1 (
	ECHO !%DEFENDER%NAME! absorbs !ITEMEFFECT%EFFECTNUM%ELEMENT! magic
	ECHO.
	CALL :WAITFORONE
)
IF !ITEMEFFECT%EFFECTNUM%IMMUNE! EQU 1 (
	ECHO !%DEFENDER%NAME! is immune to !ITEMEFFECT%EFFECTNUM%ELEMENT!
	ECHO.
	CALL :WAITFORONE
)
IF !ITEMEFFECT%EFFECTNUM%STATUSIMMUNE! EQU 1 (
	ECHO !%DEFENDER%NAME! is immune to !ITEMEFFECT%EFFECTNUM%ATTRIBUTE!
	ECHO.
	CALL :WAITFORONE
)
GOTO :EOF


:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF

:BADCHOICEQUERY
CALL :STATUSIMMUNEQUERY
CALL :ELEMENTRESISTANCEQUERY
CALL :UNNECESSARYDISPELQUERY
CALL :ALREADYACTIVEQUERY
CALL :REFLECTQUERY
CALL :CUREQUERY
CALL :UNNECESSARYESUNAQUERY
GOTO :EOF

:STATUSIMMUNEQUERY
SET TEMPTARGET=!%ENEMYSPELLEFFECT1TARGET%!
IF /I !ENEMYSPELLEFFECT1TYPE! EQU STATUS (
	IF /I !ENEMYSPELLEFFECT1ATTRIBUTE! EQU !%TEMPTARGET%STATUSIMMUNE1! (
		SET /a BADCHOICE = 1
	) ELSE IF /I !ENEMYSPELLEFFECT1ATTRIBUTE! EQU !%TEMPTARGET%STATUSIMMUNE2! (
		SET /a BADCHOICE = 1
	) ELSE IF /I !ENEMYSPELLEFFECT1ATTRIBUTE! EQU !%TEMPTARGET%STATUSIMMUNE3! (
		SET /a BADCHOICE = 1
	)
)
GOTO :EOF

:ELEMENTRESISTANCEQUERY
SET TEMPTARGET=!%ENEMYSPELLEFFECT1TARGET%!
IF /I %ENEMYSPELLEFFECT1ELEMENT% NEQ NONE (
	IF /I !ENEMYSPELLEFFECT1ELEMENT! EQU !%TEMPTARGET%ELEMENTIMMUNE! (
		SET /a BADCHOICE = 1
	) ELSE IF /I !ENEMYSPELLEFFECT1ELEMENT! EQU !%TEMPTARGET%STRENGTH! (
		SET /a BADCHOICE = 1
	) ELSE IF /I !ENEMYSPELLEFFECT1ELEMENT! EQU !%TEMPTARGET%ABSORB! (
		SET /a BADCHOICE = 1
	)
)
GOTO :EOF

:UNNECESSARYDISPELQUERY
IF /I %ENEMYSPELLTYPE% EQU SUPP (
	IF %ENEMYSPELLCHOICE% EQU %DISPELID% (
		SET /a BADCHOICE = 1
	)
)
GOTO :EOF

:ALREADYACTIVEQUERY
SET TEMPTARGET=!%ENEMYSPELLEFFECT1TARGET%!
SET TEMPTYPE=!ENEMYSPELLEFFECT1ATTRIBUTE!
IF %ENEMYSPELLEFFECTSNUM% EQU 1 (
	IF !%TEMPTARGET%%TEMPTYPE%! EQU !ENEMYSPELLEFFECT1AMOUNT! (
		SET /a BADCHOICE = 1
	)
)
GOTO :EOF

:REFLECTQUERY
SET TEMPTARGET=!%ENEMYSPELLEFFECT1TARGET%!
IF !%TEMPTARGET%REFLECT! EQU 1 (
	IF %ENEMYSPELLREFLECTABLE% EQU 1 (
		SET /a BADCHOICE = 1
	)
)
GOTO :EOF

:CUREQUERY
CALL "%BATTLERESOURCEPATH%\ranresources.bat"
SET /a ENEMYPERCENTHP = %ENEMYHP%*100/%ENEMYMAXHP%
IF /I %ENEMYSPELLTYPE% EQU REST (
	IF %RANPERC% LSS %ENEMYPERCENTHP% (
		IF %ENEMYSPELLCHOICE% EQU %CUREID% (
			SET /a BADCHOICE = 1
		) ELSE IF %ENEMYSPELLCHOICE% EQU %CURAID% (
			SET /a BADCHOICE = 1
		) ELSE IF %ENEMYSPELLCHOICE% EQU %CURAGAID% (
			SET /a BADCHOICE = 1
		)
	)
)
GOTO :EOF

:UNNECESSARYESUNAQUERY
IF /I %ENEMYSPELLTYPE% EQU REST (
	IF %ENEMYSPELLCHOICE% EQU %ESUNAID% (
		IF %ENEMYPOISON% EQU 0 (
			IF %ENEMYBLIND% EQU 0 (
				IF %ENEMYSLOW% EQU 0 (
					SET /a BADCHOICE = 1
				)
			)
		)
	)
)
GOTO :EOF
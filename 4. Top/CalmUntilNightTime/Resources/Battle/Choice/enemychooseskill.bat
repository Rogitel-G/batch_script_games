:ENEMYSKILLCHOOSE
CALL :SETSKILLCHANCETOT
CALL :CHOOSESKILL
CALL :INTELLIGENCEQUERY
CALL :SETSKILL
GOTO :EOF


:SETSKILLCHANCETOT
SET /a ENEMYSKILLCHANCETOT = 0
SET /a ENEMYSKILLCHANCE0CUM = 0
SET /a COUNT = 0
:SETSKILLCHANCETOTLOOP
SET /a OLDCOUNT = %COUNT%
SET /a COUNT = %COUNT% + 1
SET /a ENEMYSKILLCHANCE%COUNT%CUM = !ENEMYSKILLCHANCE%OLDCOUNT%CUM! + !ENEMYSKILL%COUNT%CHANCE!
IF %COUNT% LSS %ENEMYSKILLNUM% (
	GOTO :SETSKILLCHANCETOTLOOP
)
SET /a ENEMYSKILLCHANCETOT = !ENEMYSKILLCHANCE%COUNT%CUM!
GOTO :EOF

:CHOOSESKILL
SET /a ENEMYSKILLCHOICERAN=(%RANDOM% %% %ENEMYSKILLCHANCETOT%)+1
SET /a COUNT = 0
:CHOOSESKILLLOOP
SET /a COUNT = %COUNT% + 1
IF %ENEMYSKILLCHOICERAN% LEQ !ENEMYSKILLCHANCE%COUNT%CUM! (
	SET /a ENEMYSKILLCHOICE = !ENEMYSKILL%COUNT%!
	GOTO :EOF
) ELSE IF %COUNT% LSS %ENEMYSKILLNUM% (
	GOTO :CHOOSESKILLLOOP
) ELSE (
	GOTO :ENEMYSKILLCHOOSE
)

:INTELLIGENCEQUERY
CALL "%BATTLERESOURCEPATH%\ranresources.bat"
IF %ENEMYINT% GTR %RANPERC% (
	CALL "%BATTLECHOICERESOURCEPATH%\badskillchoicequery.bat"
)
GOTO :EOF

:SETSKILL
SET ENEMYSKILLNAME=!ENEMYSKILL%ENEMYSKILLCHOICE%NAME!
SET ENEMYSKILLNICKNAME=!ENEMYSKILL%ENEMYSKILLCHOICE%NICKNAME!
GOTO :EOF




SET ATTACKER=PLAYER
SET DEFENDER=ENEMY
SET /a PLAYEROLDACTION = %PLAYERACTION%
:CHOOSEACTION
SET /a BACK = 0
CALL "%BATTLEDISPLAYRESOURCEPATH%\battledisplay.bat"
IF %PLAYERCONFUSION% EQU 1 (
	SET /a PLAYERACTION = 1
	GOTO :EOF
)
IF %AUTOBATTLE% EQU 1 (
	CALL :AUTOBATTLECHOICE
)
IF %AUTOBATTLE% EQU 1 (
	IF %MEMORYAUTOBATTLE% EQU 1 (
		IF %PLAYERACTION% EQU 4 (
			CALL :ITEMCHECK
		) ELSE IF %PLAYERACTION% EQU 9 (
			SET /a AUTOBATTLE = 0
			GOTO :NOITEMS
		) ELSE IF %PLAYERACTION% EQU 5 (
			IF %SPECIALFIGHT% EQU 1 (
				SET /a AUTOBATTLE = 0
				GOTO :NOITEMS
			)
		)
		GOTO :EOF
	) ELSE (
		SET /a PLAYERACTION = 1
		GOTO :EOF
	)
)
:NOITEMS
ECHO What would you like to do?
ECHO.
IF %BANDITQUESTCOMPLETE% EQU 0 (
	ECHO. 1 - Attack
) ELSE (
	ECHO. 1 - Mug
)
ECHO. 2 - Magic
ECHO. 3 - Skill
ECHO. 4 - Item
ECHO. 5 - Catch
ECHO. 6 - Run
IF %PLAYERLIMITBREAKACTIVE% EQU 1 (
	ECHO. 9 - Limit Break
)
ECHO. 0 - Auto Battle
ECHO.
IF %SCAN% EQU 1 (
	ECHO. S - Toggle Scan
	IF %DETECT% EQU 0 (
		ECHO.
	)
)
IF %DETECT% EQU 1 (
	ECHO. D - Toggle Detect
	ECHO.
)
:CHOICE
SET /P PLAYERACTION=
ECHO.
:CHOICEEND
IF "%PLAYERACTION%" EQU "1" (
	ECHO.
) ELSE IF "%PLAYERACTION%" EQU "2" (
	IF %PLAYERSILENCE% EQU 0 (
		CALL "%BATTLECHOICERESOURCEPATH%\choosemagictype.bat"
	) ELSE (
		ECHO You can't use magic when you're silenced
		ECHO.
		CALL :WAITFORONE
		GOTO :CHOOSEACTION
	)
) ELSE IF "%PLAYERACTION%" EQU "3" (
	CALL "%BATTLECHOICERESOURCEPATH%\chooseskill.bat"
) ELSE IF "%PLAYERACTION%" EQU "4" (
	CALL "%BATTLECHOICERESOURCEPATH%\chooseitemtype.bat"
) ELSE IF "%PLAYERACTION%" EQU "5" (
	IF %SPECIALFIGHT% EQU 1 (
		ECHO. You cannot catch this enemy^^!
		ECHO.
		CALL :WAITFORONE
		GOTO :CHOOSEACTION
	)
	ECHO.
) ELSE IF "%PLAYERACTION%" EQU "6" (
	ECHO.
) ELSE IF "%PLAYERACTION%" EQU "9" (
	IF %PLAYERLIMITBREAKACTIVE% EQU 1 (
		ECHO.
	) ELSE (
		GOTO :CHOICE
	)
) ELSE IF "%PLAYERACTION%" EQU "0" (
	SET /a AUTOBATTLE = 1
	IF %MEMORYAUTOBATTLE% EQU 1 (
		SET /a PLAYERACTION = %PLAYEROLDACTION%
		GOTO :CHOOSEACTION
	) ELSE (
		SET /a PLAYERACTION = 1
		GOTO :CHOOSEACTION
	)
	GOTO :CHOOSEACTION
) ELSE IF /I "%PLAYERACTION%" EQU "S" (
	IF %SCAN% EQU 1 (
		CALL :TOGGLESCAN
	)
	GOTO :CHOOSEACTION
) ELSE IF /I "%PLAYERACTION%" EQU "D" (
	IF %DETECT% EQU 1 (
		CALL :TOGGLEDETECT
	)
	GOTO :CHOOSEACTION
) ELSE (
	GOTO :CHOICE
)
IF %BACK% EQU 1 (
	GOTO :CHOOSEACTION
)
GOTO :EOF

:TOGGLESCAN
SET /a DETECTACTIVE = 0
IF %SCANACTIVE% EQU 1 (
	SET /a SCANACTIVE = 0
) ELSE (
	SET /a SCANACTIVE = 1
)
GOTO :EOF

:TOGGLEDETECT
SET /a SCANACTIVE = 0
IF %DETECTACTIVE% EQU 1 (
	SET /a DETECTACTIVE = 0
) ELSE (
	SET /a DETECTACTIVE = 1
)
GOTO :EOF

:AUTOBATTLECHOICE
ECHO. Press 0 to stop Auto Battle
ECHO.
CHOICE /C:01 /N /T 2 /D 1
ECHO.
CLS
CALL "%BATTLEDISPLAYRESOURCEPATH%\battledisplay.bat"
SET /a AUTOBATTLE = %ERRORLEVEL% - 1
GOTO :EOF

:ITEMCHECK
IF !PLAYERITEM%ITEMCHOICE%NUM! LSS 1 (
	SET /a AUTOBATTLE = 0
	GOTO :NOITEMS
) ELSE IF %CUSTOMITEM% EQU 1 (
	SET /a AUTOBATTLE = 0
	GOTO :NOITEMS
)
GOTO :EOF

:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF

GOTO :START
::This was for testing
@ECHO OFF
SETLOCAL ENABLEDELAYEDEXPANSION
SET INITIALRESOURCEPATH=D:\Users\NDSisson\Desktop\Recent\Game\Resources\Setup\Initial
CALL "%INITIALRESOURCEPATH%\offspellstats.bat"
CALL "%INITIALRESOURCEPATH%\statspellstats.bat"
CALL "%INITIALRESOURCEPATH%\suppspellstats.bat"
CALL "%INITIALRESOURCEPATH%\itemlist.bat"
CALL "%INITIALRESOURCEPATH%\settabsvars.bat"
SET /a COUNT = 0
:TEMPLOOPSTART
SET /a COUNT=%COUNT%+1
SET /a PLAYERITEM%COUNT%NUM = %RANDOM% %% 30
IF %COUNT% LSS %ITEMTOTAL% (
	GOTO :TEMPLOOPSTART
)
SET /a TUTORIALFIGHT = 0
SET DIFFICULTY=HARD
SET /a PLAYERLCK = 20
SET /a PLAYERGIL = 10000
SET ENEMYNAME=Yorkshire Terrier


:START
IF %TUTORIALFIGHT% EQU 1 (
	GOTO :EOF
)
IF /I %DIFFICULTY% EQU EASY (
	SET /a LOSSFACTOR1 = 50
) ELSE IF /I %DIFFICULTY% EQU HARD (
	SET /a LOSSFACTOR1 = 75
)
SET /a LOSSTOT = 0
SET /a LOSSCOUNT = 0
SET /a COUNT = 0
SET /a TEMP = 100-%PLAYERLCK%
IF %TEMP% LSS 10 (
	SET /a TEMP = 10
)
:LOOPSTART
SET /a COUNT=%COUNT%+1
IF !PLAYERITEM%COUNT%NUM! GTR 0 (
	CALL :LOSEITEMQUERY
)
IF %LOSSCOUNT% GEQ 15 (
	GOTO :ITEMLOSSEND
) ELSE IF %COUNT% LSS %ITEMTOTAL% (
	GOTO :LOOPSTART
)
:ITEMLOSSEND
SET /a LOSSTOT = %LOSSCOUNT%
IF %LOSSTOT% EQU 0 (
	CALL :ITEMLOSSFALSEDISPLAY
)
CALL :GILLOSS
GOTO :EOF

:LOSEITEMQUERY
::These calculations should, on average, make you lose (100-luck)percent of 50percent of your items, (100-luck)percent of the time.
SET /a RANITEMLOSS=%RANDOM% %% 100
IF %RANITEMLOSS% LEQ %PLAYERLCK% (
	GOTO :EOF
)
SET /a LOSSFACTOR2 = %RANDOM% %% %TEMP%
SET /a LOSSFACTOR = %LOSSFACTOR1%*%LOSSFACTOR2%/100
SET /a LOSSNUM = (!PLAYERITEM%COUNT%NUM!*%LOSSFACTOR%)/100
IF %LOSSNUM% LEQ 0 (
	GOTO :EOF
)
SET /a LOSSCOUNT = %LOSSCOUNT% + 1
SET /a PLAYERITEM%COUNT%NUM = !PLAYERITEM%COUNT%NUM!-%LOSSNUM%
CALL :ITEMLOSSTRUEDISPLAY
GOTO :EOF

:GILLOSS
SET /a RANGILLOSS=%RANDOM% %% 100
IF %RANGILLOSS% LEQ %PLAYERLCK% (
	CALL :GILLOSSFALSEDISPLAY
	CALL :ENDDISPLAY
	GOTO :EOF
) ELSE IF %ENEMYID% LEQ %NIGHTMAREBOSSID% (
	CALL :GILLOSSFALSEDISPLAY
	CALL :ENDDISPLAY
	GOTO :EOF
)
SET /a LOSSFACTOR2 = %RANDOM% %% %TEMP%
SET /a LOSSFACTOR = %LOSSFACTOR1%*%LOSSFACTOR2%/100
SET /a LOSSNUM = (%PLAYERGIL%*%LOSSFACTOR%)/100
SET /a PLAYERGIL = %PLAYERGIL%-%LOSSNUM%
CALL :GILLOSSTRUEDISPLAY
CALL :ENDDISPLAY
GOTO :EOF






:ITEMLOSSFALSEDISPLAY
ECHO You were lucky enough to not lose any items^^!
GOTO :EOF

:ITEMLOSSTRUEDISPLAY
SET /a SPACENUM = 30 - !ITEM%COUNT%NAMELENGTH!
IF %LOSSNUM% GEQ 10 (
	SET /a SPACENUM = %SPACENUM% - 1
)
IF %LOSSNUM% GTR 1 (
	ECHO You have lost %LOSSNUM% !ITEM%COUNT%NAME!s !SPACES%SPACENUM%!You have !PLAYERITEM%COUNT%NUM! remaining.
) ELSE (
	ECHO You have lost %LOSSNUM% !ITEM%COUNT%NAME!  !SPACES%SPACENUM%!You have !PLAYERITEM%COUNT%NUM! remaining.
)
GOTO :EOF

:GILLOSSFALSEDISPLAY
ECHO.
ECHO You were lucky enough to not lose any Gil^^!
GOTO :EOF

:GILLOSSTRUEDISPLAY
ECHO.
ECHO You have lost %LOSSNUM% Gil. You have %PLAYERGIL% gil remaining.
GOTO :EOF

:ENDDISPLAY
ECHO.
pause
GOTO :EOF

@ECHO OFF
CALL :INITIALISE
SET /a FIRSTDISPLAY = 1
CALL :VARIABLES
CALL :DISPLAY
CALL :WAITFORTHREE
CALL "%BATTLEENDRESOURCEPATH%\distributerewards.bat"
SET /a FIRSTDISPLAY = 0
CALL :VARIABLES
CALL :DISPLAY
SET /a NEWITEMS = 0
SET TYPE=ITEM
CALL :NEWITEMS
SET TYPE=SPECIALITEM
CALL :NEWITEMS
pause
GOTO :EOF

:INITIALISE
SET /a MAXSPACENUM1 = 43
SET /a MAXSPACENUM2 = 52
SET /a MAXSPACENUM3 = %MAXSPACENUM2% - %MAXSPACENUM1%
SET PLAYERITEM1VAR=!SPACES%MAXSPACENUM2%!
SET PLAYERITEM2VAR=!SPACES%MAXSPACENUM2%!
SET PLAYERITEM3VAR=!SPACES%MAXSPACENUM2%!
SET PLAYERITEM4VAR=!SPACES%MAXSPACENUM2%!
SET PLAYEREQUIP1VAR=!SPACES%MAXSPACENUM2%!
SET PLAYEREQUIP2VAR=!SPACES%MAXSPACENUM2%!
SET PLAYEREQUIP3VAR=!SPACES%MAXSPACENUM2%!
SET PLAYEREQUIP4VAR=!SPACES%MAXSPACENUM2%!
SET /a PLAYERLEVELUPNUM = 0
GOTO :EOF

:VARIABLES
CALL :PLAYEREXPCALCULATIONS
CALL :PLAYERLVLCALCULATION
CALL :GIL
IF %TRAINBATTLE% EQU 1 (
	GOTO :EOF
)
SET TYPE=ITEM
SET TYPE2=ITEM
CALL :REWARDS
SET TYPE=SPECIALITEM
CALL :REWARDS
SET TYPE=WEAPON
SET TYPE2=EQUIP
CALL :REWARDS
SET TYPE=ARMOUR
CALL :REWARDS
SET TYPE=ACCESSORY
CALL :REWARDS
GOTO :EOF


:PLAYEREXPCALCULATIONS
IF %PLAYERDOUBLEEXP% EQU 1 (
	SET /a GAINEDEXP = %GAINEDEXP%*2
)
SET /a PLAYERMAXEXP = %REQEXP% - %OLDREQEXP%
SET /a PLAYEREXP = %EXP% - %OLDREQEXP%
CALL :TARGETSTATCALCULATIONS
GOTO :EOF

:TARGETSTATCALCULATIONS
SET PLAYEREXPDISPLAY=Exp:%PLAYEREXP%/%PLAYERMAXEXP%
SET /a LENGTH = 0
:TARGETSTATLOOPSTART
SET /a LENGTH += 1
SET SUBSTR=!PLAYEREXPDISPLAY:~%LENGTH%!
IF DEFINED SUBSTR (
	GOTO :TARGETSTATLOOPSTART
)
IF %FIRSTDISPLAY% EQU 1 (
	SET /a SPACENUM = %MAXSPACENUM1% - %LENGTH%
) ELSE (
	SET /a SPACENUM = %MAXSPACENUM2% - %LENGTH%
)
SET PLAYEREXPVAR=%PLAYEREXPDISPLAY%!SPACES%SPACENUM%!
IF %FIRSTDISPLAY% EQU 0 (
	GOTO :TARGETSTATLOOP2END
)
SET /a LENGTH = 0
:TARGETSTATLOOP2START
SET /a LENGTH += 1
SET SUBSTR=!GAINEDEXP:~%LENGTH%!
IF DEFINED SUBSTR (
	GOTO :TARGETSTATLOOP2START
)
SET /a LENGTH = %LENGTH% + 3
SET /a SPACENUM = %MAXSPACENUM3% - %LENGTH%
SET PLAYEREXPVAR=%PLAYEREXPVAR%(+%GAINEDEXP%)!SPACES%SPACENUM%!
:TARGETSTATLOOP2END
SET /a BARNUM = (((%PLAYEREXP%)*10)/%PLAYERMAXEXP%)
IF %BARNUM% GTR 10 (
	SET /a BARNUM = 10
)
IF %BARNUM% EQU 0 (
	IF %PLAYEREXP% NEQ 0 (
		SET /a BARNUM = 1
	)
)
SET /a SPACENUM = %MAXSPACENUM2%-10
CALL :BARVAR%BARNUM%
GOTO :EOF

:BARVAR10
SET PLAYEREXPBAR=ÛÛÛÛÛÛÛÛÛÛ!SPACES%SPACENUM%!
GOTO :EOF

:BARVAR9
SET PLAYEREXPBAR=ÛÛÛÛÛÛÛÛÛ°!SPACES%SPACENUM%!
GOTO :EOF

:BARVAR8
SET PLAYEREXPBAR=ÛÛÛÛÛÛÛÛ°°!SPACES%SPACENUM%!
GOTO :EOF

:BARVAR7
SET PLAYEREXPBAR=ÛÛÛÛÛÛÛ°°°!SPACES%SPACENUM%!
GOTO :EOF

:BARVAR6
SET PLAYEREXPBAR=ÛÛÛÛÛÛ°°°°!SPACES%SPACENUM%!
GOTO :EOF

:BARVAR5
SET PLAYEREXPBAR=ÛÛÛÛÛ°°°°°!SPACES%SPACENUM%!
GOTO :EOF

:BARVAR4
SET PLAYEREXPBAR=ÛÛÛÛ°°°°°°!SPACES%SPACENUM%!
GOTO :EOF

:BARVAR3
SET PLAYEREXPBAR=ÛÛÛ°°°°°°°!SPACES%SPACENUM%!
GOTO :EOF

:BARVAR2
SET PLAYEREXPBAR=ÛÛ°°°°°°°°!SPACES%SPACENUM%!
GOTO :EOF

:BARVAR1
SET PLAYEREXPBAR=Û°°°°°°°°°!SPACES%SPACENUM%!
GOTO :EOF

:BARVAR0
SET PLAYEREXPBAR=°°°°°°°°°°!SPACES%SPACENUM%!
GOTO :EOF

:PLAYERLVLCALCULATION
IF %PLAYERLEVELUPNUM% EQU 0 (
	CALL :LVLCALCULATION1
) ELSE (
	CALL :LVLCALCULATION2
)
GOTO :EOF

:LVLCALCULATION1
IF %PLAYERLVL% LSS 10 (
	SET /a SPACENUM = %MAXSPACENUM2% - 7
) ELSE IF %PLAYERLVL% LSS 100 (
	SET /a SPACENUM = %MAXSPACENUM2% - 8
) ELSE (
	SET /a SPACENUM = %MAXSPACENUM2% - 9
)
SET PLAYERLVLVAR=Level %PLAYERLVL%!SPACES%SPACENUM%!
GOTO :EOF

:LVLCALCULATION2
IF %PLAYERLVL% LSS 10 (
	SET /a SPACENUM = %MAXSPACENUM1% - 7
) ELSE IF %PLAYERLVL% LSS 100 (
	SET /a SPACENUM = %MAXSPACENUM1% - 8
) ELSE (
	SET /a SPACENUM = %MAXSPACENUM1% - 9
)
SET PLAYERLVLVAR=Level %PLAYERLVL%!SPACES%SPACENUM%!
IF %PLAYERLEVELUPNUM% LSS 10 (
	SET /a SPACENUM = %MAXSPACENUM3% - 4
) ELSE (
	SET /a SPACENUM = %MAXSPACENUM3% - 5
)
SET PLAYERLVLVAR=%PLAYERLVLVAR%(+%PLAYERLEVELUPNUM%)!SPACES%SPACENUM%!
GOTO :EOF

:GIL
SET PLAYERGILDISPLAY=Gil:%PLAYERGIL%
SET /a LENGTH = 0
:GILLOOPSTART
SET /a LENGTH += 1
SET SUBSTR=!PLAYERGILDISPLAY:~%LENGTH%!
IF DEFINED SUBSTR (
	GOTO :GILLOOPSTART
)
IF %FIRSTDISPLAY% EQU 1 (
	SET /a SPACENUM = %MAXSPACENUM1% - %LENGTH%
) ELSE (
	SET /a SPACENUM = %MAXSPACENUM2% - %LENGTH%
)
SET PLAYERGILVAR=%PLAYERGILDISPLAY%!SPACES%SPACENUM%!
IF %FIRSTDISPLAY% EQU 0 (
	GOTO :EOF
)
SET /a LENGTH = 0
:GILLOOP2START
SET /a LENGTH += 1
SET SUBSTR=!GAINEDGIL:~%LENGTH%!
IF DEFINED SUBSTR (
	GOTO :GILLOOP2START
)
SET /a LENGTH = %LENGTH% + 3
SET /a SPACENUM = %MAXSPACENUM3% - %LENGTH%
SET PLAYERGILVAR=%PLAYERGILVAR%(+%GAINEDGIL%)!SPACES%SPACENUM%!
GOTO :EOF

:REWARDS
IF !%TYPE%REWARDNUM! EQU 0 (
	GOTO :EOF
)
SET /a COUNT = 0
:REWARDSLOOP
SET /a COUNT = %COUNT% + 1
IF !PLAYER%TYPE%REWARD%COUNT%NUM! EQU 0 (
	GOTO :REWARDSLOOP3END
)
SET /a TEMP = !%TYPE%REWARD%COUNT%!
SET %TYPE2%%COUNT%DISPLAY=!PLAYER%TYPE%%TEMP%NUM! !%TYPE%%TEMP%NAME!
SET /a LENGTH = 0
:REWARDSLOOP2START
SET /a LENGTH += 1
SET SUBSTR=!%TYPE2%%COUNT%DISPLAY:~%LENGTH%!
IF DEFINED SUBSTR (
	GOTO :REWARDSLOOP2START
)
IF %FIRSTDISPLAY% EQU 1 (
	SET /a SPACENUM = %MAXSPACENUM1% - %LENGTH%
) ELSE (
	SET /a SPACENUM = %MAXSPACENUM2% - %LENGTH%
)
SET PLAYER%TYPE2%%COUNT%VAR=!%TYPE2%%COUNT%DISPLAY!!SPACES%SPACENUM%!
IF %FIRSTDISPLAY% EQU 0 (
	GOTO :REWARDSLOOP3END
)
IF !PLAYER%TYPE%REWARD%COUNT%NUM! GEQ 10 (
	SET /a LENGTH = 5
) ELSE (
	SET /a LENGTH = 4
)
SET /a SPACENUM = %MAXSPACENUM3% - %LENGTH%
SET PLAYER%TYPE2%%COUNT%VAR=!PLAYER%TYPE2%%COUNT%VAR!(+!PLAYER%TYPE%REWARD%COUNT%NUM!)!SPACES%SPACENUM%!
:REWARDSLOOP3END
IF %COUNT% LSS !%TYPE%REWARDNUM! (
	GOTO :REWARDSLOOP
)
GOTO :EOF


:DISPLAY
CLS
ECHO.
ECHO. ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
ECHO. ³                                                      ³
ECHO. ³ %PLAYERLVLVAR% ³
ECHO. ³ %PLAYEREXPVAR% ³
ECHO. ³ %PLAYEREXPBAR% ³
ECHO. ³                                                      ³
ECHO. ³ %PLAYERGILVAR% ³
ECHO. ³                                                      ³
ECHO. ³ Items:                                               ³
ECHO. ³ %PLAYERITEM1VAR% ³
ECHO. ³ %PLAYERITEM2VAR% ³
ECHO. ³ %PLAYERITEM3VAR% ³
ECHO. ³ %PLAYERITEM4VAR% ³
ECHO. ³                                                      ³
ECHO. ³ Equipment:                                           ³
ECHO. ³ %PLAYEREQUIP1VAR% ³
ECHO. ³ %PLAYEREQUIP2VAR% ³
ECHO. ³ %PLAYEREQUIP3VAR% ³
ECHO. ³ %PLAYEREQUIP4VAR% ³
ECHO. ³                                                      ³
ECHO. ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ECHO.
GOTO :EOF

:NEWITEMS
SET /a COUNT = 0
:NEWITEMSLOOP
SET /a COUNT = %COUNT% + 1
IF !%TYPE%REWARDNUM! EQU 0 (
	GOTO :EOF
)
SET /a TEMP = !%TYPE%REWARD%COUNT%!
::If the number gained equals the total number
IF !PLAYER%TYPE%%TEMP%NUM! EQU !PLAYER%TYPE%REWARD%COUNT%NUM! (
	IF !PLAYER%TYPE%REWARD%COUNT%NUM! GTR 0 (
		IF %NEWITEMS% EQU 0 (
			CALL :NEWITEMSTEXT
		)
		ECHO. !%TYPE%%TEMP%NAME! - !%TYPE%%TEMP%DESCRIPTION!
	)
)
IF %COUNT% LSS !%TYPE%REWARDNUM! (
	GOTO :NEWITEMSLOOP
)
IF %NEWITEMS% EQU 1 (
	ECHO.
)
GOTO :EOF

:NEWITEMSTEXT
ECHO. New Items^^!
SET /a NEWITEMS = 1
GOTO :EOF

:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF

IF NOT DEFINED ENEMY%ENEMYID%LOADED (
	SET /a ENEMY%ENEMYID%LOADED = 1
	CALL "%MISCRESOURCEPATH%\loadenemy.bat"
)
CALL "%BATTLESTARTRESOURCEPATH%\initialiseplayer.bat"
CALL "%BATTLESTARTRESOURCEPATH%\initialiseenemy.bat"
CALL "%BATTLEDISPLAYRESOURCEPATH%\setupscandisplay.bat"
CALL "%BATTLEDISPLAYRESOURCEPATH%\setupdetectdisplay.bat"
CALL "%BATTLESTARTRESOURCEPATH%\stealchances.bat"
CALL "%BATTLEDISPLAYRESOURCEPATH%\animationsetup.bat"
CLS
CALL "%BATTLESTARTRESOURCEPATH%\allyappearquery.bat"
CALL :ALLYAPPEAR
IF %ENDBATTLE% EQU 1 (
	GOTO :EOF
)
CALL :BATTLEMUSIC
CALL "%BATTLEDISPLAYRESOURCEPATH%\battledisplaycalculations.bat"
:ROUNDSTART
CALL :STATUSCOUNTS
CALL :MODIFYSTATS
CALL "%BATTLEDISPLAYRESOURCEPATH%\battledisplaycalculations.bat"
CALL :TRANSFORMQUERY
CALL :INITIALISEROUND
CALL :CHOOSEACTION
CALL :ENEMYCHOOSEACTION
CALL "%BATTLEDISPLAYRESOURCEPATH%\battledisplaycalculations.bat"
CALL :FIRSTACTION
CALL :LIMITBREAKQUERY
CALL :BATTLEENDQUERY
IF %ENDBATTLE% EQU 1 (
	GOTO :EOF
)
CALL "%BATTLEDISPLAYRESOURCEPATH%\battledisplaycalculations.bat"
CALL :SECONDACTION
CALL :LIMITBREAKQUERY
CALL :BATTLEENDQUERY
IF %ENDBATTLE% EQU 1 (
	GOTO :EOF
)
CALL "%BATTLEDISPLAYRESOURCEPATH%\battledisplaycalculations.bat"
GOTO :ROUNDSTART


:BATTLEMUSIC
SET TEMPPATH=%MUSICPATH%\Battle\!ENEMY%ENEMYID%MUSIC!Battle
SET /a STOP = 1
SET /a LOOP = 1
CALL "%MUSICPATH%\startmusic.bat"
taskkill /FI "WINDOWTITLE eq Battle" > nul
GOTO :EOF


:ALLYAPPEAR
IF %PACMANAPPEAR% EQU 1 (
	CALL "%BATTLESTARTRESOURCEPATH%\pacmanappear.bat"
) ELSE IF %LEEROYAPPEAR% EQU 1 (
	CALL "%BATTLESTARTRESOURCEPATH%\leeroyappear.bat"
)
GOTO :EOF

:ENERGYQUERY
IF %PLAYERENERGY% LSS 5 (
	SET /a PLAYERSUPERSAIYAN = 0
	ECHO.
	ECHO. You do not have enough energy to maintain your transformation
	ECHO.
	ECHO. You are no longer a Super Saiyan
)
GOTO :EOF

:MUSICRESTARTQUERY
tasklist /FI "WINDOWTITLE eq MUSIC" 2>NUL | find /I /N "cmd.exe">NUL
IF "%ERRORLEVEL%" EQU "0" (
	GOTO :EOF
)
SET TEMPPATH=%MUSICPATH%\Battle\!ENEMY%ENEMYID%MUSIC!Battle
SET /a STOP = 0
SET /a LOOP = 1
CALL "%MUSICPATH%\startmusic.bat"
GOTO :EOF

:STATUSCOUNTS
CALL "%BATTLESTATUSRESOURCEPATH%\statusturncounts.bat"
GOTO :EOF

:MODIFYSTATS
CALL "%BATTLETURNSTARTRESOURCEPATH%\enemymodifiedstats.bat"
CALL "%BATTLETURNSTARTRESOURCEPATH%\playermodifiedstats.bat"
GOTO :EOF

:TRANSFORMQUERY
SET /a PLAYERTRANSFORMAVAILABLE = 0
IF %PLAYERMED% EQU 1 (
	IF %PLAYERFOC% EQU 1 (
		SET /a PLAYERTRANSFORMAVAILABLE = 1
	)
)
GOTO :EOF

:INITIALISEROUND
SET /a BATTLEROUNDNUM = %BATTLEROUNDNUM%+1
GOTO :EOF

:CHOOSEACTION
IF %PLAYERSUPERSAIYAN% EQU 1 (
	CALL "%BATTLECHOICERESOURCEPATH%\choosesuperaction.bat"
) ELSE (
	CALL "%BATTLECHOICERESOURCEPATH%\chooseaction.bat"
)
GOTO :EOF

:ENEMYCHOOSEACTION
CALL "%BATTLECHOICERESOURCEPATH%\enemychooseaction.bat"
GOTO :EOF

:FIRSTACTION
CALL "%BATTLEDISPLAYRESOURCEPATH%\battledisplay.bat"
CALL "%BATTLETURNSTARTRESOURCEPATH%\firstaction.bat"
IF %ACTFIRST% EQU 1 (
	ECHO You acted first!
	ECHO.
	CALL :WAITFORONE
	IF %PLAYERSUPERSAIYAN% EQU 1 (
		CALL "%BATTLEACTIONRESOURCEPATH%\playersuperturn.bat"
	) ELSE (
		CALL "%BATTLEACTIONRESOURCEPATH%\playerturn.bat"
	)
) ELSE IF %ACTFIRST% EQU 0 (
	ECHO %ENEMYNAME% acted first!
	ECHO.
	CALL :WAITFORONE
	CALL "%BATTLEACTIONRESOURCEPATH%\enemyturn.bat"	
)
GOTO :EOF

:SECONDACTION
REM CALL "%BATTLEDISPLAYRESOURCEPATH%\battledisplay.bat"
IF %ACTFIRST% EQU 1 (
	CALL "%BATTLEACTIONRESOURCEPATH%\enemyturn.bat"
) ELSE IF %ACTFIRST% EQU 0 (
	IF %PLAYERSUPERSAIYAN% EQU 1 (
		CALL "%BATTLEACTIONRESOURCEPATH%\playersuperturn.bat"
	) ELSE (
		CALL "%BATTLEACTIONRESOURCEPATH%\playerturn.bat"
	)
)
GOTO :EOF


:BATTLEENDQUERY
IF %WINBATTLE% EQU 1 (
	SET /a ENDBATTLE = 1
	CALL :WAITFORONE
	CALL "%BATTLEENDRESOURCEPATH%\winbattle.bat"
) ELSE IF %LOSEBATTLE% EQU 1 (
	IF %PLAYERAUTOLIFE% EQU 1 (
		CALL :AUTOLIFE
		GOTO :EOF
	)
	SET /a ENDBATTLE = 1
	CALL :WAITFORONE
	CALL "%BATTLEENDRESOURCEPATH%\losebattle.bat"
) ELSE IF %BATTLERUN% EQU 1 (
	SET /a ENDBATTLE = 1
) ELSE IF %BATTLECAUGHT% EQU 1 (
	SET /a ENDBATTLE = 1
) ELSE IF %ENEMYRUN% EQU 1 (
	SET /a ENDBATTLE = 1
)
GOTO :EOF

:AUTOLIFE
SET /a LOSEBATTLE = 0
SET /a PLAYERAUTOLIFE = 0
SET /a PLAYERHP = %PLAYERMAXHP%
IF /I %DIFFICULTY% EQU EASY (
	SET /a PLAYERMP = %PLAYERMAXMP%
)
SET TEMPPATH=%MUSICPATH%\Battle\Spells
IF EXIST "%TEMPPATH%\Autolife.mp3" (
	SET FILE=Autolife.mp3
	CALL "%MUSICPATH%\soundeffect.bat"
)
ECHO You have been revived by Auto Life^^!
ECHO.
CALL :WAITFORTWO
GOTO :EOF

:LIMITBREAKQUERY
IF %PLAYERLIMITBREAK% GEQ %PLAYERMAXLIMITBREAK% (
	SET /a PLAYERLIMITBREAKACTIVE = 1
)
GOTO :EOF

:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF




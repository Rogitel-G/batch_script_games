CALL "%DYNAMICRESOURCEPATH%\dynamicequipmentstatsshop.bat"
SET /a SELECTED = 1
:START
SET /a NOTENOUGHGIL = 0
CLS
ECHO.
ECHO !SHOPKEEPER%SHOPNUM%NAME!: Which %TYPE% would you like?
ECHO.
ECHO You currently have %PLAYERGIL% Gil
ECHO.
ECHO   Name                              Cost         Held
CALL "%MISCRESOURCEPATH%\menu2.bat"
IF %VARSELECTED% EQU 1 (
	CALL :GILQUERY
)
IF %VARSELECTED% EQU 1 (
	IF %NOTENOUGHGIL% EQU 0 (
		CALL :CONFIRMATION
	)
)
IF %BACK% EQU 1 (
	GOTO :EOF
)
GOTO :START

:GILQUERY
SET /a COST = !TEXT%SELECTED%NAME2!
IF %PLAYERGIL% GEQ %COST% (
	GOTO :EOF
)
SET /a NOTENOUGHGIL = 1
CLS
ECHO.
ECHO !SHOPKEEPER%SHOPNUM%NAME!: You don't have enough gil for that^^!
ECHO.
CALL :WAITFORTWO
GOTO :EOF


:CONFIRMATION
CALL :SETSTATICID
CLS
ECHO.
ECHO !SHOPKEEPER%SHOPNUM%NAME!: Are you sure you'd like to buy a !%TYPE%%COUNT%NAME!
ECHO.
ECHO. 1 - Yes
ECHO. 0 - No
ECHO.
SET /P CONFIRMATION=
ECHO.
IF "%CONFIRMATION%" EQU "1" (
	GOTO :PURCHASE
) ELSE IF "%CONFIRMATION%" EQU "0" (
	GOTO :EOF
)
GOTO :EOF

:SETSTATICID
SET /a COUNT = 0
:LOOPSTART
SET /a COUNT = %COUNT% + 1
IF "!%TYPE%%COUNT%NAME!" EQU "!TEXT%SELECTED%NAME1!" (
	SET /a CHOICE = %COUNT%
	GOTO :EOF
)
GOTO :LOOPSTART

:PURCHASE
SET /a PLAYERGIL = %PLAYERGIL% - !%TYPE%%CHOICE%COST!
SET /a SHOP%SHOPNUM%SPEND = !SHOP%SHOPNUM%SPEND! + !%TYPE%%CHOICE%COST!
SET /a PLAYER%TYPE%%CHOICE%NUM = !PLAYER%TYPE%%CHOICE%NUM! + 1
CLS
ECHO.
ECHO Would you like to equip this %TYPE% now?
ECHO.
ECHO. 1 - Yes
ECHO. 0 - No
ECHO.
SET /P EQUIP=
ECHO.
IF "%EQUIP%" EQU "1" (
	CALL :UNEQUIP%TYPE%
	CALL :EQUIP%TYPE%
) ELSE IF "%EQUIP%" EQU "0" (
	GOTO :EOF
) ELSE (
	GOTO :PURCHASE
)
CALL "%DYNAMICRESOURCEPATH%\dynamicequipmentstatsshop.bat"
GOTO :EOF

:UNEQUIPWEAPON
IF !WEAPON%PLAYERWEAPONID%SKILLTOT! EQU 0 (
	GOTO :UNEQUIPWEAPONLOOPEND
)
SET /a COUNT = 0
:UNEQUIPWEAPONLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !WEAPON%PLAYERWEAPONID%SKILL%COUNT%!
IF !PLAYERSKILL%TEMP%EARNED! EQU 0 (
	SET /a PLAYERSKILL%TEMP%ENABLED = 0
)
IF %COUNT% LSS !WEAPON%PLAYERWEAPONID%SKILLTOT! (
	GOTO :UNEQUIPWEAPONLOOP
)
:UNEQUIPWEAPONLOOPEND
SET PLAYERWEAPON=NONE
SET /a PLAYERWEAPONID = 0
SET /a PLAYEREQUIPSTR = 0
SET /a PLAYEREQUIPMAG = 0
SET /a PLAYEREQUIPACC = 0
GOTO :EOF

:EQUIPWEAPON
SET PLAYERWEAPON=!WEAPON%CHOICE%NAME!
SET /a PLAYERWEAPONID = %CHOICE%
SET /a PLAYEREQUIPSTR=!WEAPON%CHOICE%STR!
SET /a PLAYEREQUIPMAG=!WEAPON%CHOICE%MAG!
SET /a PLAYEREQUIPACC=!WEAPON%CHOICE%ACC!
IF !WEAPON%PLAYERWEAPONID%SKILLTOT! EQU 0 (
	GOTO :EQUIPWEAPONLOOPEND
)
SET /a COUNT = 0
:EQUIPWEAPONLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !WEAPON%PLAYERWEAPONID%SKILL%COUNT%!
SET /a PLAYERSKILL%TEMP%ENABLED = 1
IF %COUNT% LSS !WEAPON%PLAYERWEAPONID%SKILLTOT! (
	GOTO :EQUIPWEAPONLOOP
)
:EQUIPWEAPONLOOPEND
ECHO. %PLAYERWEAPON% Equipped^^!
ECHO. 
CALL :WAITFORTWO
GOTO :EOF

:UNEQUIPARMOUR
SET PLAYERARMOUR=NONE
SET /a PLAYERARMOURID=0
SET /a PLAYEREQUIPEND=0
SET /a PLAYEREQUIPSPR=0
SET /a PLAYEREQUIPEVA=0
SET /a PLAYEREQUIPSPD=0
SET /a PLAYERWEAKNESSNUM=0
SET /a PLAYERSTRENGTHNUM=0
SET /a PLAYERELEMENTIMMUNENUM=0
SET /a PLAYERABSORBNUM=0
SET /a PLAYERSTATUSIMMUNENUM=0
GOTO :EOF

:EQUIPARMOUR
SET PLAYERARMOUR=!ARMOUR%CHOICE%NAME!
SET /a PLAYERARMOURID=%CHOICE%
SET /a PLAYEREQUIPEND=!ARMOUR%CHOICE%END!
SET /a PLAYEREQUIPSPR=!ARMOUR%CHOICE%SPR!
SET /a PLAYEREQUIPEVA=!ARMOUR%CHOICE%EVA!
SET /a PLAYEREQUIPSPD=!ARMOUR%CHOICE%SPD!
SET /a PLAYERWEAKNESSNUM=!ARMOUR%CHOICE%WEAKNESSNUM!
SET /a PLAYERSTRENGTHNUM=!ARMOUR%CHOICE%STRENGTHNUM!
SET /a PLAYERELEMENTIMMUNENUM=!ARMOUR%CHOICE%ELEMENTIMMUNENUM!
SET /a PLAYERABSORBNUM=!ARMOUR%CHOICE%ABSORBNUM!
SET /a PLAYERSTATUSIMMUNENUM=!ARMOUR%CHOICE%STATUSIMMUNENUM!
CALL :SETRESISTANCES
ECHO. %PLAYERARMOUR% Equipped^^!
ECHO. 
CALL :WAITFORTWO
GOTO :EOF

:SETRESISTANCES
SET /a COUNT = 0
IF %PLAYERWEAKNESSNUM% EQU 0 (
	GOTO :STRENGTHS3
)
:WEAKNESSLOOP3
SET /a COUNT = %COUNT% + 1
SET PLAYERWEAKNESS%COUNT%=!ARMOUR%CHOICE%WEAKNESS%COUNT%!
IF %COUNT% LSS %PLAYERWEAKNESSNUM% (
	GOTO :WEAKNESSLOOP3
)
:STRENGTHS3
SET /a COUNT = 0
IF %PLAYERSTRENGTHNUM% EQU 0 (
	GOTO :IMMUNES3
)
:STRENGTHLOOP3
SET /a COUNT = %COUNT% + 1
SET PLAYERSTRENGTH%COUNT%=!ARMOUR%CHOICE%STRENGTH%COUNT%!
IF %COUNT% LSS %PLAYERSTRENGTHNUM% (
	GOTO :STRENGHLOOP3
)
:IMMUNES3
SET /a COUNT = 0
IF %PLAYERELEMENTIMMUNENUM% EQU 0 (
	GOTO :ABSORBS3
)
:IMMUNELOOP3
SET /a COUNT = %COUNT% + 1
SET PLAYERELEMENTIMMUNE%COUNT%=!ARMOUR%CHOICE%ELEMENTIMMUNE%COUNT%!
IF %COUNT% LSS %PLAYERELEMENTIMMUNENUM% (
	GOTO :IMMUNELOOP3
)
:ABSORBS3
SET /a COUNT = 0
IF %PLAYERABSORBNUM% EQU 0 (
	GOTO :STATUSIMMUNES3
)
:ABSORBLOOP3
SET /a COUNT = %COUNT% + 1
SET PLAYERABSORB%COUNT%=!ARMOUR%CHOICE%ABSORB%COUNT%!
IF %COUNT% LSS %PLAYERABSORBNUM% (
	GOTO :ABSORBLOOP3
)
:STATUSIMMUNES3
SET /a COUNT = 0
IF %PLAYERSTATUSIMMUNENUM% EQU 0 (
	GOTO :EOF
)
:STATUSIMMUNELOOP3
SET /a COUNT = %COUNT% + 1
SET PLAYERSTATUSIMMUNE%COUNT%=!ARMOUR%CHOICE%STATUSIMMUNE%COUNT%!
IF %COUNT% LSS %PLAYERSTATUSIMMUNENUM% (
	GOTO :STATUSIMMUNELOOP3
)
GOTO :EOF


:UNEQUIPACCESSORY
IF !ACCESSORY%PLAYERACCESSORYID%SPECIALTOT! EQU 0 (
	GOTO :UNEQUIPACCESSORYEND
)
SET /a COUNT = 0
:UNEQUIPACCESSORYLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !ACCESSORY%PLAYERACCESSORYID%SPECIAL%COUNT%!
SET /a PLAYER!SPECIAL%TEMP%NICKNAME! = 0
IF %COUNT% LSS !ACCESSORY%PLAYERACCESSORYID%SPECIALTOT! (
	GOTO :UNEQUIPACCESSORYLOOP
)
:UNEQUIPACCESSORYEND
SET PLAYERACCESSORY=NONE
SET /a PLAYERACCESSORYID = 0
SET /a PLAYEREQUIPMAXHP = 0
SET /a PLAYEREQUIPMAXMP = 0
SET /a PLAYEREQUIPLCK = 0
GOTO :EOF

:EQUIPACCESSORY
SET PLAYERACCESSORY=!ACCESSORY%CHOICE%NAME!
SET /a PLAYERACCESSORYID = %CHOICE%
SET /a PLAYEREQUIPMAXHP=!ACCESSORY%CHOICE%MAXHP!
SET /a PLAYEREQUIPMAXMP=!ACCESSORY%CHOICE%MAXMP!
SET /a PLAYEREQUIPLCK=!ACCESSORY%CHOICE%LCK!
SET /a PLAYERMAXHP = %PLAYERBASEMAXHP% + %PLAYEREQUIPMAXHP%
SET /a PLAYERMAXMP = %PLAYERBASEMAXMP% + %PLAYEREQUIPMAXMP%
IF %PLAYERHP% GTR %PLAYERMAXHP% (
	SET /a PLAYERHP = %PLAYERMAXHP%
)
IF %PLAYERMP% GTR %PLAYERMAXMP% (
	SET /a PLAYERMP = %PLAYERMAXMP%
)
IF !ACCESSORY%PLAYERACCESSORYID%SPECIALTOT! EQU 0 (
	GOTO :EQUIPACCESSORYLOOPEND
)
SET /a COUNT = 0
:EQUIPACCESSORYLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !ACCESSORY%PLAYERACCESSORYID%SPECIAL%COUNT%!
SET /a PLAYER!SPECIAL%TEMP%NICKNAME! = 1
IF %COUNT% LSS !ACCESSORY%PLAYERACCESSORYID%SPECIALTOT! (
	GOTO :EQUIPACCESSORYLOOP
)
:EQUIPACCESSORYLOOPEND
ECHO. %PLAYERACCESSORY% Equipped^^!
ECHO. 
CALL :WAITFORTWO
GOTO :EOF


:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF
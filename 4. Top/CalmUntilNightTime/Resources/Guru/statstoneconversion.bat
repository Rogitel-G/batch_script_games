SET /a CONVERSIONGILCOST = 1000
:START
CALL "%DYNAMICRESOURCEPATH%\dynamicgurustatstones.bat"
CALL "%GURURESOURCEPATH%\gurudisplay.bat"
ECHO The Guru holds his hand out for stat stones...
ECHO.
SET /a COUNT = 0
:LOOP
SET /a COUNT = %COUNT% + 1
SET /a SPACENUM = 34 - !DYNAMICSPECIALITEM%COUNT%NAMELENGTH!
IF %COUNT% GEQ 10 (
	SET /a SPACENUM = %SPACENUM% - 1
)
ECHO. %COUNT% - !DYNAMICSPECIALITEM%COUNT%NAME!!SPACES%SPACENUM%!!DYNAMICSPECIALITEM%COUNT%NUM! Held
IF %COUNT% LSS %OPTIONTOT% (
	GOTO :LOOP
)
ECHO. 0 - Back
ECHO.
:CHOICE
SET /P DYNAMICCHOICE=
IF "%DYNAMICCHOICE%" EQU "" (
	GOTO :CHOICE
) ELSE IF "%DYNAMICCHOICE%" EQU "0" (
	GOTO :EOF
)
IF %DYNAMICCHOICE% LSS 1 (
	GOTO :CHOICE
) ELSE IF %DYNAMICCHOICE% GTR %OPTIONTOT% (
	GOTO :CHOICE
)
CALL :SETSTATICID
:CONVERSIONCHOICESTART
CALL :CONVERSIONCHOICE
IF %CONVERSIONCHOICE% EQU 0 (
	GOTO :START
)
CALL :CONVERSIONAMOUNT
IF %CONVERSIONAMOUNT% EQU 0 (
	GOTO :CONVERSIONCHOICESTART
)
CALL :CONVERT
GOTO :START


:SETSTATICID
SET /a COUNT = 0
:LOOPSTART
SET /a COUNT = %COUNT% + 1
IF /I "!SPECIALITEM%COUNT%NAME!" EQU "!DYNAMICSPECIALITEM%DYNAMICCHOICE%NAME!" (
	SET /a CHOICE = %COUNT%
	GOTO :EOF
)
GOTO :LOOPSTART

:CONVERSIONCHOICE
CALL "%GURURESOURCEPATH%\gurudisplay.bat"
ECHO What stone would you like to create from your !SPECIALITEM%CHOICE%NAME!s?
ECHO.
SET /a COUNT = 0
:LOOP2
SET /a COUNT = %COUNT% + 1
SET /a SPACENUM = 34 - !SPECIALITEM%COUNT%NAMELENGTH!
IF %COUNT% GEQ 10 (
	SET /a SPACENUM = %SPACENUM% - 1
)
ECHO. %COUNT% - !SPECIALITEM%COUNT%NAME!!SPACES%SPACENUM%!!PLAYERSPECIALITEM%COUNT%NUM! Held
IF %COUNT% LSS %STATSTONEEND% (
	GOTO :LOOP2
)
ECHO. 0 - Back
ECHO.
SET /P CONVERSIONCHOICE=
IF "%CONVERSIONCHOICE%" EQU "" (
	GOTO :CONVERSIONCHOICE
) ELSE IF "%CONVERSIONCHOICE%" EQU "0" (
	GOTO :EOF
)
IF %CONVERSIONCHOICE% GTR %STATSTONEEND% (
	GOTO :CONVERSIONCHOICE
) ELSE IF %CONVERSIONCHOICE% LSS 0 (
	GOTO :CONVERSIONCHOICE
)
GOTO :EOF

:CONVERSIONAMOUNT
CALL "%GURURESOURCEPATH%\gurudisplay.bat"
ECHO You've chosen to convert !SPECIALITEM%CHOICE%NAME!s into !SPECIALITEM%CONVERSIONCHOICE%NAME!s
ECHO.
ECHO It costs %CONVERSIONGILCOST% gil per stone converted. You currently have %PLAYERGIL% gil
ECHO You have !PLAYERSPECIALITEM%CHOICE%NUM! !SPECIALITEM%CHOICE%NAME!(s)
ECHO You have !PLAYERSPECIALITEM%CONVERSIONCHOICE%NUM! !SPECIALITEM%CONVERSIONCHOICE%NAME!(s)
ECHO.
ECHO How many would you like to convert?
ECHO.
SET /P CONVERSIONAMOUNT=
IF "%CONVERSIONAMOUNT%" EQU "" (
	GOTO :CONVERSIONAMOUNT
) ELSE IF "%CONVERSIONAMOUNT%" EQU "0" (
	GOTO :EOF
)
IF %CONVERSIONAMOUNT% GTR !PLAYERSPECIALITEM%CHOICE%NUM! (
	GOTO :CONVERSIONAMOUNT
) ELSE IF %CONVERSIONAMOUNT% LSS 0 (
	GOTO :CONVERSIONAMOUNT
)
SET /a GILCOST = %CONVERSIONGILCOST%*%CONVERSIONAMOUNT%
IF %GILCOST% GTR %PLAYERGIL% (
	CALL "%GURURESOURCEPATH%\gurudisplay.bat"
	ECHO You do not have enough gil for this.
	ECHO.
	CALL :WAITFORTHREE
	GOTO :CONVERSIONAMOUNT
)
GOTO :EOF

:CONVERT
SET /a PLAYERSPECIALITEM%CONVERSIONCHOICE%NUM = !PLAYERSPECIALITEM%CONVERSIONCHOICE%NUM! + %CONVERSIONAMOUNT%
SET /a PLAYERSPECIALITEM%CHOICE%NUM = !PLAYERSPECIALITEM%CHOICE%NUM! - %CONVERSIONAMOUNT%
SET /a PLAYERGIL = %PLAYERGIL% - %GILCOST%
CALL "%GURURESOURCEPATH%\gurudisplay.bat"
IF %CONVERSIONAMOUNT% EQU 1 (
	ECHO You hand The Guru a !SPECIALITEM%CHOICE%NAME! and he immediately swallows it
) ELSE (
	ECHO You hand The Guru %CONVERSIONAMOUNT% !SPECIALITEM%CHOICE%NAME!s and he immediately swallows them one after another
)
CALL :WAITFORFOUR
ECHO.
ECHO He then stands up and walks into to a mysterious room marked "WC"
CALL :WAITFORFOUR
CALL "%GURURESOURCEPATH%\gurudisplay.bat"
::CALL "%GURURESOURCEPATH%\gurudisplay2.bat"
ECHO You hear splashing, this must be part of the ritual...
::TODO Splashing sound. Looping depending on the number of stones converted.
SET TEMPPATH=%MUSICPATH%\Splash
SET FILE=Splash.mp3
SET /a COUNT = 0
:SPLASHLOOP
SET /a COUNT = %COUNT% + 1
IF EXIST "%TEMPPATH%\%FILE%" (
	CALL "%MUSICPATH%\soundeffect.bat"
)
CALL :WAITFORONE
IF %COUNT% LSS %CONVERSIONAMOUNT% (
	GOTO :SPLASHLOOP
)
CALL :WAITFORTHREE
CALL "%GURURESOURCEPATH%\gurudisplay.bat"
IF %CONVERSIONAMOUNT% EQU 1 (
	ECHO The Guru returns from the mystical WC room and hands you a !SPECIALITEM%CONVERSIONCHOICE%NAME!
) ELSE (
	ECHO The Guru returns from the mystical WC room and hands you %CONVERSIONAMOUNT% !SPECIALITEM%CONVERSIONCHOICE%NAME!s
)
ECHO.
IF !PLAYERSPECIALITEM%CONVERSIONCHOICE%NUM! EQU 1 (
	ECHO You now have 1 !SPECIALITEM%CONVERSIONCHOICE%NAME!^^!
) ELSE (
	ECHO You now have !PLAYERSPECIALITEM%CONVERSIONCHOICE%NUM! !SPECIALITEM%CONVERSIONCHOICE%NAME!s^^!
)
ECHO.
SET TEMPPATH=%MUSICPATH%\Chest
SET FILE=Chest.mp3
IF EXIST "%TEMPPATH%\%FILE%" (
	CALL "%MUSICPATH%\soundeffect.bat"
)
pause
IF %CONVERSIONCHOICE% EQU %CHOICE% (
	CALL :POINTLESS
)
GOTO :EOF

:POINTLESS
CALL "%GURURESOURCEPATH%\gurudisplay.bat"
ECHO An entirely pointless exercise and complete waste of %GILCOST% gil.
CALL :WAITFORONE
ECHO.
ECHO But you much prefer the fragrance of the new stones...
ECHO.
CALL :WAITFORFOUR
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF

:WAITFORFOUR
TIMEOUT /T 4 > nul
GOTO :EOF
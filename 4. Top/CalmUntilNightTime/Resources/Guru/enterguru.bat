IF %REMOTEACCESS% EQU 0 (
	CALL :SETLOCATION
) ELSE (
	SET /a REMOTEACCESS = 0
)
SET /a STORY28VERSIONNUM = 2
::IF %GURUWELCOMESEEN% EQU 0 (
::	CALL "%GURURESOURCEPATH%\guruwelcome.bat"
::)
:ENTERGURU
CALL "%GURURESOURCEPATH%\gurudisplay.bat"
ECHO The Guru looks at you but does not speak...
ECHO.
ECHO. 1 - Tell me how to earn skills
ECHO. 2 - Let me fight enemies
ECHO. 3 - Improve my weapons
ECHO. 4 - Stat stone conversion
ECHO. 0 - Bye bye Mr Guru
ECHO.
:CHOICE
SET /P GURUCHOICE=
ECHO.
IF "%GURUCHOICE%" EQU "1" (
	CALL :SKILLS
) ELSE IF "%GURUCHOICE%" EQU "2" (
	CALL "%GURURESOURCEPATH%\enemychoose.bat"
) ELSE IF "%GURUCHOICE%" EQU "3" (
	CALL :FIRE
) ELSE IF "%GURUCHOICE%" EQU "4" (
	CALL "%GURURESOURCEPATH%\statstoneconversion.bat"
) ELSE IF "%GURUCHOICE%" EQU "0" (
	CALL "%REMOTERESOURCEPATH%\guru.bat"
	GOTO :EOF
) ELSE (
	GOTO :CHOICE
)
GOTO :ENTERGURU

:SKILLS
SET /a SELECTED = 1
SET /a VARSELECTED = 0
CALL "%DYNAMICRESOURCEPATH%\dynamicguruskills.bat"
:MENUSTART
CLS
ECHO.
ECHO The Guru takes you into a room at the side of his guru hut.
ECHO.
CALL "%MISCRESOURCEPATH%\menu2.bat"
IF %VARSELECTED% EQU 1 (
	GOTO :EOF
)
IF %BACK% EQU 1 (
	SET /a BACK = 0
	GOTO :EOF
)
GOTO :MENUSTART

:FIRE
CALL :WEAPONCHECK
IF %DYNAMICCHOICE% EQU 0 (
	GOTO :EOF
)
IF %COUNT% EQU 0 (
	GOTO :EOF
)
CALL :SETONFIRE
GOTO :EOF

:WEAPONCHECK
::This could be run at the start of the game, it's just here for testing
SET /a FIREWEAPON1 = %ELDERWANDID%
SET /a FIREWEAPON2 = %GKNUCKLESID%
SET /a FIREWEAPON3 = %DRAGONBOWZID%
SET /a FIREWEAPON4 = %MACHETEID%
SET /a FIREWEAPON5 = %GANDSTAFFID%
SET /a FIREWEAPON6 = %MASAMUNEID%
SET /a GURUGIL = 50000
CALL "%GURURESOURCEPATH%\gurudisplay.bat"
ECHO The Guru holds one hand out for you to give him an upgradable weapon and
ECHO the other for you to give him %GURUGIL% gil.
ECHO.
ECHO Gil - %PLAYERGIL%
ECHO.
SET /a STATICCOUNT = 0
SET /a COUNT = 0
:WEAPONCHECKLOOP
SET /a STATICCOUNT = %STATICCOUNT% + 1
SET /a TEMP = !FIREWEAPON%STATICCOUNT%!
IF !PLAYERWEAPON%TEMP%NUM! GTR 0 (
	SET /a COUNT = %COUNT% + 1
)
IF !PLAYERWEAPON%TEMP%NUM! GTR 0 (
	SET DYNAMICWEAPON%COUNT%NAME=!WEAPON%TEMP%NAME!
	ECHO. %COUNT% - !WEAPON%TEMP%NAME!
)
IF %STATICCOUNT% LSS 6 (
	GOTO :WEAPONCHECKLOOP
)
IF %COUNT% EQU 0 (
	SET /a DYNAMICCHOICE = 0
	ECHO. You don't have any weapons that can be improved.
	ECHO.
	pause
	GOTO :EOF
)
ECHO. 0 - Back
ECHO.
:WEAPONCHOICE
SET /P DYNAMICCHOICE=
IF "%DYNAMICCHOICE%" EQU "" (
	GOTO :WEAPONCHOICE
) ELSE IF "%DYNAMICCHOICE%" EQU "0" (
	GOTO :EOF
)
IF %DYNAMICCHOICE% LSS 0 (
	GOTO :WEAPONCHOICE
) ELSE IF %DYNAMICCHOICE% GTR %COUNT% (
	GOTO :WEAPONCHOICE
)
CALL :GILCHECK
IF %GILCHECK% EQU 0 (
	SET /a DYNAMICCHOICE = 0
	GOTO :EOF
)
CALL :SETSTATICID
GOTO :EOF

:GILCHECK
IF %PLAYERGIL% LSS %GURUGIL% (
	CLS
	ECHO.
	ECHO You don't have enough money for this.
	ECHO.
	pause
	SET /a GILCHECK = 0
) ELSE (
	SET /a PLAYERGIL = %PLAYERGIL% - %GURUGIL%
	SET /a GILCHECK = 1
)
GOTO :EOF

:SETSTATICID
SET /a COUNT = 0
:LOOPSTART
SET /a COUNT = %COUNT% + 1
IF /I "!WEAPON%COUNT%NAME!" EQU "!DYNAMICWEAPON%DYNAMICCHOICE%NAME!" (
	SET /a CHOICE = %COUNT%
	GOTO :EOF
)
GOTO :LOOPSTART

:SETONFIRE
CALL "%GURURESOURCEPATH%\gurudisplay.bat"
ECHO The Guru takes your !WEAPON%CHOICE%NAME!, covers it in gasoline and sets it on fire.
ECHO.
CALL :WAITFORTHREE
ECHO He then hands it back to you.
ECHO.
CALL :WAITFORTWO
SET /a NEWID = %CHOICE% + 1
SET /a PLAYERWEAPON%NEWID%NUM = !PLAYERWEAPON%NEWID%NUM! + 1
SET /a PLAYERWEAPON%CHOICE%NUM = !PLAYERWEAPON%CHOICE%NUM! - 1
ECHO You have gained an !WEAPON%NEWID%NAME!^^!
ECHO.
CALL :WAITFORTWO
IF %PLAYERWEAPONID% EQU %CHOICE% (
	CALL :EQUIPWEAPON
)
GOTO :EOF


:EQUIPWEAPON
SET PLAYERWEAPON=!WEAPON%NEWID%NAME!
SET /a PLAYERWEAPONID = %NEWID%
SET /a PLAYEREQUIPSTR=!WEAPON%NEWID%STR!
SET /a PLAYEREQUIPMAG=!WEAPON%NEWID%MAG!
SET /a PLAYEREQUIPACC=!WEAPON%NEWID%ACC!
SET /a COUNT = 0
:EQUIPWEAPONLOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = !WEAPON%PLAYERWEAPONID%SKILL%COUNT%!
SET /a PLAYERSKILL%TEMP%ENABLED = 1
IF %COUNT% LSS !WEAPON%PLAYERWEAPONID%SKILLTOT! (
	GOTO :EQUIPWEAPONLOOP
)
GOTO :EOF

:SETLOCATION
IF /I %MAP% EQU TOWN6 (
	SET /a PLAYERXCOORD = 10
	SET /a PLAYERYCOORD = 5
)
GOTO :EOF

:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF
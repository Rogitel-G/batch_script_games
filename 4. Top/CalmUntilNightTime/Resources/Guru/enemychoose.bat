:FIGHT
CLS
ECHO.
ECHO Guru: ...
ECHO.
ECHO. 1 - %TOWN2NICKNAME%
ECHO. 2 - %CAVE1NICKNAME%
ECHO. 3 - %TOWN3NICKNAME%
ECHO. 4 - %MINE1NICKNAME%
ECHO. 5 - %TOWN4NICKNAME%
ECHO. 6 - %LAKE1NICKNAME%
ECHO. 7 - %FOREST1MIST0NICKNAME%
ECHO. 8 - %TOWN6NICKNAME%
IF %BEATRAGATA% EQU 1 (
	ECHO. 9 - %HELL1NICKNAME%
)
::ECHO. 10 - Misc
ECHO. 0 - Back
ECHO.
SET /P CHOICE=
IF "%CHOICE%" EQU "" (
	GOTO :FIGHT
) ELSE IF "%CHOICE%" EQU "0" (
	GOTO :EOF
)
IF %CHOICE% LSS 0 (
	GOTO :FIGHT
) ELSE IF %CHOICE% GTR 10 (
	GOTO :FIGHT
)
IF %CHOICE% EQU 9 (
	IF %BEATRAGATA% EQU 0 (
		GOTO :FIGHT
	)
)
CALL :GURUFIGHTLIST
GOTO :EOF

:GURUFIGHTLIST
CLS
ECHO.
ECHO You have %PLAYERGIL% Gil
ECHO.
SET /a GURUTEMPCOUNT = 0
:FIGHTLOOP
SET /a GURUTEMPCOUNT = %GURUTEMPCOUNT% + 1
SET /a ENEMYID = !GURUGROUP%CHOICE%ENEMY%GURUTEMPCOUNT%!
IF NOT DEFINED ENEMY%ENEMYID%LOADED (
	SET /a ENEMY%ENEMYID%LOADED = 1
	CALL "%MISCRESOURCEPATH%\loadenemy.bat"
)
IF %GURUTEMPCOUNT% LSS 10 (
	SET /a SPACENUM = 32 - !ENEMY%ENEMYID%NAMELENGTH!
) ELSE (
	SET /a SPACENUM = 31 - !ENEMY%ENEMYID%NAMELENGTH!
)
SET /a FIGHTGIL = !ENEMY%ENEMYID%GIL!*2
ECHO. %GURUTEMPCOUNT% - !ENEMY%ENEMYID%NAME!!SPACES%SPACENUM%!%FIGHTGIL% Gil
IF %GURUTEMPCOUNT% LSS !GURUGROUP%CHOICE%ENEMIES! (
	GOTO :FIGHTLOOP
)
IF !GURUGROUP%CHOICE%EXTRAS! EQU 0 (
	GOTO :NOEXTRAS
)
:FIGHTLOOP2
SET /a GURUTEMPCOUNT = %GURUTEMPCOUNT% + 1
SET /a GURUTEMPCOUNT2 = %GURUTEMPCOUNT% - !GURUGROUP%CHOICE%ENEMIES!
SET /a ENEMYID = !GURUGROUP%CHOICE%EXTRA%GURUTEMPCOUNT2%!
IF NOT DEFINED ENEMY%ENEMYID%LOADED (
	SET /a ENEMY%ENEMYID%LOADED = 1
	CALL "%MISCRESOURCEPATH%\loadenemy.bat"
)
IF %GURUTEMPCOUNT% LSS 10 (
	SET /a SPACENUM = 32 - !ENEMY%ENEMYID%NAMELENGTH!
) ELSE (
	SET /a SPACENUM = 31 - !ENEMY%ENEMYID%NAMELENGTH!
)
SET /a FIGHTGIL = !ENEMY%ENEMYID%GIL!*2
ECHO. %GURUTEMPCOUNT% - !ENEMY%ENEMYID%NAME!!SPACES%SPACENUM%!%FIGHTGIL% Gil
IF %GURUTEMPCOUNT2% LSS !GURUGROUP%CHOICE%EXTRAS! (
	GOTO :FIGHTLOOP2
)
:NOEXTRAS
ECHO. 0 - Back
ECHO.
SET /P CHOICE2=
ECHO.
IF "%CHOICE2%" EQU "" (
	GOTO :GURUFIGHTLIST
) ELSE IF "%CHOICE2%" EQU "0" (
	GOTO :EOF
)
IF %CHOICE2% GTR %GURUTEMPCOUNT% (
	GOTO :GURUFIGHTLIST
) ELSE IF %CHOICE2% LSS 0 (
	GOTO :GURUFIGHTLIST
)
CALL :SETSTATICID
CALL :SCANDETECT
CALL :BATTLE
GOTO :EOF

:SETSTATICID
IF %CHOICE2% GTR !GURUGROUP%CHOICE%ENEMIES! (
	CALL :EXTRAS
) ELSE (
	SET /a ENEMYID = !GURUGROUP%CHOICE%ENEMY%CHOICE2%!
)
GOTO :EOF

:EXTRAS
SET /a TEMP = %CHOICE2% - !GURUGROUP%CHOICE%ENEMIES!
SET /a ENEMYID = !GURUGROUP%CHOICE%EXTRA%TEMP%!
GOTO :EOF

:SCANDETECT
SET /a FIGHTGIL = !ENEMY%ENEMYID%GIL!*2
SET /a GURUSCAN = 0
SET /a GURUDETECT = 0
CLS
ECHO.
ECHO The Guru points at a picture of !ENEMY%ENEMYID%NAME!
ECHO.
ECHO The Guru then points at a picture of Scanning and Detecting in a questioning way... (use your imagination^^!)
ECHO.
ECHO. 1 - Scan       (!ENEMY%ENEMYID%GIL!)
ECHO. 2 - Detect     (!ENEMY%ENEMYID%GIL!)
ECHO. 0 - No thanks
ECHO.
SET /P CHOICE3=
IF "%CHOICE3%" EQU "1" (
	CALL :SCANCHECK
) ELSE IF "%CHOICE3%" EQU "2" (
	CALL :DETECTCHECK
) ELSE IF "%CHOICE3%" EQU "0" (
	GOTO :EOF
) ELSE (
	GOTO :SCANDETECT
)
GOTO :EOF

:SCANCHECK
IF %PLAYERASCAN% EQU 1 (
	CALL :SCANNED
) ELSE IF !ENEMY%ENEMYID%CAUGHT! EQU 1 (
	CALL :SCANNED
) ELSE (
	SET /a FIGHTGIL = %FIGHTGIL% + !ENEMY%ENEMYID%GIL!
	SET /a PLAYERASCAN = 1
	SET /a GURUSCAN = 1
)
GOTO :EOF

:DETECTCHECK
IF %PLAYERADETECT% EQU 1 (
	CALL :DETECTED
) ELSE IF !ENEMY%ENEMYID%CAUGHT! EQU 1 (
	CALL :DETECTED
) ELSE (
	SET /a FIGHTGIL = %FIGHTGIL% + !ENEMY%ENEMYID%GIL!
	SET /a PLAYERADETECT = 1
	SET /a GURUDETECT = 1
)
GOTO :EOF

:SCANNED
CLS
ECHO.
ECHO The Guru looks at you condescendingly.
ECHO.
CALL :WAITFORTWO
GOTO :EOF

:DETECTED
CLS
ECHO.
ECHO The Guru looks at you condescendingly.
ECHO.
CALL :WAITFORTWO
GOTO :EOF

:BATTLE
IF %PLAYERGIL% LSS %FIGHTGIL% (
	CLS
	ECHO.
	ECHO The Guru slaps you with his feet.
	CALL :WAITFORTWO
	GOTO :EOF
)
SET /a PLAYERGIL = %PLAYERGIL% - %FIGHTGIL%
IF %ENEMYID% GEQ %BOSSIDSTART% (
	SET /a SPECIALFIGHT = 1
) ELSE (
	SET /a SPECIALFIGHT = 0
)
SET /a TRAINBATTLE = 0
SET /a GURUBATTLE = 1
CALL "%BATTLERESOURCEPATH%\battle.bat"
IF %GURUSCAN% EQU 1 (
	SET /a PLAYERASCAN = 0
) ELSE IF %GURUDETECT% EQU 1 (
	SET /a PLAYERADETECT = 0
)
IF %ALREADYCAUGHT% EQU 1 (
	SET /a ENEMY%ENEMYID%CAUGHT = 1
)
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF
GOTO :START
::TEST
@ECHO OFF
SETLOCAL ENABLEDELAYEDEXPANSION
SET /a ENEMYMAXHP = 1000
SET /a ENEMYPERCHP = 42
SET /a ENEMY1SET1MORPHMINPERC = 40
SET /a ENEMY1SET1MORPHMAXNUM = 1


SET /a ENEMYHP = (%ENEMYMAXHP%*%ENEMYPERCHP%)/100
SET /a ENEMY1SET1MORPHID = 1
SET ENEMY1SET1MORPHTYPE=ITEM
SET /a ENEMYID = 1
SET /a REWARDSET = 1
SET ENEMYNAME=Enemy
SET /a GURUBATTLE = 0
SET /a SPECIALFIGHT = 0
SET /a PLAYERLCK = 20
SET /a PLAYERITEM1NUM = 0
SET ITEM1NAME=Item


:START
::IF %GURUBATTLE% EQU 0 (
::	IF %SPECIALFIGHT% EQU 1 (
::		CALL :CANTTRANSFORM
::		GOTO :EOF
::	)
::)
CALL :SETUPVARS
CALL :MORPHSUCCESSQUERY
IF %MORPHSUCCESS% EQU 1 (
	CALL :MORPHREWARD
)
GOTO :EOF


:SETUPVARS
SET /a MORPHSUCCESS = 0
SET /a ENEMYPERCHP = (%ENEMYHP%*100)/%ENEMYMAXHP%
SET /a PERCOFPERC = (%ENEMYPERCHP%*100)/!ENEMY%ENEMYID%SET%REWARDSET%MORPHMINPERC!
GOTO :EOF

:MORPHSUCCESSQUERY
::At luck 100, this will double the minimum percentage HP an enemy can be morphed with
SET /a TEMP = !ENEMY%ENEMYID%SET%REWARDSET%MORPHMINPERC! + ((%PLAYERLCK%*!ENEMY%ENEMYID%SET%REWARDSET%MORPHMINPERC!)/100)
IF %ENEMYPERCHP% GTR %TEMP% (
	CALL :FAIL
) ELSE (
::If we have a count of number of enemies morphed, add it here
	SET /a MORPHSUCCESS = 1
	SET /a ENDBATTLE = 1
)
GOTO :EOF

:MORPHREWARD
SET /a COUNT = 0
:LOOP
SET /a COUNT = %COUNT% + 1
SET /a TEMP = (100/!ENEMY%ENEMYID%SET%REWARDSET%MORPHMAXNUM!)*%COUNT%
IF %PERCOFPERC% LSS %TEMP% (
	CALL :SUCCESS
	GOTO :EOF
) ELSE IF %COUNT% LSS !ENEMY%ENEMYID%SET%REWARDSET%MORPHMAXNUM! (
	GOTO :LOOP
) ELSE (
	CALL :SUCCESS
)
GOTO :EOF

:SUCCESS
SET /a TEMP = (!ENEMY%ENEMYID%SET%REWARDSET%MORPHMAXNUM! - %COUNT%) + 1
::If a special is made that increases the number of items got through morphing then add something here to increase %TEMP%
SET TYPE=!ENEMY%ENEMYID%SET%REWARDSET%MORPHTYPE!
SET /a ID = !ENEMY%ENEMYID%SET%REWARDSET%MORPHID!
SET /a PLAYER%TYPE%%ID%NUM = !PLAYER%TYPE%%ID%NUM! + %TEMP%
SET /a ENDBATTLE = 1
SET /a WINBATTLE = 1
IF %TEMP% EQU 1 (
	ECHO You morphed the %ENEMYNAME% into %TEMP% !%TYPE%%ID%NAME!^^!
) ELSE (
	ECHO You morphed the %ENEMYNAME% into %TEMP% !%TYPE%%ID%NAME!s^^!
)
ECHO.
IF !PLAYER%TYPE%%ID%NUM! EQU 1 (
	ECHO You now have !PLAYER%TYPE%%ID%NUM! !%TYPE%%ID%NAME!
) ELSE (
	ECHO You now have !PLAYER%TYPE%%ID%NUM! !%TYPE%%ID%NAME!s
)
ECHO.
CALL :WAITFORTHREE
GOTO :EOF

:FAIL
ECHO You have failed to morph the %ENEMYNAME%
ECHO.
CALL :WAITFORTWO
GOTO :EOF

:CANTTRANSFORM
ECHO You cannot transform %ENEMYNAME% in this fight^^!
ECHO.
CALL :WAITFORTWO
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF

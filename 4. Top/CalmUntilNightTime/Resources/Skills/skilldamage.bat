CALL :INITIALISE
CALL :ELEMENTCHECK
CALL :DAMAGECALC
CALL :CRITICALQUERY
CALL :PROTECTQUERY
CALL :APPLYDAMAGE
CALL :ENDBATTLEQUERY
GOTO :EOF

:INITIALISE
SET /a WEAKNESSTRUE = 0
SET /a STRENGTHTRUE = 0
SET /a IMMUNETRUE = 0
SET /a ABSORBTRUE = 0
SET /a SKILLDAMABSORBDISPLAY = 0
SET /a PROTECTTRUE = 0
SET /a CRITICAL = 0
GOTO :EOF

:ELEMENTCHECK
IF /I %SKILLELEMENT% EQU NONE (
	GOTO :EOF
)
:WEAKNESSES
IF %ENEMYWEAKNESSNUM% EQU 0 (
	GOTO :STRENGTHS
)
SET /a COUNT = 0
:WEAKNESSLOOP
SET /a COUNT = %COUNT% + 1
IF /I %SKILLELEMENT% EQU !ENEMYWEAKNESS%COUNT%! (
	SET /a WEAKNESSTRUE = 1
)
IF %COUNT% LSS %ENEMYWEAKNESSNUM% (
	GOTO :WEAKNESSLOOP
)

:STRENGTHS
IF %ENEMYSTRENGTHNUM% EQU 0 (
	GOTO :IMMUNES
)
SET /a COUNT = 0
:STRENGTHLOOP
SET /a COUNT = %COUNT% + 1
IF /I %SKILLELEMENT% EQU !ENEMYSTRENGTH%COUNT%! (
	SET /a STRENGTHTRUE = 1
)
IF %COUNT% LSS %ENEMYSTRENGTHNUM% (
	GOTO :STRENGTHLOOP
)

:IMMUNES
IF %ENEMYELEMENTIMMUNENUM% EQU 0 (
	GOTO :ABSORBS
)
SET /a COUNT = 0
:IMMUNELOOP
SET /a COUNT = %COUNT% + 1
IF /I %SKILLELEMENT% EQU !ENEMYELEMENTIMMUNE%COUNT%! (
	SET /a IMMUNETRUE = 1
)
IF %COUNT% LSS %ENEMYELEMENTIMMUNENUM% (
	GOTO :IMMUNELOOP
)

:ABSORBS
IF %ENEMYABSORBNUM% EQU 0 (
	GOTO :EOF
)
SET /a COUNT = 0
:ABSORBLOOP
SET /a COUNT = %COUNT% + 1
IF /I %SKILLELEMENT% EQU !ENEMYABSORB%COUNT%! (
	SET /a ABSORBTRUE = 1
)
IF %COUNT% LSS %ENEMYABSORBNUM% (
	GOTO :ABSORBLOOP
)
GOTO :EOF


:DAMAGECALC
IF %IMMUNETRUE% EQU 1 (
	SET /a SKILLDAM = 0
	GOTO :EOF
)
IF %SKILLDAM% EQU 0 (
	SET /a SKILLDAM = 1
)
IF %WEAKNESSTRUE% EQU 1 (
	CALL :APPLYWEAKNESS
)
IF %STRENGTHTRUE% EQU 1 (
	CALL :APPLYSTRENGTH
)
IF %ABSORBTRUE% EQU 1 (
	CALL :APPLYABSORB
)
CALL :DAMAGELIM
GOTO :EOF

:DAMAGELIM
IF %PLAYERBREAKDAM% EQU 0 (
	IF %SKILLDAM% GTR %MAXDAM% (
		SET /a SKILLDAM = %MAXDAM%
	) 
)
GOTO :EOF


:APPLYWEAKNESS
SET /a SKILLDAM = %SKILLDAM%*2
GOTO :EOF

:APPLYSTRENGTH
SET /a SKILLDAM = %SKILLDAM%/2
GOTO :EOF

:APPLYABSORB
SET /a SKILLDAM = -%SKILLDAM%
GOTO :EOF

:CRITICALQUERY
IF %SKILLCRITICALCHANCE% EQU 0 (
	GOTO :EOF
) ELSE IF %IMMUNETRUE% EQU 1 (
	GOTO :EOF
)
SET /a RANCRITICALFACTOR=%RANDOM% %% %CRITICALHITFACTOR%
IF %PLAYERINCCRITCHANCE% EQU 1 (
	SET /a RANCRITICALFACTOR = %RANCRITICALFACTOR%/2
)
SET /a SKILLCRITICALCHANCE = (%SKILLCRITICALCHANCE%*10)/%ENEMYLCK%
IF %SKILLCRITICALCHANCE% GTR %RANCRITICALFACTOR% (
	CALL :CRITICALSUCCESS
)
GOTO :EOF

:CRITICALSUCCESS
SET /a CRITICAL = 1
SET /a CRITICALHITSUCCESSCOUNT = %CRITICALHITSUCCESSCOUNT% + 1
SET /a SKILLDAM = %SKILLDAM%*2
IF %PLAYERINCCRITDAM% EQU 1 (
	SET /a SKILLDAM = %SKILLDAM%*2
)
GOTO :EOF

:PROTECTQUERY
IF !SKILL%SKILLCHOICE%PROTECTABLE! EQU 1 (
	IF %ENEMYPROTECT% EQU 1 (
		SET /a SKILLDAM = %SKILLDAM%/2
		SET /a PROTECTTRUE = 1
	)
)
GOTO :EOF

:APPLYDAMAGE
IF %ABSORBTRUE% EQU 1 (
	SET /a SKILLDAMABSORBDISPLAY = -%SKILLDAM%
)
SET /a ENEMYHP = %ENEMYHP% - %SKILLDAM%
IF %CRITICAL% EQU 1 (
	ECHO Critical Hit^^!
	ECHO.
	CALL :WAITFORTWO
)
IF %IMMUNETRUE% EQU 1 (
	ECHO But the enemy is immune to %SKILLELEMENT% magic^^!
	ECHO.
	CALL :WAITFORTWO
	GOTO :EOF
)
IF %WEAKNESSTRUE% EQU 1 (
	ECHO It's super effective^^!
	ECHO.
	CALL :WAITFORTWO
)
IF %STRENGTHTRUE% EQU 1 (
	ECHO It's not very effective...
	ECHO.
	CALL :WAITFORTWO
)
IF %ABSORBTRUE% EQU 1 (
	ECHO %ENEMYNAME% absorbed the damage and converted it to HP^^!
	ECHO.
	CALL :WAITFORTWO
	ECHO %ENEMYNAME% was healed by %SKILLDAMABSORBDISPLAY% HP^^!
	ECHO.
	CALL :WAITFORTWO
	GOTO :EOF
)
IF %PROTECTTRUE% EQU 1 (
	ECHO Protect halved the effectiveness of the !SKILL%SKILLCHOICE%NAME!
	ECHO.
	CALL :WAITFORTWO
)
ECHO The !SKILL%SKILLCHOICE%NAME! did %SKILLDAM% damage to %ENEMYNAME%
ECHO.
CALL :WAITFORTWO
GOTO :EOF




:ENDBATTLEQUERY
IF !%DEFENDER%HP! LEQ 0 (
	SET /a %DEFENDER%HP = 0
	IF %DEFENDER% EQU ENEMY (
		SET /a WINBATTLE = 1
	)
) ELSE IF !%DEFENDER%HP! GTR !%DEFENDER%MAXHP! (
	SET /a %DEFENDER%HP = !%DEFENDER%MAXHP!
)
GOTO :EOF








:WAITFORZERO
TIMEOUT /T 0 > nul
GOTO :EOF

:WAITFORONE
TIMEOUT /T 1 > nul
GOTO :EOF

:WAITFORTWO
TIMEOUT /T 2 > nul
GOTO :EOF

:WAITFORTHREE
TIMEOUT /T 3 > nul
GOTO :EOF
